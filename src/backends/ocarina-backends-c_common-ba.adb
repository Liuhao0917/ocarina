------------------------------------------------------------------------------
--                                                                          --
--                           OCARINA COMPONENTS                             --
--                                                                          --
--         O C A R I N A . B A C K E N D S . C _ C O M M O N . B A          --
--                                                                          --
--                                 B o d y                                  --
--                                                                          --
--                    Copyright (C) 2019-2020 OpenAADL                      --
--                                                                          --
-- Ocarina  is free software; you can redistribute it and/or modify under   --
-- terms of the  GNU General Public License as published  by the Free Soft- --
-- ware  Foundation;  either version 3,  or (at your option) any later ver- --
-- sion. Ocarina is distributed in the hope that it will be useful, but     --
-- WITHOUT ANY WARRANTY; without even the implied warranty of               --
-- MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.                     --
--                                                                          --
-- As a special exception under Section 7 of GPL version 3, you are granted --
-- additional permissions described in the GCC Runtime Library Exception,   --
-- version 3.1, as published by the Free Software Foundation.               --
--                                                                          --
-- You should have received a copy of the GNU General Public License and    --
-- a copy of the GCC Runtime Library Exception along with this program;     --
-- see the files COPYING3 and COPYING.RUNTIME respectively.  If not, see    --
-- <http://www.gnu.org/licenses/>.                                          --
--                                                                          --
--                    Ocarina is maintained by OpenAADL team                --
--                              (info@openaadl.org)                         --
--                                                                          --
------------------------------------------------------------------------------

with Ada.Strings.Fixed;

with Ocarina.AADL_Values;
with Ocarina.Instances.Queries;
with Ocarina.Namet;
with Locations;
with Utils;
with Ocarina.Backends.Utils;

with Ocarina.Backends.Messages;
with Ocarina.Backends.C_Tree.Nodes;
with Ocarina.Backends.C_Tree.Nutils;
with Ocarina.Backends.C_Values;
with Ocarina.Backends.Properties;
with Ocarina.Backends.C_Common.Mapping;

with Ocarina.ME_AADL;
with Ocarina.ME_AADL.AADL_Tree.Nodes;
with Ocarina.ME_AADL_BA;
with Ocarina.ME_AADL.AADL_Instances.Nodes;
with Ocarina.ME_AADL.AADL_Instances.Nutils;

with Ocarina.ME_AADL_BA.BA_Tree.Nodes;
with Ocarina.ME_AADL_BA.BA_Tree.Nutils;
with Ocarina.Backends.Helper;
with Ocarina.Analyzer.AADL_BA;
with Ocarina.Backends.PO_HI_C.Runtime;
with Ocarina.Backends.C_Common.Types;

package body Ocarina.Backends.C_Common.BA is

   use Ocarina.Analyzer.AADL_BA;
   use Ocarina.Backends.C_Tree.Nutils;
   use Ocarina.Namet;
   use Locations;
   use Ocarina.Backends.Helper;
   use Ocarina.Backends.Messages;
   use Ocarina.Backends.Properties;
   use Ocarina.Backends.C_Tree.Nodes;
   use Ocarina.ME_AADL_BA;
   use Ocarina.ME_AADL_BA.BA_Tree.Nodes;
   use Ocarina.Backends.C_Common.Mapping;
   use Ocarina.Backends.Utils;
   use Ocarina.Backends.PO_HI_C.Runtime;
   use Ocarina.ME_AADL;

   package AAN renames Ocarina.ME_AADL.AADL_Tree.Nodes;
   package AIN renames Ocarina.ME_AADL.AADL_Instances.Nodes;
   package AINU renames Ocarina.ME_AADL.AADL_Instances.Nutils;
   package CTN renames Ocarina.Backends.C_Tree.Nodes;
   package CTU renames Ocarina.Backends.C_Tree.Nutils;
   package BATN renames Ocarina.ME_AADL_BA.BA_Tree.Nodes;
   package BANu renames Ocarina.ME_AADL_BA.BA_Tree.Nutils;
   package CV renames Ocarina.Backends.C_Values;

   --  function Get_Instances_Of_Component_Type
   --    (Root : Node_Id; E : Node_Id) return Node_Id;

   function Map_Used_Type (N : Node_Id) return Node_Id;

   procedure Map_C_Many_Transitions_Of_A_Thread
     (S            : Node_Id;
      Declarations : List_Id;
      Statements   : List_Id);

   function Compute_Nb_States
     (S            : Node_Id) return Unsigned_Long_Long;

   procedure Map_BA_States_To_C_Types
     (S            : Node_Id);

   function Compute_Nb_Trans_Stemmed_From_A_Specific_Complete_Stat
     (BA             : Node_Id;
      Complete_State : Node_Id) return Unsigned_Long_Long;

   procedure Fill_Nb_Dispatch_Triggers_Of_Each_Transition_Array
     (S               : Node_Id;
      BA              : Node_Id;
      Complete_State  : Node_Id;
      Index_Comp_Stat : Unsigned_Long_Long;
      WStatements     : List_Id);

   procedure Fill_Dispatch_Triggers_Of_All_Transitions_Array
     (S               : Node_Id;
      BA              : Node_Id;
      Complete_State  : Node_Id;
      Index_Comp_Stat : Unsigned_Long_Long;
      WStatements     : List_Id);

   function Nb_All_Dispatch_Triggers_From_A_Specific_Complete_Stat
     (BA             : Node_Id;
      Complete_State : Node_Id) return Unsigned_Long_Long;

   function Max_Dispatch_Triggers_Per_Trans_From_A_Specific_Complete_Stat
     (BA             : Node_Id;
      Complete_State : Node_Id) return Unsigned_Long_Long;

   procedure Make_States_Initialization_Function
     (S            : Node_Id);

   procedure Make_Update_Next_Complete_State_Function
     (S            : Node_Id);

   function Search_State_Kind
     (BA        : Node_Id;
      State_Idt : Node_Id) return Ocarina.Types.Byte;

   function Find_Index_In_States_Array
     (BA        : Node_Id;
      State_Idt : Node_Id) return Unsigned_Long_Long;

   procedure Update_Current_State
     (E                : Node_Id;
      BA               : Node_Id;
      Transition_Node  : Node_Id;
      Stats            : List_Id);

   function Map_C_Transition_Node
     (Node             : Node_Id;
      S                : Node_Id;
      Declarations     : List_Id;
      Statements       : List_Id;
      Else_St          : List_Id) return List_Id;

   function Map_C_On_Dispatch_Transition_Node
     (Node             : Node_Id;
      S                : Node_Id;
      Declarations     : List_Id;
      Statements       : List_Id;
      Index_Transition : in out Unsigned_Long_Long) return List_Id;

   function Has_Higher_Transition_Priority
     (Left, Right : Node_Id) return Boolean;
   --  Explicit priorities precede unspecified priorities. Equal priorities
   --  retain their declaration order.

   procedure Sort_Transitions_By_Priority (Transition_List : List_Id);
   --  Stably sort a collected transition list in descending priority order.

   procedure Map_C_A_List_Of_Transitions
     (S                         : Node_Id;
      BA                        : Node_Id;
      Otherwise_Transition_Node : Node_Id;
      Sub_Transition_List       : List_Id;
      WDeclarations             : List_Id;
      WStatements               : List_Id);

   procedure Map_C_A_List_Of_On_Dispatch_Transitions
     (S                            : Node_Id;
      BA                           : Node_Id;
      Sub_Transition_List          : List_Id;
      WDeclarations                : List_Id;
      WStatements                  : List_Id);

   procedure Examine_Current_State_Until_Reaching_Complete_State
     (S                         : Node_Id;
      BA                        : Node_Id;
      WDeclarations             : List_Id;
      WStatements               : List_Id);

   procedure Make_BA_Initialization_Function
     (S            : Node_Id);

   procedure Map_C_Implementation_of_BA_Body_Function
     (S            : Node_Id;
      Declarations : List_Id;
      Statements   : List_Id);

   procedure Make_BA_Body_Function_For_Periodic_Thread
     (S            : Node_Id;
      Declarations : List_Id;
      Statements   : List_Id);

   procedure Make_BA_Body_Function_For_Sporadic_Thread
     (S            : Node_Id;
      Declarations : List_Id;
      Statements   : List_Id);

   function Map_C_State_Kind_Name
     (State_Kind : Ocarina.Types.Byte) return Node_Id;

   procedure Map_C_Behavior_Action_Block
     (Node         : Node_Id;
      S            : Node_Id;
      Declarations : List_Id;
      Statements   : List_Id);

   procedure Map_C_Behav_Acts
     (Node         : Node_Id;
      S            : Node_Id;
      Declarations : List_Id;
      WStatements  : List_Id);

   procedure Map_C_Behavior_Action
     (Node         : Node_Id;
      S            : Node_Id;
      Declarations : List_Id;
      Statements   : List_Id);

   function Map_C_Elsif_stat
     (Node         : Node_Id;
      S            : Node_Id;
      Declarations : List_Id;
      Statements   : List_Id;
      Else_st      : Node_Id) return List_Id;

   procedure Map_C_If_Cond_Struct
     (Node         : Node_Id;
      S            : Node_Id;
      Declarations : List_Id;
      Statements   : List_Id);

   procedure Map_C_For_or_ForAll_Cond_Struct
     (Node         : Node_Id;
      S            : Node_Id;
      Declarations : List_Id;
      Statements   : List_Id);

   procedure Map_C_While_Cond_Struct
     (Node         : Node_Id;
      S            : Node_Id;
      Declarations : List_Id;
      Statements   : List_Id);

   procedure Map_C_DoUntil_Cond_Struct
     (Node         : Node_Id;
      S            : Node_Id;
      Declarations : List_Id;
      Statements   : List_Id);

   procedure Map_C_Communication_Action
     (Node         : Node_Id;
      S            : Node_Id;
      Declarations : List_Id;
      Statements   : List_Id);

   procedure Make_Send_Output_Port
     (Node       : Node_Id;
      S          : Node_Id;
      Statements : List_Id);

   procedure Make_Put_Value_On_port
     (Node         : Node_Id;
      S            : Node_Id;
      Declarations : List_Id;
      Statements   : List_Id);

   procedure Make_Output_Port_Name
     (Node         : Node_Id;
      S            : Node_Id;
      Statements   : List_Id);

   function Map_C_Data_Component_Reference
     (Node             : Node_Id;
      Is_Out_Parameter : Boolean := False;
      S                : Node_Id;
      Declarations     : List_Id;
      Statements       : List_Id) return Node_Id;

   function Map_C_BA_Name
     (Node             : Node_Id;
      Is_Out_Parameter : Boolean := False;
      S                : Node_Id;
      Declarations     : List_Id;
      Statements       : List_Id) return Node_Id;

   function Map_C_Target
     (Node         : Node_Id;
      S            : Node_Id;
      Declarations : List_Id;
      Statements   : List_Id) return Node_Id;

   procedure Map_C_Assignment_Action
     (Node         : Node_Id;
      S            : Node_Id;
      Declarations : List_Id;
      Statements   : List_Id);

   function Evaluate_BA_Value_Expression
     (Node                 : Node_Id;
      Is_Out_parameter     : Boolean := False;
      Subprogram_Root      : Node_Id := No_Node;
      Declarations         : List_Id;
      Statements           : List_Id;
      Is_Put_Value_On_Port : Boolean := False) return Node_Id;

   function Evaluate_BA_Relation
     (Node                 : Node_Id;
      Is_Out_Parameter     : Boolean := False;
      Subprogram_Root      : Node_Id := No_Node;
      Declarations         : List_Id;
      Statements           : List_Id;
      Is_Put_Value_On_Port : Boolean := False) return Node_Id;

   function Evaluate_BA_Operator (Node : Node_Id) return Operator_Type;

   function Evaluate_BA_Simple_Expression
     (Node                 : Node_Id;
      Is_Out_Parameter     : Boolean := False;
      Subprogram_Root      : Node_Id := No_Node;
      Declarations         : List_Id;
      Statements           : List_Id;
      Is_Put_Value_On_Port : Boolean := False) return Node_Id;

   function Evaluate_BA_Term
     (Node                 : Node_Id;
      Is_Out_Parameter     : Boolean := False;
      Subprogram_Root      : Node_Id := No_Node;
      Declarations         : List_Id;
      Statements           : List_Id;
      Is_Put_Value_On_Port : Boolean := False) return Node_Id;

   function Evaluate_BA_Factor
     (Node                 : Node_Id;
      Is_Out_Parameter     : Boolean := False;
      Subprogram_Root      : Node_Id := No_Node;
      Declarations         : List_Id;
      Statements           : List_Id;
      Is_Put_Value_On_Port : Boolean := False) return Node_Id;

   function Evaluate_BA_Value
     (Node                 : Node_Id;
      Is_Out_Parameter     : Boolean := False;
      Subprogram_Root      : Node_Id := No_Node;
      Declarations         : List_Id;
      Statements           : List_Id;
      Is_Put_Value_On_Port : Boolean := False) return Node_Id;

   function Evaluate_BA_Integer_Value
     (Node         : Node_Id;
      S            : Node_Id;
      Declarations : List_Id;
      Statements   : List_Id) return Node_Id;

   function Evaluate_BA_Literal (Node : Node_Id) return Node_Id;

   procedure Make_Intermediate_Variable_Declaration
     (N            : Name_Id;
      Used_Type    : Node_Id;
      Declarations : List_Id);

   function Get_Port_Spec_Instance
     (Node             : Node_Id;
      Parent_Component : Node_Id) return Node_Id;

   function Get_Subcomponent_Data_Instance
     (Node             : Node_Id;
      Parent_Component : Node_Id) return Node_Id;

   function Evaluate_BA_Value_Variable
     (Node             : Node_Id;
      S                : Node_Id;
      Declarations     : List_Id;
      Statements       : List_Id) return Node_Id;

   function Evaluate_BA_Property_Constant
     (Node                 : Node_Id;
      Is_Out_parameter     : Boolean := False;
      Subprogram_Root      : Node_Id;
      Declarations         : List_Id;
      Statements           : List_Id;
      Is_Put_Value_On_Port : Boolean := False) return Node_Id;

   function Make_Call_Parameter_For_Get_Count_and_Next_Value
     (Node : Node_Id;
      S    : Node_Id) return List_Id;

   function Make_Get_Count_of_Port
     (Node             : Node_Id;
      S                : Node_Id) return Node_Id;

   procedure Make_Next_Value_of_Port
     (Node             : Node_Id;
      S                : Node_Id;
      Statements       : List_Id);

   function Make_Request_Variable_Name_From_Port_Name
     (Port_Name : Name_Id) return Name_Id;

   procedure Make_Request_Variable_Declaration
     (Declarations : List_Id;
      Port_Name    : Name_Id);

   function Make_Get_Value_of_Port
     (Node             : Node_Id;
      Subprogram_Root  : Node_Id;
      Declarations     : List_Id;
      Statements       : List_Id) return Node_Id;

   function Evaluate_BA_Identifier
     (Node                 : Node_Id;
      Is_Out_Parameter     : Boolean := False;
      Subprogram_Root      : Node_Id := No_Node;
      Declarations         : List_Id;
      Statements           : List_Id;
      Is_Put_Value_On_Port : Boolean := False) return Node_Id;

   function Evaluate_BA_Boolean_Literal (Node : Node_Id) return Node_Id;

   --     -------------------------------------
   --     -- Get_Instances_Of_Component_Type --
   --     -------------------------------------
   --
   --     function Get_Instances_Of_Component_Type
   --       (Root : Node_Id; E : Node_Id) return Node_Id
   --     is
   --        use type Aan.Node_Kind;
   --
   --        pragma Assert
   --          (AAN.Kind (E) = Aan.K_Component_Type
   --           or else AAN.Kind (E) = Aan.K_Component_Implementation
   --           or else AAN.Kind (E) = Aan.K_Feature_Group_Type);
   --
   --        Fs : constant Ocarina.ME_AADL.AADL_Instances.Nutils.Node_Array
   --          := Features_Of (Root);
   --     begin
   --        for F of Fs loop
   --           if AIN.Corresponding_Declaration
   --             (AIN.Corresponding_Instance (F)) = E
   --           then
   --              return AIN.Corresponding_Instance (F);
   --           end if;
   --
   --        end loop;
   --
   --        --  raise Program_Error;
   --        return No_Node;
   --
   --     end Get_Instances_Of_Component_Type;

   --  Only integer expressions used by mod need this target-side type
   --  selection.  Compare maximum nonnegative values after promotion: this
   --  preserves both range and signedness without assuming a host int size.
   type BA_Integer_Type is record
      Type_Name    : Name_Id := No_Name;
      Maximum_Name : Name_Id := No_Name;
      Is_Promoted  : Boolean := False;
   end record;

   type BA_Integer_Iterator;
   type BA_Integer_Iterator_Access is access all BA_Integer_Iterator;
   type BA_Integer_Iterator is record
      Iterator_Name : Name_Id;
      Data_Instance : Node_Id;
      Previous      : BA_Integer_Iterator_Access;
   end record;
   Current_Integer_Iterator : BA_Integer_Iterator_Access := null;

   function Integer_Type_Of
     (Node : Node_Id; Subprogram_Root : Node_Id) return BA_Integer_Type;

   function Integer_Type_Info
     (Type_Spelling, Maximum_Spelling : String;
      Is_Promoted : Boolean := False) return BA_Integer_Type
   is
   begin
      return (Get_String_Name (Type_Spelling),
              Get_String_Name (Maximum_Spelling), Is_Promoted);
   end Integer_Type_Info;

   function Integer_Type_Index (Value : Name_Id) return String is
      Spelling : constant String := Name_Id'Image (Value);
   begin
      return Spelling (Spelling'First + 1 .. Spelling'Last);
   end Integer_Type_Index;

   function Emit_Integer_Type
     (Alias_Spelling, Selection : String) return BA_Integer_Type
   is
      Marker : constant Name_Id := Get_String_Name
        (Alias_Spelling & "_defined");
      Existing_Node : Node_Id := CTN.First_Node
        (CTN.Declarations (Current_File));
   begin
      while Present (Existing_Node) loop
         if CTN.Kind (Existing_Node) = CTN.K_Define_Statement
           and then CTN.Name (CTN.Defining_Identifier (Existing_Node)) = Marker
         then
            return Integer_Type_Info
              (Alias_Spelling, Alias_Spelling & "_MAX", Is_Promoted => True);
         end if;
         Existing_Node := CTN.Next_Node (Existing_Node);
      end loop;
      Add_Include (Make_Include_Clause
        (Make_Defining_Identifier (Get_String_Name ("limits"), False),
         Local => False));
      Add_Include (Make_Include_Clause
        (Make_Defining_Identifier (Get_String_Name ("stdint"), False),
         Local => False));
      Append_Node_To_List
        (Message_Comment
           ("Select the integer range and signedness on the target C "
            & "compiler; the generator's native integer size is irrelevant."),
         CTN.Declarations (Current_File));
      --  The existing C tree has no #if-expression node.  Keep this small
      --  standard-C99 declaration template separate from runtime expressions.
      --  The marker also deduplicates declarations within the current source.
      Append_Node_To_List
        (Make_Define_Statement
           (Make_Defining_Identifier (Marker, False),
            Make_Defining_Identifier
              (Get_String_Name ("1" & ASCII.LF & Selection), False)),
         CTN.Declarations (Current_File));
      return Integer_Type_Info
        (Alias_Spelling, Alias_Spelling & "_MAX", Is_Promoted => True);
   end Emit_Integer_Type;

   function Integer_Type_Branch
     (Alias_Spelling : String; Info : BA_Integer_Type) return String
   is
   begin
      return "typedef " & Get_Name_String (Info.Type_Name) & " "
        & Alias_Spelling & ";" & ASCII.LF
        & "#define " & Alias_Spelling & "_MAX "
        & Get_Name_String (Info.Maximum_Name) & ASCII.LF;
   end Integer_Type_Branch;

   function Promote_Integer_Type
     (Info : BA_Integer_Type) return BA_Integer_Type
   is
      Alias_Spelling : constant String := "ocarina_ba_promoted_"
        & Integer_Type_Index (Info.Type_Name);
   begin
      --  A common type is already promoted. Reusing it keeps consecutive
      --  mod operations on one helper instead of adding redundant aliases.
      if Info.Is_Promoted then
         return Info;
      elsif Info.Maximum_Name = Get_String_Name ("INT_MAX") then
         return Integer_Type_Info ("int", "INT_MAX", Is_Promoted => True);
      end if;
      return Emit_Integer_Type
        (Alias_Spelling,
         "#if " & Get_Name_String (Info.Maximum_Name) & " <= INT_MAX"
         & ASCII.LF
         & Integer_Type_Branch
           (Alias_Spelling, Integer_Type_Info ("int", "INT_MAX"))
         & "#else" & ASCII.LF
         & Integer_Type_Branch (Alias_Spelling, Info)
         & "#endif");
   end Promote_Integer_Type;

   function Common_Integer_Type
     (Left, Right : BA_Integer_Type) return BA_Integer_Type
   is
      Left_Info : constant BA_Integer_Type := Promote_Integer_Type (Left);
      Right_Info : constant BA_Integer_Type := Promote_Integer_Type (Right);
      Alias_Spelling : constant String := "ocarina_ba_common_"
        & Integer_Type_Index (Left_Info.Type_Name) & "_"
        & Integer_Type_Index (Right_Info.Type_Name);
   begin
      if Left_Info.Maximum_Name = Right_Info.Maximum_Name then
         return Left_Info;
      end if;
      --  Positive maxima can be compared by the preprocessor without signed
      --  overflow.  The larger range is exactly the usual arithmetic result's
      --  range after promotion, including signed-wide/unsigned-narrow pairs.
      return Emit_Integer_Type
        (Alias_Spelling,
         "#if " & Get_Name_String (Left_Info.Maximum_Name) & " >= "
         & Get_Name_String (Right_Info.Maximum_Name) & ASCII.LF
         & Integer_Type_Branch (Alias_Spelling, Left_Info)
         & "#else" & ASCII.LF
         & Integer_Type_Branch (Alias_Spelling, Right_Info)
         & "#endif");
   end Common_Integer_Type;

   function Declared_Integer_Type
     (Data_Instance, Source_Node : Node_Id) return BA_Integer_Type
   is
      Data_Size : Size_Type;
      Byte_Count : Unsigned_Long_Long;
      Width : Natural;
   begin
      if Present (Data_Instance)
        and then Get_Data_Representation (Data_Instance) = Data_Integer
      then
         Data_Size := Get_Data_Size (Data_Instance);
         if Data_Size.S = 0 then
            --  Match C_Common.Types: unspecified size maps to signed int,
            --  even when Number_Representation says unsigned.
            return Integer_Type_Info ("int", "INT_MAX");
         end if;
         Byte_Count := To_Bytes (Data_Size);
         if Byte_Count = 1 or else Byte_Count = 2
           or else Byte_Count = 4 or else Byte_Count = 8
         then
            Width := Natural (Byte_Count) * 8;
            declare
               Width_Image : constant String := Natural'Image (Width);
               Width_Text : constant String := Width_Image
                 (Width_Image'First + 1 .. Width_Image'Last);
               Maximum_Prefix : constant String :=
                 (if Get_Number_Representation (Data_Instance) = Signed
                  then "INT" else "UINT");
            begin
               return
                 (CTN.Name (Map_C_Data_Type_Designator (Data_Instance)),
                  Get_String_Name (Maximum_Prefix & Width_Text & "_MAX"),
                  False);
            end;
         end if;
      end if;
      Display_Located_Error
        (BATN.Loc (Source_Node),
         "Cannot determine the C integer type of this mod operand",
         Fatal => True);
      return (No_Name, No_Name, False);
   end Declared_Integer_Type;

   function Integer_Name_Instance
     (Node, Subprogram_Root : Node_Id;
      Owner : Node_Id := No_Node) return Node_Id
   is
      Variable_Node, Data_Instance, Item : Node_Id := No_Node;
      Items : List_Id := No_List;
      Iterator : BA_Integer_Iterator_Access := Current_Integer_Iterator;

      function Same_Name (Actual_Name : Name_Id) return Boolean is
      begin
         return Standard.Utils.To_Lower (Actual_Name) =
           Standard.Utils.To_Lower (BATN.Display_Name (Node));
      end Same_Name;
   begin
      case BATN.Kind (Node) is
         when BATN.K_Identifier =>
            if Present (Owner) then
               for Field of Subcomponents_Of (Owner) loop
                  if Same_Name (AIN.Display_Name (AIN.Identifier (Field))) then
                     return AIN.Corresponding_Instance (Field);
                  end if;
               end loop;
            else
               while Iterator /= null loop
                  if Same_Name (Iterator.Iterator_Name) then
                     return Iterator.Data_Instance;
                  end if;
                  Iterator := Iterator.Previous;
               end loop;
               Variable_Node := Find_BA_Variable
                 (Node, Get_Behavior_Specification (Subprogram_Root));
               if Present (Variable_Node) then
                  return AAN.Default_Instance
                    (BATN.Corresponding_Declaration
                       (BATN.Classifier_Ref (Variable_Node)));
               end if;
               for Feature of Features_Of (Subprogram_Root) loop
                  if Same_Name (AIN.Display_Name (AIN.Identifier (Feature)))
                  then
                     return AIN.Corresponding_Instance (Feature);
                  end if;
               end loop;
               for Field of Subcomponents_Of (Subprogram_Root) loop
                  if Same_Name (AIN.Display_Name (AIN.Identifier (Field))) then
                     return AIN.Corresponding_Instance (Field);
                  end if;
               end loop;
            end if;
         when BATN.K_Name =>
            Items := BATN.Idt (Node);
         when BATN.K_Data_Component_Reference =>
            Items := BATN.Identifiers (Node);
         when others =>
            null;
      end case;
      Data_Instance := Owner;
      if not BANu.Is_Empty (Items) then
         Item := BATN.First_Node (Items);
         while Present (Item) loop
            Data_Instance := Integer_Name_Instance
              (Item, Subprogram_Root, Data_Instance);
            exit when No (Data_Instance);
            Item := BATN.Next_Node (Item);
         end loop;
         if Present (Data_Instance) and then BATN.Kind (Node) = BATN.K_Name
           and then not BANu.Is_Empty (BATN.Array_Index (Node))
         then
            Item := BATN.First_Node (BATN.Array_Index (Node));
            while Present (Item) loop
               if Get_Data_Representation (Data_Instance) /= Data_Array
                 or else No (Get_Base_Type (Data_Instance))
               then
                  return No_Node;
               end if;
               --  Consume this classifier's dimensions before following its
               --  element type, which may itself be another array classifier.
               declare
                  Dimensions : constant ULL_Array :=
                    Get_Dimension (Data_Instance);
               begin
                  if Dimensions'Length = 0 then
                     return No_Node;
                  end if;
                  for Dimension in Dimensions'Range loop
                     if No (Item) then
                        return No_Node;
                     end if;
                     Item := BATN.Next_Node (Item);
                  end loop;
               end;
               Data_Instance := AAN.Entity
                 (AAN.First_Node (Get_Base_Type (Data_Instance)));
            end loop;
         end if;
         return Data_Instance;
      end if;
      return No_Node;
   end Integer_Name_Instance;

   function Literal_Integer_Type (Node : Node_Id) return BA_Integer_Type is
      use type Ocarina.AADL_Values.Literal_Type;
      Literal_Value : constant Ocarina.AADL_Values.Value_Type :=
        Ocarina.AADL_Values.Value (BATN.Value (Node));
      Magnitude_Image : constant String := Unsigned_Long_Long'Image
        (Literal_Value.IVal);
      Magnitude : constant String := Magnitude_Image
        (Magnitude_Image'First + 1 .. Magnitude_Image'Last);
      Based : constant Boolean := not Literal_Value.ISign
        and then (Literal_Value.IBase = 8 or else Literal_Value.IBase = 16);
      Alias_Spelling : constant String := "ocarina_ba_literal_"
        & (if Based then "based_" else "decimal_") & Magnitude;

      function Candidate
        (Directive, Type_Spelling, Maximum_Spelling : String) return String
      is
      begin
         return Directive & " " & Magnitude & "ULL <= " & Maximum_Spelling
           & ASCII.LF & Integer_Type_Branch
             (Alias_Spelling,
              Integer_Type_Info (Type_Spelling, Maximum_Spelling));
      end Candidate;
   begin
      if Literal_Value.T /= Ocarina.AADL_Values.LT_Integer then
         Display_Located_Error
           (BATN.Loc (Node), "mod requires an integer operand", Fatal => True);
      end if;
      --  Every C99 int represents these values; avoid aliases for small
      --  constants.  A leading unary minus does not change the token type.
      if Literal_Value.IVal <= 32767 then
         return Integer_Type_Info ("int", "INT_MAX", Is_Promoted => True);
      end if;
      return Emit_Integer_Type
        (Alias_Spelling,
         Candidate ("#if", "int", "INT_MAX")
         & (if Based then Candidate ("#elif", "unsigned int", "UINT_MAX")
            else "")
         & Candidate ("#elif", "long", "LONG_MAX")
         & (if Based then Candidate ("#elif", "unsigned long", "ULONG_MAX")
            else "")
         & Candidate ("#elif", "long long", "LLONG_MAX")
         & "#else" & ASCII.LF
         & Integer_Type_Branch
           (Alias_Spelling,
            Integer_Type_Info ("unsigned long long", "ULLONG_MAX"))
         & "#endif");
      --  The final unsigned candidate also matches the existing compiler
      --  extension for oversized decimal tokens.  Their original spelling
      --  remains the responsibility of the existing literal generator.
   end Literal_Integer_Type;

   function Integer_Type_Of
     (Node : Node_Id; Subprogram_Root : Node_Id) return BA_Integer_Type
   is
      use type Ocarina.AADL_Values.Literal_Type;

      function Resolve
        (Operand_Node : Node_Id;
         Power_Base : Boolean := False;
         Negate : Boolean := False) return BA_Integer_Type
      is
         Items : List_Id := No_List;
         Item : Node_Id;
         Result_Info : BA_Integer_Type;
         Pending_Operator : Operator_Type := Op_None;
         Has_Left : Boolean := False;
      begin
         case BATN.Kind (Operand_Node) is
            when BATN.K_Literal =>
               if Power_Base then
                  declare
                     Literal_Value : constant Ocarina.AADL_Values.Value_Type :=
                       Ocarina.AADL_Values.Value (BATN.Value (Operand_Node));
                  begin
                     if Literal_Value.T = Ocarina.AADL_Values.LT_Integer then
                        --  Bug 1 deliberately gives power literals an
                        --  explicit 64-bit base type; preserve that return.
                        if Literal_Value.ISign /= Negate
                          or else Literal_Value.IVal <= 2 ** 63 - 1
                        then
                           return Integer_Type_Info ("int64_t", "INT64_MAX");
                        else
                           return Integer_Type_Info
                             ("uint64_t", "UINT64_MAX");
                        end if;
                     end if;
                  end;
               else
                  return Literal_Integer_Type (Operand_Node);
               end if;
            when BATN.K_Identifier | BATN.K_Name |
                 BATN.K_Data_Component_Reference =>
               return Declared_Integer_Type
                 (Integer_Name_Instance (Operand_Node, Subprogram_Root),
                  Operand_Node);
            when BATN.K_Property_Constant =>
               return Resolve
                 (BATN.Identifier (Operand_Node), Power_Base, Negate);
            when BATN.K_Integer_Value =>
               return Resolve (BATN.Entity (Operand_Node), Power_Base, Negate);
            when BATN.K_Value_Variable =>
               if BATN.Is_Count (Operand_Node) then
                  --  Evaluate_BA_Value_Variable stores count in int16_t.
                  return Integer_Type_Info ("int16_t", "INT16_MAX");
               end if;
               return Resolve
                 (BATN.Identifier (Operand_Node), Power_Base, Negate);
            when BATN.K_Boolean_Literal =>
               return Integer_Type_Info ("int", "INT_MAX");
            when BATN.K_Factor =>
               if BATN.Is_Not (Operand_Node) then
                  return Integer_Type_Info ("int", "INT_MAX");
               end if;
               if Present (BATN.Upper_Value (Operand_Node)) then
                  --  An outer unary minus does not change a power helper's
                  --  declared return type. Only signs inside its base matter.
                  return Resolve
                    (BATN.Lower_Value (Operand_Node), Power_Base => True);
               end if;
               return Resolve
                 (BATN.Lower_Value (Operand_Node), Power_Base, Negate);
            when BATN.K_Value_Expression =>
               Items := BATN.Relations (Operand_Node);
            when BATN.K_Relation =>
               Items := BATN.Simple_Exprs (Operand_Node);
            when BATN.K_Simple_Expression =>
               Items := BATN.Term_And_Operator (Operand_Node);
            when BATN.K_Term =>
               Items := BATN.Factors (Operand_Node);
            when others =>
               null;
         end case;
         if not BANu.Is_Empty (Items) then
            Item := BATN.First_Node (Items);
            while Present (Item) loop
               if BATN.Kind (Item) = BATN.K_Operator then
                  Pending_Operator := Evaluate_BA_Operator (Item);
               elsif not Has_Left then
                  Result_Info := Resolve
                    (Item, Power_Base,
                     Negate /= (Pending_Operator = Op_Minus));
                  if Pending_Operator /= Op_None and then not Power_Base then
                     Result_Info := Promote_Integer_Type (Result_Info);
                  end if;
                  Has_Left := True;
                  Pending_Operator := Op_None;
               else
                  case Pending_Operator is
                     when Op_Plus | Op_Minus | Op_Asterisk | Op_Slash |
                          Op_Modulo =>
                        Result_Info := Common_Integer_Type
                          (Result_Info, Resolve (Item));
                     when Op_And | Op_Or | Op_Equal_Equal | Op_Not_Equal |
                          Op_Less | Op_Less_Equal | Op_Greater |
                          Op_Greater_Equal =>
                        Result_Info := Integer_Type_Info ("int", "INT_MAX");
                     when others =>
                        Display_Located_Error
                          (BATN.Loc (Item),
                           "Cannot determine integer expression type for mod",
                           Fatal => True);
                  end case;
                  Pending_Operator := Op_None;
               end if;
               Item := BATN.Next_Node (Item);
            end loop;
            if Has_Left then
               return Result_Info;
            end if;
         end if;
         Display_Located_Error
           (BATN.Loc (Operand_Node),
            "Cannot determine the C integer type of this mod operand",
            Fatal => True);
         return (No_Name, No_Name, False);
      end Resolve;
   begin
      return Resolve (Node);
   end Integer_Type_Of;

   ------------------------------
   -- Is_To_Make_Init_Sequence --
   ------------------------------

   function Is_To_Make_Init_Sequence (S : Node_Id) return Boolean
   is
      BA     : Node_Id;
      State  : Node_Id;
      Result : Boolean := False;
   begin
      BA := Get_Behavior_Specification (S);
      State := BATN.First_Node (BATN.States (BA));

      while Present (State) loop

         Result := Behavior_State_Kind'Val (BATN.State_Kind (State))
           = BSK_Initial;

         exit when Result;
         State := BATN.Next_Node (State);
      end loop;
      return Result;
   end Is_To_Make_Init_Sequence;

   --------------------------------
   -- Get_Behavior_Specification --
   --------------------------------

   function Get_Behavior_Specification
     (S : Node_Id) return Node_Id is
      D, BA  : Node_Id;
   begin
      D := AIN.First_Node (AIN.Annexes (S));
      BA := No_Node;
      while No (BA) loop
         if (Standard.Utils.To_Upper (AIN.Display_Name (AIN.Identifier (D))) =
               Standard.Utils.To_Upper (Get_String_Name
                                          ("behavior_specification")))
           and then Present (AIN.Corresponding_Annex (D))
         then
            BA := AIN.Corresponding_Annex (D);
            return BA;
         end if;
         D := AIN.Next_Node (D);
      end loop;

      raise Program_Error;

      return No_Node;
   end Get_Behavior_Specification;

   -------------------
   -- Map_Used_Type --
   -------------------

   function Map_Used_Type (N : Node_Id) return Node_Id
   is
      Data_Instance : Node_Id;
   begin
      Data_Instance := AAN.Default_Instance (N);

      if No (AIN.Backend_Node (AIN.Identifier (Data_Instance))) then
         Ocarina.Backends.C_Common.Types.Header_File.Visit (Data_Instance);
      end if;

      return Map_C_Data_Type_Designator (Data_Instance);

   end Map_Used_Type;

   ------------------------------
   -- Map_C_Behavior_Variables --
   ------------------------------

   procedure Map_C_Behavior_Variables (S            : Node_Id;
                                       Declarations : List_Id)
   is
      BA, P, T        : Node_Id;
      Classifier_Decl : Node_Id;
      Initializer     : Node_Id;

      function Map_Initial_Value (Data_Instance : Node_Id) return Node_Id is
         Data_Rep : constant Supported_Data_Representation :=
           Get_Data_Representation (Data_Instance);
         Value_Name : Name_Id;
      begin
         if Data_Rep not in Data_Integer | Data_Boolean then
            return No_Node;
         end if;

         --  The instance carries inherited and overridden classifier values.
         --  Get_String_Property resolves the first element of this list
         --  property without assuming that its AST node is a literal.
         Value_Name := Ocarina.Instances.Queries.Get_String_Property
           (Data_Instance, "data_model::initial_value");
         if Value_Name = No_Name then
            return No_Node;
         end if;

         declare
            Text : constant String := Ada.Strings.Fixed.Trim
              (Get_Name_String (Value_Name), Ada.Strings.Both);
            Raw_Value : constant Node_Id :=
              Make_Defining_Identifier (Get_String_Name (Text), False);
            First : Integer := Text'First;
            Negative : Boolean;
            Magnitude : Unsigned_Long_Long;
            Signed_Max : constant Unsigned_Long_Long := 2 ** 63 - 1;
         begin
            if Data_Rep = Data_Boolean then
               return Make_Literal
                 (CV.New_Int_Value
                    (Unsigned_Long_Long (Boolean'Pos (Boolean'Value (Text))),
                     1, 10));
            end if;

            --  Normalize plain decimal integers, including both 64-bit
            --  boundaries. Other C initializers are interpreted by the C
            --  compiler, as specified by Data_Model::Initial_Value.
            if Text'Length = 0 then
               return Raw_Value;
            end if;
            Negative := Text (First) = '-';
            if Text (First) in '+' | '-' then
               First := First + 1;
            end if;
            if First > Text'Last then
               return Raw_Value;
            end if;
            if Text (First) = '0' and then First < Text'Last then
               --  Preserve C octal and hexadecimal syntax, e.g. 077 and 0xFF.
               return Raw_Value;
            end if;
            for J in First .. Text'Last loop
               if Text (J) not in '0' .. '9' then
                  return Raw_Value;
               end if;
            end loop;
            Magnitude := Unsigned_Long_Long'Value (Text (First .. Text'Last));

            if Negative and then Magnitude = Signed_Max + 1 then
               --  The positive magnitude of INT64_MIN is not a signed C99
               --  decimal literal. Subtract one from -INT64_MAX instead.
               return Make_Expression
                 (Make_Literal (CV.New_Int_Value (Signed_Max, -1, 10)),
                  Op_Minus, Make_Literal (CV.New_Int_Value (1, 1, 10)));
            elsif Negative and then Magnitude > Signed_Max then
               return Raw_Value;
            elsif not Negative and then Magnitude > Signed_Max then
               Add_Include
                 (Make_Include_Clause
                    (Make_Defining_Identifier
                       (Get_String_Name ("stdint"), False), Local => False));
               return Make_Call_Profile
                 (Make_Defining_Identifier
                    (Get_String_Name ("UINT64_C"), False),
                  Make_List_Id
                    (Make_Literal (CV.New_Int_Value (Magnitude, 1, 10))));
            end if;
            return Make_Literal
              (CV.New_Int_Value (Magnitude, (if Negative then -1 else 1), 10));
         exception
            when Constraint_Error =>
               --  A source-language expression need not be an Ada scalar
               --  literal; keep it as an initializer rather than dropping it.
               return Raw_Value;
         end;
      end Map_Initial_Value;

   begin

      BA := Get_Behavior_Specification (S);
      if not BANu.Is_Empty (BATN.Variables (BA)) then
         P := BATN.First_Node (BATN.Variables (BA));
         loop
            T := BATN.First_Node (BATN.Identifiers (P));
            loop
               if Present (BATN.Corresponding_Declaration
                           (BATN.Classifier_Ref (P)))
                 and then Present (AAN.Default_Instance
                                   (BATN.Corresponding_Declaration
                                      (BATN.Classifier_Ref (P))))
               then

                  Classifier_Decl := BATN.Corresponding_Declaration
                    (BATN.Classifier_Ref (P));
                  Initializer := Map_Initial_Value
                    (AAN.Default_Instance (Classifier_Decl));
                  if Present (Initializer) then
                     Append_Node_To_List
                       (Message_Comment
                          ("Initialize the BA variable from "
                           & "Data_Model::Initial_Value before its actions."),
                        Declarations);
                  end if;
                  CTU.Append_Node_To_List
                    (CTU.Make_Variable_Declaration
                       (Defining_Identifier => CTU.Make_Defining_Identifier
                            (BATN.Display_Name (T)),
                        Used_Type => Map_Used_Type (Classifier_Decl),
                        Value     => Initializer),
                     Declarations);

               else
                  if not BANu.Is_Empty (BATN.Package_Name
                                        (BATN.Classifier_Ref (P)))
                  then
                     Display_Error
                       (" The mapping of BA variable type ("
                        & Get_Name_String (BATN.Display_Name
                          (BATN.Full_Identifier (BATN.Classifier_Ref (P))))
                        & ") at " & Image (BATN.Loc (BATN.Classifier_Ref (P)))
                        & ", is not yet supported",
                        Fatal => True);
                  else
                     if Present (BATN.Component_Impl (BATN.Classifier_Ref (P)))
                     then
                        Display_Error
                          (" The mapping of BA variable type ("
                           & Get_Name_String
                             (Standard.Utils.Remove_Prefix_From_Name
                                  ("%ba%", BATN.Name (BATN.Component_Type
                                   (BATN.Classifier_Ref (P)))))
                           & "."
                           & Get_Name_String
                             (Standard.Utils.Remove_Prefix_From_Name
                                  ("%ba%", BATN.Name (BATN.Component_Impl
                                   (BATN.Classifier_Ref (P)))))
                           & ") at "
                           & Image (BATN.Loc (BATN.Classifier_Ref (P)))
                           & ", is not yet supported",
                           Fatal => True);
                     else
                        Display_Error
                          (" The mapping of BA variable type ("
                           & Get_Name_String
                             (Standard.Utils.Remove_Prefix_From_Name
                                  ("%ba%", BATN.Name (BATN.Component_Type
                                   (BATN.Classifier_Ref (P)))))
                           & ") at "
                           & Image (BATN.Loc (BATN.Classifier_Ref (P)))
                           & ", is not yet supported",
                           Fatal => True);
                     end if;
                  end if;
               end if;

               T := BATN.Next_Node (T);
               exit when No (T);
            end loop;
            P := BATN.Next_Node (P);
            exit when No (P);
         end loop;
      end if;

   end Map_C_Behavior_Variables;

   --------------------------------
   -- Map_C_Behavior_Transitions --
   --------------------------------

   procedure Map_C_Behavior_Transitions (S            : Node_Id;
                                         Declarations : List_Id;
                                         Statements   :  List_Id)
   is
      BA                 : Node_Id;
      behav_transition   : Node_Id;
      Transition_Node    : Node_Id;
   begin

      BA := Get_Behavior_Specification (S);

      if not BANu.Is_Empty (BATN.Transitions (BA)) then

         behav_transition := BATN.First_Node (BATN.Transitions (BA));
         Transition_Node := BATN.Transition (behav_transition);

         if BANu.Length (BATN.Transitions (BA)) = 1 then
            if AINU.Is_Thread (S) then
               Map_C_Behavior_Variables (S, Declarations);
            end if;

            if Present (BATN.Behavior_Action_Block (Transition_Node)) then
               if BATN.Kind (Transition_Node) =
                 BATN.K_Execution_Behavior_Transition
               then
                  --  For an AADL subprogram or thread with BA that includes
                  --  a unique transition, the Behavior_Action_Block
                  --  of this transition is mapped into C-statements.
                  Map_C_Behavior_Action_Block
                    (BATN.Behavior_Action_Block (Transition_Node),
                     S, Declarations, Statements);

                  if AINU.Is_Thread (S) then
                     Map_C_Implementation_of_BA_Body_Function
                       (S, Declarations, Statements);
                  end if;

               else
                  --  i.e. Kind (Transition_Node) = K_Mode_Transition
                  --  We do not support Mode transition in a subprogram
                  --
                  Display_Error
                    ("Mode Transition is not supported",
                     Fatal => True);
               end if;
            end if;
         else
            --  For an AADL subprogram with BA, many transitions
            --  are not supported.
            if AINU.Is_Subprogram (S) then
               Display_Error
                 ("Many transitions in the BA of a"
                  & "subprogram are not supported",
                  Fatal => True);
            elsif AINU.Is_Thread (S) then
               if Get_Thread_Dispatch_Protocol (S) = Thread_Periodic or else
                 Get_Thread_Dispatch_Protocol (S) = Thread_Sporadic
               then
                  Map_C_Many_Transitions_Of_A_Thread
                    (S, Declarations, Statements);
               else
                  Display_Error
                    ("The mapping of many BA transitions is only "
                     & " supported of periodic or sporadic threads",
                     Fatal => True);
               end if;
            end if;
         end if;

      end if;

   end Map_C_Behavior_Transitions;

   ----------------------------------------
   -- Compute_Nb_On_Dispatch_Transitions --
   -----------------------------------------

   function Compute_Nb_On_Dispatch_Transitions
     (S : Node_Id) return Unsigned_Long_Long
   is
      BA                        : Node_Id;
      behav_transition          : Node_Id;
      Transition_Node           : Node_Id;
      Nb_Dispatch_Transitions   : Unsigned_Long_Long;
   begin

      BA := Get_Behavior_Specification (S);

      Nb_Dispatch_Transitions := 0;

      --  /** compute the number of all « on dispatch » transitions
      --  that stems from the state "List_Node"
      --  i.e. have « BATN.Display_Name (List_Node) » as source state.
      --
      if not BANu.Is_Empty (BATN.Transitions (BA)) then
         Behav_Transition := BATN.First_Node (BATN.Transitions (BA));
         while Present (Behav_Transition) loop
            Transition_Node := BATN.Transition (Behav_Transition);

            if BATN.Kind (Transition_Node) =
              BATN.K_Execution_Behavior_Transition
              and then
                Present (BATN.Behavior_Condition (Transition_Node))
                and then
                  Present (BATN.Condition
                           (BATN.Behavior_Condition
                              (Transition_Node)))
              and then
                BATN.Kind
                  (BATN.Condition
                     (Behavior_Condition (Transition_Node)))
                = BATN.K_Dispatch_Condition_Thread
            then
               Nb_Dispatch_Transitions := Nb_Dispatch_Transitions + 1;
            end if;
            Behav_Transition := BATN.Next_Node (Behav_Transition);
         end loop;
      end if;

      return Nb_Dispatch_Transitions;

   end Compute_Nb_On_Dispatch_Transitions;

   ---------------------------------------------------------
   -- Compute_Max_Dispatch_Transitions_Per_Complete_State --
   ---------------------------------------------------------

   function Compute_Max_Dispatch_Transitions_Per_Complete_State
     (S : Node_Id) return Unsigned_Long_Long
   is
      BA                        : Node_Id;
      State                     : Node_Id;
      List_Node                 : Node_Id;
      behav_transition          : Node_Id;
      Transition_Node           : Node_Id;
      Source                    : Node_Id;
      Max_Dispatch_Transitions  : Unsigned_Long_Long := 0;
      Nb_Dispatch_Transitions   : Unsigned_Long_Long;
   begin

      BA := Get_Behavior_Specification (S);

      State := BATN.First_Node (BATN.States (BA));

      while Present (State) loop
         if Behavior_State_Kind'Val (BATN.State_Kind (State))
           = BSK_Initial_Complete
           or else Behavior_State_Kind'Val (BATN.State_Kind (State))
             = BSK_Initial_Complete_Final
           or else Behavior_State_Kind'Val (BATN.State_Kind (State))
             = BSK_Complete
           or else Behavior_State_Kind'Val (BATN.State_Kind (State))
             = BSK_Complete_Final
         then
            List_Node := BATN.First_Node (BATN.Identifiers (State));

            while Present (List_Node) loop

               Nb_Dispatch_Transitions := 0;

               --  /** compute the number of all « on dispatch » transitions
               --  that stems from the state "List_Node"
               --  i.e. have « BATN.Display_Name (List_Node) » as source state.
               --
               if not BANu.Is_Empty (BATN.Transitions (BA)) then
                  Behav_Transition := BATN.First_Node (BATN.Transitions (BA));
                  while Present (Behav_Transition) loop
                     Transition_Node := BATN.Transition (Behav_Transition);

                     if BATN.Kind (Transition_Node) =
                       BATN.K_Execution_Behavior_Transition
                       and then
                         Present (BATN.Behavior_Condition (Transition_Node))
                         and then
                           Present (BATN.Condition
                                    (BATN.Behavior_Condition
                                       (Transition_Node)))
                       and then
                         BATN.Kind
                           (BATN.Condition
                              (Behavior_Condition (Transition_Node)))
                         = BATN.K_Dispatch_Condition_Thread
                     then
                        if BANu.Length (BATN.Sources (Transition_Node)) = 1
                        then
                           Source := BATN.First_Node
                             (BATN.Sources (Transition_Node));
                           if  (Standard.Utils.To_Upper
                                (BATN.Display_Name (List_Node)) =
                                  Standard.Utils.To_Upper
                                    (BATN.Display_Name (Source)))
                           then
                              Nb_Dispatch_Transitions :=
                                Nb_Dispatch_Transitions + 1;
                           end if;
                        end if;
                     end if;
                     Behav_Transition := BATN.Next_Node (Behav_Transition);
                  end loop;
               end if;

               if Nb_Dispatch_Transitions > Max_Dispatch_Transitions then
                  Max_Dispatch_Transitions := Nb_Dispatch_Transitions;
               end if;

               List_Node := BATN.Next_Node (List_Node);
            end loop;
         end if;

         State := BATN.Next_Node (State);
      end loop;

      return Max_Dispatch_Transitions;

   end Compute_Max_Dispatch_Transitions_Per_Complete_State;

   -----------------------------------------------------------
   -- Compute_Max_Dispatch_Triggers_Per_Dispatch_Transition --
   -----------------------------------------------------------

   function Compute_Max_Dispatch_Triggers_Per_Dispatch_Transition
     (S : Node_Id) return Unsigned_Long_Long
   is
      BA                        : Node_Id;
      behav_transition          : Node_Id;
      Node                      : Node_Id;
      Dispatch_Conjunction_Node : Node_Id;
      Dipatch_Trigger_Event     : Node_Id;
      Max_Dispatch_Triggers     : Unsigned_Long_Long := 0;
      Nb_Dispatch_Triggers      : Unsigned_Long_Long;
   begin

      BA := Get_Behavior_Specification (S);

      if not BANu.Is_Empty (BATN.Transitions (BA)) then

         behav_transition := BATN.First_Node (BATN.Transitions (BA));

         while Present (Behav_Transition) loop

            Node := BATN.Transition (Behav_Transition);

            if BATN.Kind (Node) = BATN.K_Execution_Behavior_Transition
              and then Present (BATN.Behavior_Condition (Node))
              and then Present (BATN.Condition
                                (BATN.Behavior_Condition (Node)))
              and then
                BATN.Kind (BATN.Condition (Behavior_Condition (Node)))
              = BATN.K_Dispatch_Condition_Thread
              and then Present
                (BATN.Dispatch_Trigger_Condition
                   (BATN.Condition
                      (BATN.Behavior_Condition (Node))))
              and then not BANu.Is_Empty
                (BATN.Dispatch_Conjunction
                   (BATN.Dispatch_Trigger_Condition
                      (BATN.Condition
                           (BATN.Behavior_Condition (Node)))))
            then
               Dispatch_Conjunction_Node :=
                 BATN.First_Node
                   (Dispatch_Conjunction
                      (Dispatch_Trigger_Condition (BATN.Condition
                       (BATN.Behavior_Condition (Node)))));

               while Present (Dispatch_Conjunction_Node) loop

                  if not BANu.Is_Empty
                    (Dispatch_Triggers (Dispatch_Conjunction_Node))
                  then
                     Dipatch_Trigger_Event := BATN.First_Node
                       (Dispatch_Triggers (Dispatch_Conjunction_Node));

                     Nb_Dispatch_Triggers := 0;

                     while Present (Dipatch_Trigger_Event) loop

                        Nb_Dispatch_Triggers := Nb_Dispatch_Triggers + 1;

                        Dipatch_Trigger_Event := BATN.Next_Node
                          (Dipatch_Trigger_Event);
                     end loop;

                     if Nb_Dispatch_Triggers > Max_Dispatch_Triggers then
                        Max_Dispatch_Triggers := Nb_Dispatch_Triggers;
                     end if;

                  end if;

                  Dispatch_Conjunction_Node := BATN.Next_Node
                    (Dispatch_Conjunction_Node);
               end loop;

            end if;
            behav_transition := BATN.Next_Node (behav_transition);
         end loop;
      end if;

      return Max_Dispatch_Triggers;

   end Compute_Max_Dispatch_Triggers_Per_Dispatch_Transition;

   --------------------------------------
   -- Create_Enum_Type_Of_States_Names --
   --------------------------------------

   procedure Create_Enum_Type_Of_States_Names (S : Node_Id)
   is
      BA                         : Node_Id;
      State, List_Node           : Node_Id;
      N                          : Node_Id;
      State_Enumerator_List      : List_Id;
      State_Identifier           : Unsigned_Long_Long;
      Thread_Instance_Name       : Name_Id;
      E                          : constant Node_Id :=
        AIN.Parent_Subcomponent (S);
   begin

      BA := Get_Behavior_Specification (S);

      --  1) Create an enumeration type declaration
      --  called __po_hi_<<thread_instance_name>>_state_name_t
      --  that enumerates all states in the BA of the thread
      --  typedef enum
      --  {
      --     s0, s1, s2, s3
      --  } __po_hi__<<thread_instance_name>>__state_name_t;

      State_Enumerator_List := New_List (CTN.K_Enumeration_Literals);

      State := BATN.First_Node (BATN.States (BA));
      State_Identifier := 0;

      while Present (State) loop

         if not BANu.Is_Empty (BATN.Identifiers (State)) then
            List_Node := BATN.First_Node (BATN.Identifiers (State));

            while Present (List_Node) loop

               N := CTU.Make_Defining_Identifier
                 (BATN.Display_Name (List_Node));

--                 N := Make_Expression
--                   (CTU.Make_Defining_Identifier
--                      (BATN.Display_Name (List_Node)),
--                    Op_Equal,
--                    Make_Literal
--                      (CV.New_Int_Value (State_Identifier, 0, 10)));

               Append_Node_To_List (N, State_Enumerator_List);
               State_Identifier := State_Identifier + 1;

               List_Node := BATN.Next_Node (List_Node);
            end loop;
         end if;

         State := BATN.Next_Node (State);
      end loop;

      Thread_Instance_Name := Map_Thread_Port_Variable_Name (S);
      N :=
        Message_Comment
          ("For each state in the BA of the thread instance << " &
             Get_Name_String (Thread_Instance_Name) &
             " >> add an enumerator");
      Append_Node_To_List (N, CTN.Declarations (Current_File));

      N :=
        Make_Full_Type_Declaration
          (Defining_Identifier => Make_Defining_Identifier
             (Map_C_Variable_Name (E, State_Name_T => True)),
           Type_Definition => Make_Enum_Aggregate (State_Enumerator_List));
      Append_Node_To_List (N, CTN.Declarations (Current_File));

   end Create_Enum_Type_Of_States_Names;

   -----------------------
   -- Create_State_Type --
   -----------------------

   procedure Create_State_Type (S : Node_Id)
   is
      N                          : Node_Id;
      State_Struct               : List_Id;
      E                          : constant Node_Id :=
        AIN.Parent_Subcomponent (S);
   begin

      --  3)
      --  typedef struct
      --  {
      --    __po_hi_<<thread_instance_name>>_state_name_t name;
      --    __po_hi_state_kind_t kind;
      --  } __po_hi_<<thread_instance_name>>_state_t;

      State_Struct := New_List (CTN.K_Enumeration_Literals);

      N :=
        Make_Member_Declaration
          (Defining_Identifier => Make_Defining_Identifier
             (MN (M_Name)),
           Used_Type           => Make_Defining_Identifier
             (Map_C_Variable_Name (E, State_Name_T => True)));
      Append_Node_To_List (N, State_Struct);

      N :=
        Make_Member_Declaration
          (Defining_Identifier => Make_Defining_Identifier
             (MN (M_kind)),
           Used_Type           => RE (RE_State_Kind_T));
      Append_Node_To_List (N, State_Struct);

      if Get_Thread_Dispatch_Protocol (S) = Thread_Sporadic
        and then Compute_Nb_On_Dispatch_Transitions (S) > 1
      then
         N :=
           Make_Member_Declaration
             (Defining_Identifier => Make_Defining_Identifier
                (MN (M_States_Attributes)),
                  Used_Type           => RE (RE_Ba_Automata_State_T));
         Append_Node_To_List (N, State_Struct);
      end if;

      N :=
        Make_Full_Type_Declaration
          (Defining_Identifier => Make_Defining_Identifier
             (Map_C_Variable_Name (E, State_T => True)),
           Type_Definition     =>
             Make_Struct_Aggregate (Members => State_Struct));
      Append_Node_To_List (N, CTN.Declarations (Current_File));

   end Create_State_Type;

   -----------------------
   -- Compute_Nb_States --
   -----------------------

   function Compute_Nb_States
     (S            : Node_Id) return Unsigned_Long_Long
   is
      BA                         : Node_Id;
      State, List_Node           : Node_Id;
      Nb_States                  : Unsigned_Long_Long;
   begin

      BA := Get_Behavior_Specification (S);

      State := BATN.First_Node (BATN.States (BA));
      Nb_States := 0;

      while Present (State) loop

         if not BANu.Is_Empty (BATN.Identifiers (State)) then
            List_Node := BATN.First_Node (BATN.Identifiers (State));

            while Present (List_Node) loop

               Nb_States := Nb_States + 1;

               List_Node := BATN.Next_Node (List_Node);
            end loop;
         end if;

         State := BATN.Next_Node (State);
      end loop;

      return Nb_States;
   end Compute_Nb_States;

   ------------------------------
   -- Map_BA_States_To_C_Types --
   ------------------------------

   procedure Map_BA_States_To_C_Types
     (S            : Node_Id)
   is
      N                          : Node_Id;
      Nb_States                  : constant Unsigned_Long_Long :=
        Compute_Nb_States (S);
      E                          : constant Node_Id :=
        AIN.Parent_Subcomponent (S);
   begin

      --  4)
      --  #define __po_hi_<<thread_instance_name>>_nb_states <<Nb_States>>

      N :=
        Make_Define_Statement
          (Defining_Identifier => Make_Defining_Identifier
             (Map_C_Define_Name (E, Nb_States => True)),
           Value               =>
             Make_Literal
               (CV.New_Int_Value (Nb_States, 1, 10)));
      Append_Node_To_List (N, CTN.Declarations (Current_File));

      --  5)
      --  __po_hi_<<thread_instance_name>>_state_t
      --       __po_hi_<<thread_instance_name>>_states_array
      --               [__po_hi_<<thread_instance_name>>_nb_states];

      N :=
        Make_Variable_Declaration
          (Defining_Identifier =>
             Make_Array_Declaration
               (Defining_Identifier =>
                      Make_Defining_Identifier
                  (Map_C_Variable_Name (E, States_Array => True)),
                Array_Size =>
                  Make_Defining_Identifier
                    (Map_C_Define_Name (E, Nb_States => True))),
           Used_Type =>
             Make_Defining_Identifier
               (Map_C_Variable_Name (E, State_T => True)));
      Append_Node_To_List (N, CTN.Declarations (Current_File));

      --  6)
      --  __po_hi_<<thread_instance_name>>_state_t
      --       __po_hi_<<thread_instance_name>>_current_state
      N :=
        Make_Variable_Declaration
          (Defining_Identifier =>
                Make_Defining_Identifier
                  (Map_C_Variable_Name (E, Current_State => True)),
           Used_Type =>
             Make_Defining_Identifier
               (Map_C_Variable_Name (E, State_T => True)));
      Append_Node_To_List (N, CTN.Declarations (Current_File));

   end Map_BA_States_To_C_Types;

   ---------------------------
   -- Map_C_State_Kind_Name --
   ---------------------------

   function Map_C_State_Kind_Name
     (State_Kind : Ocarina.Types.Byte) return Node_Id
   is
      Result : Node_Id;
   begin
      case Behavior_State_Kind'Val (State_Kind) is
         when BSK_Initial                => Result := RE (RE_Initial);
         when BSK_Initial_Complete       =>
            Result := RE (RE_Initial_Complete);
         when BSK_Initial_Complete_Final =>
            Result := RE (RE_Initial_Complete_Final);
         when BSK_Initial_Final          =>
            Result := RE (RE_Initial_Final);
         when BSK_Complete               => Result := RE (RE_Complete);
         when BSK_Complete_Final         =>
            Result := RE (RE_Complete_Final);
         when BSK_Final                  => Result := RE (RE_Final);
         when BSK_No_Kind                => Result := RE (RE_Execution);
         when others                     => raise Program_Error;
      end case;
      return Result;
   end Map_C_State_Kind_Name;

   -------------------------------------------------------------------
   -- Max_Dispatch_Triggers_Per_Trans_From_A_Specific_Complete_Stat --
   -------------------------------------------------------------------

   function Max_Dispatch_Triggers_Per_Trans_From_A_Specific_Complete_Stat
     (BA             : Node_Id;
      Complete_State : Node_Id) return Unsigned_Long_Long
   is
      Nb_Dispatch_Triggers_Per_Trans  : Unsigned_Long_Long := 0;
      Max_Dispatch_Triggers_Per_Trans : Unsigned_Long_Long := 0;
      behav_transition                : Node_Id;
      Node                            : Node_Id;
      Source                          : Node_Id;
      Dispatch_Conjunction_Node       : Node_Id;
      Dispatch_Trigger_Event          : Node_Id;
   begin

      --  /** compute the number of all « dispatch triggers » of all
      --  on dispatch transitions transitions that stem from the state
      --  "Complete_State"
      --  i.e. have « BATN.Display_Name (Complete_State) » as source state.
      --
      if not BANu.Is_Empty (BATN.Transitions (BA)) then
         Behav_Transition := BATN.First_Node (BATN.Transitions (BA));
         while Present (Behav_Transition) loop
            Node := BATN.Transition (Behav_Transition);

            if BATN.Kind (Node) =
              BATN.K_Execution_Behavior_Transition
              and then
                Present (BATN.Behavior_Condition (Node))
                and then
                  Present (BATN.Condition
                           (BATN.Behavior_Condition
                              (Node)))
              and then
                BATN.Kind
                  (BATN.Condition
                     (Behavior_Condition (Node)))
                = BATN.K_Dispatch_Condition_Thread
            then
               if BANu.Length (BATN.Sources (Node)) = 1
               then
                  Source := BATN.First_Node
                    (BATN.Sources (Node));
                  if  (Standard.Utils.To_Upper
                       (BATN.Display_Name (Complete_State)) =
                         Standard.Utils.To_Upper
                           (BATN.Display_Name (Source)))
                  then
                     Nb_Dispatch_Triggers_Per_Trans := 0;
                     if Present (BATN.Dispatch_Trigger_Condition
                                 (BATN.Condition
                                    (BATN.Behavior_Condition (Node))))
                       and then not BANu.Is_Empty
                         (BATN.Dispatch_Conjunction
                            (BATN.Dispatch_Trigger_Condition
                               (BATN.Condition
                                    (BATN.Behavior_Condition (Node)))))
                     then
                        if BANu.Length
                          (Dispatch_Conjunction
                             (Dispatch_Trigger_Condition (BATN.Condition
                              (BATN.Behavior_Condition (Node))))) = 1
                        then

                           Dispatch_Conjunction_Node :=
                             BATN.First_Node
                               (Dispatch_Conjunction
                                  (Dispatch_Trigger_Condition (BATN.Condition
                                   (BATN.Behavior_Condition (Node)))));

                           if not BANu.Is_Empty
                             (Dispatch_Triggers (Dispatch_Conjunction_Node))
                           then
                              Dispatch_Trigger_Event := BATN.First_Node
                                (Dispatch_Triggers
                                   (Dispatch_Conjunction_Node));

                              while Present (Dispatch_Trigger_Event) loop
                                 Nb_Dispatch_Triggers_Per_Trans :=
                                   Nb_Dispatch_Triggers_Per_Trans + 1;
                                 Dispatch_Trigger_Event :=
                                   BATN.Next_Node (Dispatch_Trigger_Event);
                              end loop;

                              if Nb_Dispatch_Triggers_Per_Trans >
                                Max_Dispatch_Triggers_Per_Trans
                              then
                                 Max_Dispatch_Triggers_Per_Trans :=
                                   Nb_Dispatch_Triggers_Per_Trans;
                              end if;
                           end if;
                        else
                           Display_Error
                             ("The code generation for many "
                              & "Dispatch Conjunction is not yet supported.",
                              Fatal => True);
                        end if;
                     end if;
                  end if;
               end if;
            end if;
            Behav_Transition := BATN.Next_Node (Behav_Transition);
         end loop;
      end if;
      return Max_Dispatch_Triggers_Per_Trans;
   end Max_Dispatch_Triggers_Per_Trans_From_A_Specific_Complete_Stat;

   -----------------------------------------------------
   -- Fill_Dispatch_Triggers_Of_All_Transitions_Array --
   -----------------------------------------------------

   procedure Fill_Dispatch_Triggers_Of_All_Transitions_Array
     (S               : Node_Id;
      BA              : Node_Id;
      Complete_State  : Node_Id;
      Index_Comp_Stat : Unsigned_Long_Long;
      WStatements     : List_Id)
   is
      E                              : constant Node_Id :=
        AIN.Parent_Subcomponent (S);
      Index_Dispatch_Triggers        : Unsigned_Long_Long := -1;
      behav_transition               : Node_Id;
      Node                           : Node_Id;
      Source                         : Node_Id;
      Dispatch_Conjunction_Node      : Node_Id;
      Dispatch_Trigger_Event         : Node_Id;
      N                              : Node_Id;
   begin
      --  6) fill the array __po_hi_consumer_states_array[0].
      --  state_attributes.dispatch_triggers_of_all_transitions[0]
      --  = LOCAL_PORT (consumer, c1);
      if not BANu.Is_Empty (BATN.Transitions (BA)) then
         Behav_Transition := BATN.First_Node (BATN.Transitions (BA));
         while Present (Behav_Transition) loop
            Node := BATN.Transition (Behav_Transition);

            if BATN.Kind (Node) =
              BATN.K_Execution_Behavior_Transition
              and then
                Present (BATN.Behavior_Condition (Node))
                and then
                  Present (BATN.Condition
                           (BATN.Behavior_Condition
                              (Node)))
              and then
                BATN.Kind
                  (BATN.Condition
                     (Behavior_Condition (Node)))
                = BATN.K_Dispatch_Condition_Thread
            then
               if BANu.Length (BATN.Sources (Node)) = 1
               then
                  Source := BATN.First_Node
                    (BATN.Sources (Node));
                  if  (Standard.Utils.To_Upper
                       (BATN.Display_Name (Complete_State)) =
                         Standard.Utils.To_Upper
                           (BATN.Display_Name (Source)))
                  then
                     if Present (BATN.Dispatch_Trigger_Condition
                                 (BATN.Condition
                                    (BATN.Behavior_Condition (Node))))
                       and then not BANu.Is_Empty
                         (BATN.Dispatch_Conjunction
                            (BATN.Dispatch_Trigger_Condition
                               (BATN.Condition
                                    (BATN.Behavior_Condition (Node)))))
                     then
                        if BANu.Length
                          (Dispatch_Conjunction
                             (Dispatch_Trigger_Condition (BATN.Condition
                              (BATN.Behavior_Condition (Node))))) = 1
                        then

                           Dispatch_Conjunction_Node :=
                             BATN.First_Node
                               (Dispatch_Conjunction
                                  (Dispatch_Trigger_Condition (BATN.Condition
                                   (BATN.Behavior_Condition (Node)))));

                           if not BANu.Is_Empty
                             (Dispatch_Triggers (Dispatch_Conjunction_Node))
                           then
                              Dispatch_Trigger_Event := BATN.First_Node
                                (Dispatch_Triggers
                                   (Dispatch_Conjunction_Node));

                              while Present (Dispatch_Trigger_Event) loop
                                 Index_Dispatch_Triggers :=
                                   Index_Dispatch_Triggers + 1;

                                 N := Make_Assignment_Statement
                                (Variable_Identifier => Make_Member_Designator
                                 (Defining_Identifier =>
                                   Make_Member_Designator
                                    (Defining_Identifier =>
                                     Make_Array_Declaration
                                      (Defining_Identifier =>
                                       Make_Defining_Identifier
                                 (MN (M_Dispatch_Triggers_Of_All_Transitions)),
                                       Array_Size => Make_Literal
                                         (CV.New_Int_Value
                                           (Index_Dispatch_Triggers, 0, 10))),
                                   Aggregate_Name  => Make_Defining_Identifier
                                      (MN (M_States_Attributes))),
                                  Aggregate_Name      => Make_Array_Declaration
                                   (Defining_Identifier =>
                                     Make_Defining_Identifier
                               (Map_C_Variable_Name (E, States_Array => True)),
                                       Array_Size => Make_Literal
                                      (CV.New_Int_Value
                                              (Index_Comp_Stat, 0, 10)))),
                                 Expression  =>
                                   Make_Call_Profile
                                    (RE (RE_Local_Port),
                                      Make_List_Id
                                      (Make_Defining_Identifier
                                        (Map_Thread_Port_Variable_Name (S)),
                                       Make_Defining_Identifier
                                           (BATN.Display_Name
                                                (Dispatch_Trigger_Event)))));

                                 CTU.Append_Node_To_List (N, WStatements);

                                 Dispatch_Trigger_Event :=
                                   BATN.Next_Node (Dispatch_Trigger_Event);
                              end loop;
                           end if;
                        else
                           Display_Error
                             ("The code generation for many "
                              & "Dispatch Conjunction is not yet supported.",
                              Fatal => True);
                        end if;
                     end if;
                  end if;
               end if;
            end if;
            Behav_Transition := BATN.Next_Node (Behav_Transition);
         end loop;
      end if;

   end Fill_Dispatch_Triggers_Of_All_Transitions_Array;

   --------------------------------------------------------
   -- Fill_Nb_Dispatch_Triggers_Of_Each_Transition_Array --
   --------------------------------------------------------

   procedure Fill_Nb_Dispatch_Triggers_Of_Each_Transition_Array
     (S               : Node_Id;
      BA              : Node_Id;
      Complete_State  : Node_Id;
      Index_Comp_Stat : Unsigned_Long_Long;
      WStatements     : List_Id)
   is
      E                              : constant Node_Id :=
        AIN.Parent_Subcomponent (S);
      Index_Transition               : Unsigned_Long_Long := -1;
      Nb_Dispatch_Triggers_Per_Trans : Unsigned_Long_Long := 0;
      behav_transition               : Node_Id;
      Node                           : Node_Id;
      Source                         : Node_Id;
      Dispatch_Conjunction_Node      : Node_Id;
      Dispatch_Trigger_Event         : Node_Id;
      N                              : Node_Id;
   begin

      if not BANu.Is_Empty (BATN.Transitions (BA)) then
         Behav_Transition := BATN.First_Node (BATN.Transitions (BA));
         while Present (Behav_Transition) loop
            Node := BATN.Transition (Behav_Transition);

            if BATN.Kind (Node) =
              BATN.K_Execution_Behavior_Transition
              and then
                Present (BATN.Behavior_Condition (Node))
                and then
                  Present (BATN.Condition
                           (BATN.Behavior_Condition
                              (Node)))
              and then
                BATN.Kind
                  (BATN.Condition
                     (Behavior_Condition (Node)))
                = BATN.K_Dispatch_Condition_Thread
            then
               if BANu.Length (BATN.Sources (Node)) = 1
               then
                  Source := BATN.First_Node
                    (BATN.Sources (Node));
                  if  (Standard.Utils.To_Upper
                       (BATN.Display_Name (Complete_State)) =
                         Standard.Utils.To_Upper
                           (BATN.Display_Name (Source)))
                  then
                     Nb_Dispatch_Triggers_Per_Trans := 0;
                     Index_Transition := Index_Transition + 1;
                     if Present (BATN.Dispatch_Trigger_Condition
                                 (BATN.Condition
                                    (BATN.Behavior_Condition (Node))))
                       and then not BANu.Is_Empty
                         (BATN.Dispatch_Conjunction
                            (BATN.Dispatch_Trigger_Condition
                               (BATN.Condition
                                    (BATN.Behavior_Condition (Node)))))
                     then
                        if BANu.Length
                          (Dispatch_Conjunction
                             (Dispatch_Trigger_Condition (BATN.Condition
                              (BATN.Behavior_Condition (Node))))) = 1
                        then

                           Dispatch_Conjunction_Node :=
                             BATN.First_Node
                               (Dispatch_Conjunction
                                  (Dispatch_Trigger_Condition (BATN.Condition
                                   (BATN.Behavior_Condition (Node)))));

                           if not BANu.Is_Empty
                             (Dispatch_Triggers (Dispatch_Conjunction_Node))
                           then
                              Dispatch_Trigger_Event := BATN.First_Node
                                (Dispatch_Triggers
                                   (Dispatch_Conjunction_Node));

                              while Present (Dispatch_Trigger_Event) loop
                                 Nb_Dispatch_Triggers_Per_Trans :=
                                   Nb_Dispatch_Triggers_Per_Trans + 1;
                                 Dispatch_Trigger_Event :=
                                   BATN.Next_Node (Dispatch_Trigger_Event);
                              end loop;

                              N := Make_Assignment_Statement
                                (Variable_Identifier => Make_Member_Designator
                                 (Defining_Identifier =>
                                   Make_Member_Designator
                                    (Defining_Identifier =>
                                     Make_Array_Declaration
                                      (Defining_Identifier =>
                                       Make_Defining_Identifier
                              (MN (M_Nb_Dispatch_Triggers_Of_Each_Transition)),
                                       Array_Size => Make_Literal
                                         (CV.New_Int_Value
                                              (Index_Transition, 0, 10))),
                                   Aggregate_Name  => Make_Defining_Identifier
                                      (MN (M_States_Attributes))),
                                  Aggregate_Name      => Make_Array_Declaration
                                   (Defining_Identifier =>
                                     Make_Defining_Identifier
                               (Map_C_Variable_Name (E, States_Array => True)),
                                       Array_Size => Make_Literal
                                      (CV.New_Int_Value
                                              (Index_Comp_Stat, 0, 10)))),
                                 Expression          =>
                                   Make_Literal
                                    (CV.New_Int_Value
                                     (Nb_Dispatch_Triggers_Per_Trans, 0, 10)));

                              CTU.Append_Node_To_List (N, WStatements);
                           end if;
                        else
                           Display_Error
                             ("The code generation for many "
                              & "Dispatch Conjunction is not yet supported.",
                              Fatal => True);
                        end if;
                     end if;
                  end if;
               end if;
            end if;
            Behav_Transition := BATN.Next_Node (Behav_Transition);
         end loop;
      end if;

   end Fill_Nb_Dispatch_Triggers_Of_Each_Transition_Array;

   ------------------------------------------------------------
   -- Nb_All_Dispatch_Triggers_From_A_Specific_Complete_Stat --
   ------------------------------------------------------------

   function Nb_All_Dispatch_Triggers_From_A_Specific_Complete_Stat
     (BA             : Node_Id;
      Complete_State : Node_Id) return Unsigned_Long_Long
   is
      Nb_All_Dispatch_Triggers  : Unsigned_Long_Long := 0;
      behav_transition          : Node_Id;
      Node                      : Node_Id;
      Source                    : Node_Id;
      Dispatch_Conjunction_Node : Node_Id;
      Dispatch_Trigger_Event    : Node_Id;
   begin

      --  /** compute the number of all « dispatch triggers » of all
      --  on dispatch transitions transitions that stem from the state
      --  "Complete_State"
      --  i.e. have « BATN.Display_Name (Complete_State) » as source state.
      --
      if not BANu.Is_Empty (BATN.Transitions (BA)) then
         Behav_Transition := BATN.First_Node (BATN.Transitions (BA));
         while Present (Behav_Transition) loop
            Node := BATN.Transition (Behav_Transition);

            if BATN.Kind (Node) =
              BATN.K_Execution_Behavior_Transition
              and then
                Present (BATN.Behavior_Condition (Node))
                and then
                  Present (BATN.Condition
                           (BATN.Behavior_Condition
                              (Node)))
              and then
                BATN.Kind
                  (BATN.Condition
                     (Behavior_Condition (Node)))
                = BATN.K_Dispatch_Condition_Thread
            then
               if BANu.Length (BATN.Sources (Node)) = 1
               then
                  Source := BATN.First_Node
                    (BATN.Sources (Node));
                  if  (Standard.Utils.To_Upper
                       (BATN.Display_Name (Complete_State)) =
                         Standard.Utils.To_Upper
                           (BATN.Display_Name (Source)))
                  then
                     if Present (BATN.Dispatch_Trigger_Condition
                                 (BATN.Condition
                                    (BATN.Behavior_Condition (Node))))
                       and then not BANu.Is_Empty
                         (BATN.Dispatch_Conjunction
                            (BATN.Dispatch_Trigger_Condition
                               (BATN.Condition
                                    (BATN.Behavior_Condition (Node)))))
                     then
                        if BANu.Length
                          (Dispatch_Conjunction
                             (Dispatch_Trigger_Condition (BATN.Condition
                              (BATN.Behavior_Condition (Node))))) = 1
                        then

                           Dispatch_Conjunction_Node :=
                             BATN.First_Node
                               (Dispatch_Conjunction
                                  (Dispatch_Trigger_Condition (BATN.Condition
                                   (BATN.Behavior_Condition (Node)))));

                           if not BANu.Is_Empty
                             (Dispatch_Triggers (Dispatch_Conjunction_Node))
                           then
                              Dispatch_Trigger_Event := BATN.First_Node
                                (Dispatch_Triggers
                                   (Dispatch_Conjunction_Node));

                              while Present (Dispatch_Trigger_Event) loop
                                 Nb_All_Dispatch_Triggers :=
                                   Nb_All_Dispatch_Triggers + 1;
                                 Dispatch_Trigger_Event :=
                                   BATN.Next_Node (Dispatch_Trigger_Event);
                              end loop;
                           end if;
                        else
                           Display_Error
                             ("The code generation for many "
                              & "Dispatch Conjunction is not yet supported.",
                              Fatal => True);
                        end if;
                     end if;
                  end if;
               end if;
            end if;
            Behav_Transition := BATN.Next_Node (Behav_Transition);
         end loop;
      end if;
      return Nb_All_Dispatch_Triggers;
   end Nb_All_Dispatch_Triggers_From_A_Specific_Complete_Stat;

   ------------------------------------------------------------
   -- Compute_Nb_Trans_Stemmed_From_A_Specific_Complete_Stat --
   ------------------------------------------------------------

   function Compute_Nb_Trans_Stemmed_From_A_Specific_Complete_Stat
     (BA             : Node_Id;
      Complete_State : Node_Id) return Unsigned_Long_Long
   is
      Nb_Dispatch_Transitions   : Unsigned_Long_Long := 0;
      behav_transition          : Node_Id;
      Transition_Node           : Node_Id;
      Source                    : Node_Id;
   begin

      --  /** compute the number of all « on dispatch » transitions
      --  that stems from the state "Complete_State"
      --  i.e. have « BATN.Display_Name (Complete_State) » as source state.
      --
      if not BANu.Is_Empty (BATN.Transitions (BA)) then
         Behav_Transition := BATN.First_Node (BATN.Transitions (BA));
         while Present (Behav_Transition) loop
            Transition_Node := BATN.Transition (Behav_Transition);

            if BATN.Kind (Transition_Node) =
              BATN.K_Execution_Behavior_Transition
              and then
                Present (BATN.Behavior_Condition (Transition_Node))
                and then
                  Present (BATN.Condition
                           (BATN.Behavior_Condition
                              (Transition_Node)))
              and then
                BATN.Kind
                  (BATN.Condition
                     (Behavior_Condition (Transition_Node)))
                = BATN.K_Dispatch_Condition_Thread
            then
               if BANu.Length (BATN.Sources (Transition_Node)) = 1
               then
                  Source := BATN.First_Node
                    (BATN.Sources (Transition_Node));
                  if  (Standard.Utils.To_Upper
                       (BATN.Display_Name (Complete_State)) =
                         Standard.Utils.To_Upper
                           (BATN.Display_Name (Source)))
                  then
                     Nb_Dispatch_Transitions :=
                       Nb_Dispatch_Transitions + 1;
                  end if;
               end if;
            end if;
            Behav_Transition := BATN.Next_Node (Behav_Transition);
         end loop;
      end if;
      return Nb_Dispatch_Transitions;
   end Compute_Nb_Trans_Stemmed_From_A_Specific_Complete_Stat;

   -----------------------------------------
   -- Make_Update_Next_Complete_State_Function --
   -----------------------------------------

   procedure Make_Update_Next_Complete_State_Function
     (S            : Node_Id)
   is
      N                          : Node_Id;
      E                          : constant Node_Id :=
        AIN.Parent_Subcomponent (S);
      WStatements : constant List_Id := New_List (CTN.K_Statement_List);
      Q : Name_Id;

   begin

      --  next_complete_state->nb_of_all_dispatch_events =
      --  __po_hi_consumer_current_state.state_attributes.
      --  nb_of_all_dispatch_events;
      --  next_complete_state->nb_transitions =
      --  __po_hi_consumer_current_state.state_attributes.nb_transitions;
      --
      --  for(int i=0;i<next_complete_state->nb_of_all_dispatch_events;i++)
      --  {
      --    next_complete_state->dispatch_triggers_of_all_transitions[i] =
      --     __po_hi_consumer_current_state.state_attributes.
      --       dispatch_triggers_of_all_transitions[i];
      --  }
      --  for(int i=0;i<next_complete_state->nb_transitions;i++)
      --  {
      --    next_complete_state->nb_dispatch_triggers_of_each_transition[i] =
      --      __po_hi_consumer_current_state.state_attributes.
      --        nb_dispatch_triggers_of_each_transition[i];
      --  }

      N := Make_Assignment_Statement
        (Variable_Identifier => Make_Member_Designator
           (Defining_Identifier => Make_Defining_Identifier
                (MN (M_Nb_Of_All_Dispatch_Events)),
            Aggregate_Name      => Make_Defining_Identifier
              (VN (V_Next_Complete_State)),
            Is_Pointer          => True),
         Expression  => Make_Member_Designator
           (Defining_Identifier => Make_Member_Designator
                (Defining_Identifier => Make_Defining_Identifier
                     (MN (M_Nb_Of_All_Dispatch_Events)),
                 Aggregate_Name      => Make_Defining_Identifier
                   (MN (M_States_Attributes))),
            Aggregate_Name      => Make_Defining_Identifier
              (Map_C_Variable_Name (E, Current_State => True))));

      CTU.Append_Node_To_List (N, WStatements);

      N := Make_Assignment_Statement
        (Variable_Identifier => Make_Member_Designator
           (Defining_Identifier => Make_Defining_Identifier
                (MN (M_Nb_Transitions)),
            Aggregate_Name      => Make_Defining_Identifier
              (VN (V_Next_Complete_State)),
            Is_Pointer          => True),
         Expression  => Make_Member_Designator
           (Defining_Identifier => Make_Member_Designator
                (Defining_Identifier => Make_Defining_Identifier
                     (MN (M_Nb_Transitions)),
                 Aggregate_Name      => Make_Defining_Identifier
                   (MN (M_States_Attributes))),
            Aggregate_Name      => Make_Defining_Identifier
              (Map_C_Variable_Name (E, Current_State => True))));

      CTU.Append_Node_To_List (N, WStatements);

      Set_Str_To_Name_Buffer ("i");
      Q := Name_Find;

      N := CTU.Make_For_Statement
        (Pre_Cond   => Make_Variable_Declaration
           (Defining_Identifier => Make_Defining_Identifier (Q),
            Used_Type           => Make_Defining_Identifier (TN (T_Int)),
            Value               => Make_Literal (CV.New_Int_Value (0, 0, 10))),
         Condition  => CTU.Make_Expression
           (Left_Expr  => Make_Defining_Identifier
                (Q),
            Operator   => CTU.Op_Less,
            Right_Expr => Make_Member_Designator
              (Defining_Identifier => Make_Defining_Identifier
                   (MN (M_Nb_Of_All_Dispatch_Events)),
               Aggregate_Name      => Make_Defining_Identifier
                 (VN (V_Next_Complete_State)),
               Is_Pointer          => True)),
         Post_Cond  =>  CTU.Make_Expression
           (Left_Expr  => Make_Defining_Identifier (Q),
            Operator   => CTU.Op_Plus_Plus,
            Right_Expr => No_Node),
         Statements =>
           Make_List_Id
             (Make_Assignment_Statement
                  (Variable_Identifier => Make_Member_Designator
                     (Defining_Identifier => Make_Array_Declaration
                        (Defining_Identifier => Make_Defining_Identifier
                             (MN (M_Dispatch_Triggers_Of_All_Transitions)),
                         Array_Size => Make_Defining_Identifier (Q)),
                      Aggregate_Name      => Make_Defining_Identifier
                        (VN (V_Next_Complete_State)),
                      Is_Pointer          => True),
                   Expression  => Make_Member_Designator
                     (Defining_Identifier => Make_Member_Designator
                        (Defining_Identifier => Make_Array_Declaration
                             (Defining_Identifier => Make_Defining_Identifier
                                (MN (M_Dispatch_Triggers_Of_All_Transitions)),
                              Array_Size => Make_Defining_Identifier (Q)),
                         Aggregate_Name      => Make_Defining_Identifier
                           (MN (M_States_Attributes))),
                      Aggregate_Name      => Make_Defining_Identifier
                        (Map_C_Variable_Name (E, Current_State => True))))));

      CTU.Append_Node_To_List (N, WStatements);

      N := CTU.Make_For_Statement
        (Pre_Cond   => Make_Variable_Declaration
           (Defining_Identifier => Make_Defining_Identifier (Q),
            Used_Type           => Make_Defining_Identifier (TN (T_Int)),
            Value               => Make_Literal (CV.New_Int_Value (0, 0, 10))),
         Condition  => CTU.Make_Expression
           (Left_Expr  => Make_Defining_Identifier
                (Q),
            Operator   => CTU.Op_Less,
            Right_Expr => Make_Member_Designator
              (Defining_Identifier => Make_Defining_Identifier
                   (MN (M_Nb_Transitions)),
               Aggregate_Name      => Make_Defining_Identifier
                 (VN (V_Next_Complete_State)),
               Is_Pointer          => True)),
         Post_Cond  =>  CTU.Make_Expression
           (Left_Expr  => Make_Defining_Identifier (Q),
            Operator   => CTU.Op_Plus_Plus,
            Right_Expr => No_Node),
         Statements =>
           Make_List_Id
             (Make_Assignment_Statement
                  (Variable_Identifier => Make_Member_Designator
                     (Defining_Identifier => Make_Array_Declaration
                        (Defining_Identifier => Make_Defining_Identifier
                             (MN (M_Nb_Dispatch_Triggers_Of_Each_Transition)),
                         Array_Size => Make_Defining_Identifier (Q)),
                      Aggregate_Name      => Make_Defining_Identifier
                        (VN (V_Next_Complete_State)),
                      Is_Pointer          => True),
                   Expression  => Make_Member_Designator
                     (Defining_Identifier => Make_Member_Designator
                        (Defining_Identifier => Make_Array_Declaration
                           (Defining_Identifier => Make_Defining_Identifier
                             (MN (M_Nb_Dispatch_Triggers_Of_Each_Transition)),
                              Array_Size => Make_Defining_Identifier (Q)),
                         Aggregate_Name      => Make_Defining_Identifier
                           (MN (M_States_Attributes))),
                      Aggregate_Name      => Make_Defining_Identifier
                        (Map_C_Variable_Name (E, Current_State => True))))));

      CTU.Append_Node_To_List (N, WStatements);

      N := Make_Function_Implementation
        (Specification => Make_Specification_Of_BA_Related_Function
           (S, Update_Next_Complete_State => True),
         Declarations  => No_List,
         Statements    => WStatements);

      Append_Node_To_List (N, CTN.Declarations (Current_File));

   end Make_Update_Next_Complete_State_Function;

   -----------------------------------------
   -- Make_States_Initialization_Function --
   -----------------------------------------

   procedure Make_States_Initialization_Function
     (S            : Node_Id)
   is
      BA                         : Node_Id;
      State, List_Node           : Node_Id;
      N                          : Node_Id;
      State_Identifier           : Unsigned_Long_Long;
      E                          : constant Node_Id :=
        AIN.Parent_Subcomponent (S);
      P      : constant Supported_Thread_Dispatch_Protocol :=
           Get_Thread_Dispatch_Protocol (S);
      WStatements : constant List_Id := New_List (CTN.K_Statement_List);
      Initial_State_Index        : Unsigned_Long_Long;
      Nb_States : constant Unsigned_Long_Long := Compute_Nb_States (S);
      Complete_States_Index : array (1 .. Nb_States) of Unsigned_Long_Long;
      Nb_Complete_States    : Unsigned_Long_Long := 0;
      i                     : Unsigned_Long_Long;
      Nb_On_Dispatch_Transitions : constant Unsigned_Long_Long :=
        Compute_Nb_On_Dispatch_Transitions (S);
   begin

      BA := Get_Behavior_Specification (S);

      --  7)
      --  void <<thread_name>>_states_initialization (void)
      --  {
      --  /* fill the __po_hi_producer_states_array with corresponding
      --  states */
      --  __po_hi_producer_states_array[0].name = s0;
      --  __po_hi_producer_states_array[0].kind = __po_hi_initial;
      --
      --  __po_hi_producer_states_array[1].name = s1;
      --  __po_hi_producer_states_array[1].kind = __po_hi_execution;
      --
      --  __po_hi_producer_states_array[2].name = s2;
      --  __po_hi_producer_states_array[2].kind = __po_hi_execution;
      --
      --  __po_hi_producer_states_array[3].name = s3;
      --  __po_hi_producer_states_array[3].kind = __po_hi_complete_final;

      --  /* initialize the __po_hi_producer_current_state with */
      --  /* the initial state */

      --  __po_hi_producer_current_state = __po_hi_producer_states_array[0];
      --  }

      N := Message_Comment ("fill the array "
                            & Get_Name_String
                              (Map_C_Variable_Name (E, States_Array => True))
                            & " with corresponding states");

      CTU.Append_Node_To_List (N, WStatements);

      State := BATN.First_Node (BATN.States (BA));
      State_Identifier := 0;

      while Present (State) loop

         if not BANu.Is_Empty (BATN.Identifiers (State)) then
            List_Node := BATN.First_Node (BATN.Identifiers (State));

            while Present (List_Node) loop

               CTU.Append_Node_To_List
                 (Make_Assignment_Statement
                    (Variable_Identifier => Make_Member_Designator
                         (Defining_Identifier => Make_Defining_Identifier
                              (MN (M_Name)),
                          Aggregate_Name      => Make_Array_Declaration
                            (Defining_Identifier =>
                                   Make_Defining_Identifier
                               (Map_C_Variable_Name (E, States_Array => True)),
                             Array_Size => Make_Literal
                               (CV.New_Int_Value (State_Identifier, 0, 10)))),
                     Expression          => CTU.Make_Defining_Identifier
                       (BATN.Display_Name (List_Node))),
                  WStatements);

               CTU.Append_Node_To_List
                 (Make_Assignment_Statement
                    (Variable_Identifier => Make_Member_Designator
                         (Defining_Identifier => Make_Defining_Identifier
                              (MN (M_Kind)),
                          Aggregate_Name      => Make_Array_Declaration
                            (Defining_Identifier =>
                                   Make_Defining_Identifier
                               (Map_C_Variable_Name (E, States_Array => True)),
                             Array_Size => Make_Literal
                               (CV.New_Int_Value (State_Identifier, 0, 10)))),
                     Expression          => Map_C_State_Kind_Name
                       (State_Kind (State))),
                  WStatements);

               if Behavior_State_Kind'Val (State_Kind (State)) = BSK_Initial
                 or else Behavior_State_Kind'Val (State_Kind (State)) =
                   BSK_Initial_Complete
                   or else Behavior_State_Kind'Val (State_Kind (State)) =
                     BSK_Initial_Final
                   or else Behavior_State_Kind'Val (State_Kind (State)) =
                       BSK_Initial_Complete_Final
               then
                  Initial_State_Index := State_Identifier;
               end if;

               if Behavior_State_Kind'Val (BATN.State_Kind (State))
                 = BSK_Initial_Complete
                 or else Behavior_State_Kind'Val (BATN.State_Kind (State))
                   = BSK_Initial_Complete_Final
                 or else Behavior_State_Kind'Val (BATN.State_Kind (State))
                   = BSK_Complete
                 or else Behavior_State_Kind'Val (BATN.State_Kind (State))
                   = BSK_Complete_Final
               then
                  Nb_Complete_States := Nb_Complete_States + 1;
                  Complete_States_Index (Nb_Complete_States) :=
                    State_Identifier;
               end if;

               State_Identifier := State_Identifier + 1;
               List_Node := BATN.Next_Node (List_Node);
            end loop;
         end if;

         State := BATN.Next_Node (State);
      end loop;

      if P = Thread_Sporadic and then
        Nb_On_Dispatch_Transitions > 1
      then

         i := 0;

         N := Message_Comment
           ("For each complete state initialize nb_transitions,"
            & " nb_dispatch_triggers_of_each_transition,"
            & " dispatch_triggers_of_all_transitions"
            & " and nb_of_all_dispatch_events");

         CTU.Append_Node_To_List (N, WStatements);

         State := BATN.First_Node (BATN.States (BA));

         while Present (State) loop

            if Behavior_State_Kind'Val (BATN.State_Kind (State))
              = BSK_Initial_Complete
              or else Behavior_State_Kind'Val (BATN.State_Kind (State))
                = BSK_Initial_Complete_Final
              or else Behavior_State_Kind'Val (BATN.State_Kind (State))
                = BSK_Complete
              or else Behavior_State_Kind'Val (BATN.State_Kind (State))
                = BSK_Complete_Final
            then
               List_Node := BATN.First_Node (BATN.Identifiers (State));

               while Present (List_Node) loop
                  i := i + 1;
                  --  /* complete state S0 */
                  N := Message_Comment
                    ("Complete state "
                     & Get_Name_String (BATN.Display_Name (List_Node)));
                  CTU.Append_Node_To_List (N, WStatements);

                  --  1)  __po_hi_consumer_states_array[0].
                  --   state_attributes.nb_transitions = 1;

                  N := Make_Assignment_Statement
                   (Variable_Identifier => Make_Member_Designator
                      (Defining_Identifier => Make_Member_Designator
                           (Defining_Identifier => Make_Defining_Identifier
                            (MN (M_Nb_Transitions)),
                            Aggregate_Name      => Make_Defining_Identifier
                            (MN (M_States_Attributes))),
                        Aggregate_Name      => Make_Array_Declaration
                          (Defining_Identifier =>
                               Make_Defining_Identifier
                             (Map_C_Variable_Name (E, States_Array => True)),
                           Array_Size => Make_Literal
                             (CV.New_Int_Value
                                  (Complete_States_Index (i), 0, 10)))),
                     Expression  => Make_Literal
                       (CV.New_Int_Value
                      (Compute_Nb_Trans_Stemmed_From_A_Specific_Complete_Stat
                                    (BA, List_Node), 0, 10)));

                  CTU.Append_Node_To_List (N, WStatements);

                  --  2) __po_hi_consumer_states_array[0].
                  --  state_attributes.nb_of_all_dispatch_events = 1;

                  N := Make_Assignment_Statement
                   (Variable_Identifier => Make_Member_Designator
                      (Defining_Identifier => Make_Member_Designator
                           (Defining_Identifier => Make_Defining_Identifier
                            (MN (M_Nb_Of_All_Dispatch_Events)),
                            Aggregate_Name      => Make_Defining_Identifier
                            (MN (M_States_Attributes))),
                        Aggregate_Name      => Make_Array_Declaration
                          (Defining_Identifier =>
                               Make_Defining_Identifier
                             (Map_C_Variable_Name (E, States_Array => True)),
                           Array_Size => Make_Literal
                             (CV.New_Int_Value
                                  (Complete_States_Index (i), 0, 10)))),
                     Expression  => Make_Literal
                       (CV.New_Int_Value
                      (Nb_All_Dispatch_Triggers_From_A_Specific_Complete_Stat
                                    (BA, List_Node), 0, 10)));

                  CTU.Append_Node_To_List (N, WStatements);

                  --  3) __po_hi_consumer_states_array[0].state_attributes.
                  --  nb_dispatch_triggers_of_each_transition =
                  --  (__po_hi_int32_t *)malloc( sizeof(__po_hi_int32_t) * 1);

                  N := Make_Assignment_Statement
                    (Variable_Identifier => Make_Member_Designator
                      (Defining_Identifier => Make_Member_Designator
                           (Defining_Identifier => Make_Defining_Identifier
                            (MN (M_Nb_Dispatch_Triggers_Of_Each_Transition)),
                            Aggregate_Name      => Make_Defining_Identifier
                            (MN (M_States_Attributes))),
                        Aggregate_Name      => Make_Array_Declaration
                          (Defining_Identifier =>
                               Make_Defining_Identifier
                             (Map_C_Variable_Name (E, States_Array => True)),
                           Array_Size => Make_Literal
                             (CV.New_Int_Value
                                  (Complete_States_Index (i), 0, 10)))),
                     Expression          => Make_Type_Conversion
                       (Subtype_Mark => Make_Pointer_Type
                            (RE (RE_Int32_T)),
                        Expression   => Make_Call_Profile
                          (Make_Defining_Identifier (FN (F_Malloc)),
                           Make_List_Id
                             (Make_Expression
                                  (Left_Expr  => Make_Call_Profile
                                     (Make_Defining_Identifier (FN (F_Sizeof)),
                                        Make_List_Id
                                          (RE (RE_Int32_T))),
                                   Operator   => Op_Asterisk,
                                   Right_Expr =>
                                     Make_Literal
                                       (CV.New_Int_Value
                 (Max_Dispatch_Triggers_Per_Trans_From_A_Specific_Complete_Stat
                                    (BA, List_Node), 0, 10)))))));

                  CTU.Append_Node_To_List (N, WStatements);

                  --  4) __po_hi_consumer_states_array[0].state_attributes.
                  --  dispatch_triggers_of_all_transitions =
                  --   (__po_hi_local_port_t *)malloc
                  --   ( sizeof(__po_hi_local_port_t) * 1);

                  N := Make_Assignment_Statement
                    (Variable_Identifier => Make_Member_Designator
                      (Defining_Identifier => Make_Member_Designator
                           (Defining_Identifier => Make_Defining_Identifier
                            (MN (M_Dispatch_Triggers_Of_All_Transitions)),
                            Aggregate_Name      => Make_Defining_Identifier
                            (MN (M_States_Attributes))),
                        Aggregate_Name      => Make_Array_Declaration
                          (Defining_Identifier =>
                               Make_Defining_Identifier
                             (Map_C_Variable_Name (E, States_Array => True)),
                           Array_Size => Make_Literal
                             (CV.New_Int_Value
                                  (Complete_States_Index (i), 0, 10)))),
                     Expression          => Make_Type_Conversion
                       (Subtype_Mark => Make_Pointer_Type
                            (RE (RE_Local_Port_T)),
                        Expression   => Make_Call_Profile
                          (Make_Defining_Identifier (FN (F_Malloc)),
                           Make_List_Id
                             (Make_Expression
                                  (Left_Expr  => Make_Call_Profile
                                     (Make_Defining_Identifier (FN (F_Sizeof)),
                                        Make_List_Id
                                          (RE (RE_Local_Port_T))),
                                   Operator   => Op_Asterisk,
                                   Right_Expr =>
                                     Make_Literal
                                       (CV.New_Int_Value
                        (Nb_All_Dispatch_Triggers_From_A_Specific_Complete_Stat
                                    (BA, List_Node), 0, 10)))))));

                  CTU.Append_Node_To_List (N, WStatements);

                  --  5) fill the array __po_hi_consumer_states_array[0].
                  --  state_attributes.nb_dispatch_triggers_of_each_transition

                  Fill_Nb_Dispatch_Triggers_Of_Each_Transition_Array
                    (S, BA, List_Node, Complete_States_Index (i), WStatements);

                  --  6) fill the array __po_hi_consumer_states_array[0].
                  --  state_attributes.dispatch_triggers_of_all_transitions[0]
                  --  = LOCAL_PORT (consumer, c1);

                  Fill_Dispatch_Triggers_Of_All_Transitions_Array
                    (S, BA, List_Node, Complete_States_Index (i), WStatements);

                  List_Node := BATN.Next_Node (List_Node);
               end loop;
            end if;

            State := BATN.Next_Node (State);
         end loop;

      end if;

      N := Message_Comment ("Initialize the current state of the thread "
                            & Get_Name_String
                              (Map_C_Variable_Name (E, Current_State => True))
                            & " with the initial state");

      CTU.Append_Node_To_List (N, WStatements);

      CTU.Append_Node_To_List
        (Make_Assignment_Statement
           (Variable_Identifier => Make_Defining_Identifier
                (Map_C_Variable_Name (E, Current_State => True)),
            Expression          => Make_Array_Declaration
              (Defining_Identifier =>
                   Make_Defining_Identifier
                 (Map_C_Variable_Name (E, States_Array => True)),
               Array_Size => Make_Literal
                 (CV.New_Int_Value (Initial_State_Index, 0, 10)))),
         WStatements);

      if P = Thread_Sporadic and then
        Nb_On_Dispatch_Transitions > 1
      then
         N := CTU.Make_Call_Profile
           (Defining_Identifier => Make_Defining_Identifier
              (Map_C_BA_Related_Function_Name
                   (E, Update_Next_Complete_State => True)),
            Parameters          =>  Make_List_Id
              (Make_Defining_Identifier
                   (VN (V_Next_Complete_State))));
         Append_Node_To_List (N, WStatements);
      end if;

      N := Make_Function_Implementation
        (Specification => Make_Specification_Of_BA_Related_Function
           (S, States_Initialization => True),
         Declarations  => No_List,
         Statements    => WStatements);

      Append_Node_To_List (N, CTN.Declarations (Current_File));

   end Make_States_Initialization_Function;

   ----------------------
   -- Search_State_Kind --
   -----------------------

   function Search_State_Kind
     (BA        : Node_Id;
      State_Idt : Node_Id) return Ocarina.Types.Byte
   is
      State, List_Node : Node_Id;
      Found            : Boolean := False;
      Result           : Ocarina.Types.Byte;
   begin

      State := BATN.First_Node (BATN.States (BA));

      while Present (State) loop

         List_Node := BATN.First_Node (BATN.Identifiers (State));

         while Present (List_Node) loop

            if (Standard.Utils.To_Upper
                (BATN.Display_Name (List_Node)) =
                  Standard.Utils.To_Upper
                    (BATN.Display_Name (State_Idt)))
            then
               Result := State_Kind (State);
               Found := True;
            end if;

            exit when Found;
            List_Node := BATN.Next_Node (List_Node);
         end loop;

         exit when Found;
         State := BATN.Next_Node (State);
      end loop;

      if not Found then
         Display_Error
           ("The state '"
              & Get_Name_String (BATN.Display_Name (State_Idt))
            & "' is not declared",
            Fatal => True);
      end if;

      return Result;

   end Search_State_Kind;

   --------------------------------
   -- Find_Index_In_States_Array --
   --------------------------------

   function Find_Index_In_States_Array
     (BA        : Node_Id;
      State_Idt : Node_Id) return Unsigned_Long_Long
   is
      State, List_Node : Node_Id;
      Found            : Boolean := False;
      Result           : Unsigned_Long_Long := 0;
   begin

      State := BATN.First_Node (BATN.States (BA));

      while Present (State) loop

         List_Node := BATN.First_Node (BATN.Identifiers (State));

         while Present (List_Node) loop

            if (Standard.Utils.To_Upper
                (BATN.Display_Name (List_Node)) =
                  Standard.Utils.To_Upper
                    (BATN.Display_Name (State_Idt)))
            then
               Found := True;
            end if;

            exit when Found;
            Result := Result + 1;
            List_Node := BATN.Next_Node (List_Node);
         end loop;

         exit when Found;
         State := BATN.Next_Node (State);
      end loop;

      if not Found then
         Display_Error
           ("The Destination state is not declared",
            Fatal => True);
      end if;

      return Result;

   end Find_Index_In_States_Array;

   --------------------------
   -- Update_Current_State --
   --------------------------

   procedure Update_Current_State
     (E                : Node_Id;
      BA               : Node_Id;
      Transition_Node  : Node_Id;
      Stats            : List_Id)
   is
      N : Node_Id;
   begin
      N := Message_Comment (" Update the current state ");
      CTU.Append_Node_To_List (N, Stats);

      N := Make_Assignment_Statement
        (Variable_Identifier => Make_Defining_Identifier
           (Map_C_Variable_Name (E, Current_State => True)),
         Expression          => Make_Array_Declaration
           (Defining_Identifier =>
                Make_Defining_Identifier
              (Map_C_Variable_Name (E, States_Array => True)),
            Array_Size => Make_Literal
              (CV.New_Int_Value
                   (Find_Index_In_States_Array
                        (BA, BATN.Destination (Transition_Node)),
                    0, 10))));
      CTU.Append_Node_To_List (N, Stats);
   end Update_Current_State;

   ---------------------------
   -- Map_C_Transition_Node --
   ---------------------------

   function Map_C_Transition_Node
     (Node             : Node_Id;
      S                : Node_Id;
      Declarations     : List_Id;
      Statements       : List_Id;
      Else_St          : List_Id) return List_Id
   is
      N, N1            : Node_Id;
      Else_stats       : constant List_Id := New_List (CTN.K_Statement_List);
      elsif_statements : constant List_Id := New_List (CTN.K_Statement_List);
      E                : constant Node_Id := AIN.Parent_Subcomponent (S);
      BA               : constant Node_Id := Get_Behavior_Specification (S);
      Condition        : Node_Id;
   begin
      N := BATN.Next_Node (Node);

      if Present (BATN.Behavior_Condition (Node))
        and then Present (BATN.Condition (BATN.Behavior_Condition (Node)))
      then
         Condition := Evaluate_BA_Value_Expression
           (Node             => BATN.Value_Expression
              (BATN.Condition
                   (BATN.Behavior_Condition (Node))),
            Subprogram_Root  => S,
            Declarations     => Declarations,
            Statements       => Statements);
      else
         Display_Error
           ("As there are an more than one transition that starts from"
            & " the same state called '"
            & Get_Name_String
              (BATN.Display_Name (BATN.First_Node
               (BATN.Sources (Node))))
            & "', all these transitions must have conditions",
            Fatal => True);
      end if;

      if Present (N) then
         if Present (BATN.Behavior_Action_Block (Node)) and then
           Present (BATN.Behav_Acts (BATN.Behavior_Action_Block (Node)))
         then
            N1 := Message_Comment (" Mapping of actions ");
            CTU.Append_Node_To_List (N1, elsif_statements);

            Map_C_Behavior_Action_Block
              (Node         => BATN.Behavior_Action_Block (Node),
               S            => S,
               Declarations => Declarations,
               Statements  => elsif_statements);
         end if;

         Update_Current_State (E, BA, Node, elsif_statements);

         CTU.Append_Node_To_List
           (CTU.Make_If_Statement
              (Condition       => Condition,
               Statements      => elsif_statements,
               Else_Statements =>
                 Map_C_Transition_Node
                   (Node         => N,
                    S            => S,
                    Declarations => Declarations,
                    Statements   => Statements,
                    Else_St      => Else_St)),
            Else_stats);
      else

         if Present (BATN.Behavior_Action_Block (Node)) and then
           Present (BATN.Behav_Acts (BATN.Behavior_Action_Block (Node)))
         then
            N1 := Message_Comment (" Mapping of actions ");
            CTU.Append_Node_To_List (N1, elsif_statements);

            Map_C_Behavior_Action_Block
              (Node         => BATN.Behavior_Action_Block (Node),
               S            => S,
               Declarations => Declarations,
               Statements  => elsif_statements);
         end if;

         Update_Current_State (E, BA, Node, elsif_statements);

         if Present (Else_St) then
            CTU.Append_Node_To_List
              (CTU.Make_If_Statement
                 (Condition       => Condition,
                  Statements      => elsif_statements,
                  Else_Statements => Else_St),
               Else_stats);
         else
            CTU.Append_Node_To_List
              (CTU.Make_If_Statement
                 (Condition       => Condition,
                  Statements      => elsif_statements),
               Else_stats);
         end if;

      end if;

      return Else_stats;

   end Map_C_Transition_Node;

   ---------------------------------------
   -- Map_C_On_Dispatch_Transition_Node --
   ---------------------------------------

   function Map_C_On_Dispatch_Transition_Node
     (Node             : Node_Id;
      S                : Node_Id;
      Declarations     : List_Id;
      Statements       : List_Id;
      Index_Transition : in out Unsigned_Long_Long) return List_Id
   is
      N, N1            : Node_Id;
      Else_stats       : constant List_Id := New_List (CTN.K_Statement_List);
      elsif_statements : constant List_Id := New_List (CTN.K_Statement_List);
      E                : constant Node_Id := AIN.Parent_Subcomponent (S);
      BA               : constant Node_Id := Get_Behavior_Specification (S);
      Condition        : Node_Id;
   begin
      N := BATN.Next_Node (Node);
      Index_Transition := Index_Transition + 1;

      Condition := CTU.Make_Expression
        (Left_Expr  => Make_Defining_Identifier
           (VN (V_Index_Transition_To_Execute)),
         Operator   => CTU.Op_Equal_Equal,
         Right_Expr => Make_Literal
           (CV.New_Int_Value (Index_Transition, 1, 10)));

      if Present (N) then
         if Present (BATN.Behavior_Action_Block (Node)) and then
           Present (BATN.Behav_Acts (BATN.Behavior_Action_Block (Node)))
         then
            N1 := Message_Comment (" Mapping of actions ");
            CTU.Append_Node_To_List (N1, elsif_statements);

            Map_C_Behavior_Action_Block
              (Node         => BATN.Behavior_Action_Block (Node),
               S            => S,
               Declarations => Declarations,
               Statements  => elsif_statements);
         end if;

         Update_Current_State (E, BA, Node, elsif_statements);

         CTU.Append_Node_To_List
           (CTU.Make_If_Statement
              (Condition       => Condition,
               Statements      => elsif_statements,
               Else_Statements =>
                 Map_C_On_Dispatch_Transition_Node
                   (Node         => N,
                    S            => S,
                    Declarations => Declarations,
                    Statements   => Statements,
                    Index_Transition => Index_Transition)),
            Else_stats);
      else
         if Present (BATN.Behavior_Action_Block (Node)) and then
           Present (BATN.Behav_Acts (BATN.Behavior_Action_Block (Node)))
         then
            N1 := Message_Comment (" Mapping of actions ");
            CTU.Append_Node_To_List (N1, elsif_statements);

            Map_C_Behavior_Action_Block
              (Node         => BATN.Behavior_Action_Block (Node),
               S            => S,
               Declarations => Declarations,
               Statements  => elsif_statements);
         end if;

         Update_Current_State (E, BA, Node, elsif_statements);

         CTU.Append_Node_To_List
           (CTU.Make_If_Statement
              (Condition       => Condition,
               Statements      => elsif_statements),
            Else_stats);

      end if;

      return Else_stats;

   end Map_C_On_Dispatch_Transition_Node;

   ---------------------------------------------
   -- Map_C_A_List_Of_On_Dispatch_Transitions --
   ---------------------------------------------

   procedure Map_C_A_List_Of_On_Dispatch_Transitions
     (S                            : Node_Id;
      BA                           : Node_Id;
      Sub_Transition_List          : List_Id;
      WDeclarations                : List_Id;
      WStatements                  : List_Id)
   is
      N                : Node_Id;
      Transition_Node  : Node_Id;
      E                : constant Node_Id := AIN.Parent_Subcomponent (S);
      Condition     : Node_Id;
      If_Statements : constant List_Id := New_List (CTN.K_Statement_List);
      Else_Stats    : List_Id;
      Index_Transition          : Unsigned_Long_Long := 0;
   begin

      if (BANu.Length (Sub_Transition_List) = 1) then

         Transition_Node := BATN.First_Node (Sub_Transition_List);

         --  i)
         --
         --  {
         --    /* map actions if there exist */
         --    ...
         --    /* update current state */
         --    __po_hi_consumer_current_state =
         --       __po_hi_consumer_states_array[3];
         --  }
         --
         --  break;

         if Present (BATN.Behavior_Action_Block (Transition_Node))
           and then Present
             (BATN.Behav_Acts
                (BATN.Behavior_Action_Block (Transition_Node)))
         then

            N := Message_Comment (" Mapping of actions ");
            CTU.Append_Node_To_List (N, WStatements);

            Map_C_Behavior_Action_Block
              (BATN.Behavior_Action_Block (Transition_Node),
               S, WDeclarations, WStatements);
         end if;

         --  ii) Update the current state
         Update_Current_State (E, BA, Transition_Node, WStatements);

      elsif (BANu.Length (Sub_Transition_List) >= 2) then
         Transition_Node := BATN.First_Node (Sub_Transition_List);
         Index_Transition := Index_Transition + 1;

         Condition := CTU.Make_Expression
           (Left_Expr  => Make_Defining_Identifier
              (VN (V_Index_Transition_To_Execute)),
            Operator   => CTU.Op_Equal_Equal,
            Right_Expr => Make_Literal
               (CV.New_Int_Value (Index_Transition, 1, 10)));

         if Present (BATN.Behavior_Action_Block (Transition_Node))
           and then Present
             (BATN.Behav_Acts
                (BATN.Behavior_Action_Block
                   (Transition_Node)))
         then

            N := Message_Comment (" Mapping of actions ");
            CTU.Append_Node_To_List (N, If_Statements);

            Map_C_Behavior_Action_Block
              (BATN.Behavior_Action_Block (Transition_Node),
               S, WDeclarations, If_Statements);
         end if;

         Update_Current_State (E, BA, Transition_Node, If_Statements);

         Transition_Node := BATN.Next_Node (Transition_Node);

         Else_Stats := Map_C_On_Dispatch_Transition_Node
           (Transition_Node, S, WDeclarations,
            WStatements, Index_Transition);

         CTU.Append_Node_To_List
           (CTU.Make_If_Statement
              (Condition       => Condition,
               Statements      => If_Statements,
               Else_Statements => Else_Stats),
            WStatements);

      end if;

   end Map_C_A_List_Of_On_Dispatch_Transitions;

   ------------------------------------
   -- Has_Higher_Transition_Priority --
   ------------------------------------

   function Has_Higher_Transition_Priority
     (Left, Right : Node_Id) return Boolean
   is
      use Ocarina.AADL_Values;
      Left_Priority : constant Node_Id :=
        BATN.Behavior_Transition_Priority (Left);
      Right_Priority : constant Node_Id :=
        BATN.Behavior_Transition_Priority (Right);
   begin
      if No (Left_Priority) then
         return False;
      elsif No (Right_Priority) then
         return True;
      end if;

      --  Compare the stored values without narrowing integer priorities to
      --  a signed type. An explicit zero still precedes an omitted priority.
      return Value (BATN.Value (Right_Priority)) <
        Value (BATN.Value (Left_Priority));
   end Has_Higher_Transition_Priority;

   ----------------------------------
   -- Sort_Transitions_By_Priority --
   ----------------------------------

   procedure Sort_Transitions_By_Priority (Transition_List : List_Id) is
      Transitions : array (1 .. BANu.Length (Transition_List)) of Node_Id;
      Current     : Node_Id;
      Position    : Positive;
   begin
      if Transitions'Length < 2 then
         return;
      end if;

      Current := BATN.First_Node (Transition_List);
      for Index in Transitions'Range loop
         Transitions (Index) := Current;
         Current := BATN.Next_Node (Current);
      end loop;

      --  Stable insertion sort keeps the first declared transition first
      --  when priorities are equal or both unspecified. Relink only after
      --  sorting, so moving a node cannot accidentally move its successors.
      for Index in 2 .. Transitions'Last loop
         Current := Transitions (Index);
         Position := Index;
         while Position > Transitions'First and then
           Has_Higher_Transition_Priority
             (Current, Transitions (Position - 1))
         loop
            Transitions (Position) := Transitions (Position - 1);
            Position := Position - 1;
         end loop;
         Transitions (Position) := Current;
      end loop;

      BATN.Set_First_Node (Transition_List, Transitions (Transitions'First));
      for Index in Transitions'First .. Transitions'Last - 1 loop
         BATN.Set_Next_Node (Transitions (Index), Transitions (Index + 1));
      end loop;
      BATN.Set_Next_Node (Transitions (Transitions'Last), No_Node);
      BATN.Set_Last_Node (Transition_List, Transitions (Transitions'Last));
   end Sort_Transitions_By_Priority;

   ---------------------------------
   -- Map_C_A_List_Of_Transitions --
   ---------------------------------

   procedure Map_C_A_List_Of_Transitions
     (S                         : Node_Id;
      BA                        : Node_Id;
      Otherwise_Transition_Node : Node_Id;
      Sub_Transition_List       : List_Id;
      WDeclarations             : List_Id;
      WStatements               : List_Id)
   is
      N                : Node_Id;
      Transition_Node  : Node_Id;
      E                : constant Node_Id := AIN.Parent_Subcomponent (S);

      Condition     : Node_Id;
      If_Statements : constant List_Id := New_List (CTN.K_Statement_List);
      Else_Stats    : List_Id;

   begin
      if BANu.Length (Sub_Transition_List) > 1 then
         Sort_Transitions_By_Priority (Sub_Transition_List);
         CTU.Append_Node_To_List
           (Message_Comment
              ("Check transitions in descending priority; unspecified "
               & "priorities are lowest and ties keep declaration order. "
               & "Use otherwise only when all other conditions are false."),
            WStatements);
      end if;

      if Present (Otherwise_Transition_Node) then
         Else_Stats := New_List (CTN.K_Statement_List);

         if Present (BATN.Behavior_Action_Block (Otherwise_Transition_Node))
           and then Present (BATN.Behav_Acts
                             (BATN.Behavior_Action_Block
                                (Otherwise_Transition_Node)))
         then
            N := Message_Comment (" Mapping of actions ");
            CTU.Append_Node_To_List (N, Else_Stats);

            Map_C_Behavior_Action_Block
              (BATN.Behavior_Action_Block (Otherwise_Transition_Node),
               S, WDeclarations, Else_Stats);
         end if;

         Update_Current_State (E, BA, Otherwise_Transition_Node, Else_Stats);
      else
         Else_Stats := No_List;
      end if;

      if (BANu.Length (Sub_Transition_List) = 1) then

         Transition_Node := BATN.First_Node (Sub_Transition_List);

         if Present (BATN.Behavior_Condition (Transition_Node))
           and then Present (BATN.Condition
                             (BATN.Behavior_Condition (Transition_Node)))
         then

            --  i) Make if .. else => in the case of otherwise transition
            --  or if without else => in the case of one transition with
            --  condition

            Condition := Evaluate_BA_Value_Expression
              (Node             => BATN.Value_Expression
                 (BATN.Condition (BATN.Behavior_Condition (Transition_Node))),
               Subprogram_Root  => S,
               Declarations     => WDeclarations,
               Statements       => WStatements);

            if Present (BATN.Behavior_Action_Block (Transition_Node))
              and then Present (BATN.Behav_Acts
                                (BATN.Behavior_Action_Block (Transition_Node)))
            then

               N := Message_Comment (" Mapping of actions ");
               CTU.Append_Node_To_List (N, If_Statements);

               Map_C_Behavior_Action_Block
                 (BATN.Behavior_Action_Block (Transition_Node),
                  S, WDeclarations, If_Statements);
            end if;

            --  ii) Update the current state
            Update_Current_State (E, BA, Transition_Node, If_Statements);

            CTU.Append_Node_To_List
              (CTU.Make_If_Statement
                 (Condition       => Condition,
                  Statements      => If_Statements,
                  Else_Statements => Else_Stats),
               WStatements);
         else
            if not Present (Otherwise_Transition_Node) then

               --  i) Map actions of the transition without if statement
               if Present (BATN.Behavior_Action_Block (Transition_Node))
                 and then Present (BATN.Behav_Acts
                                   (BATN.Behavior_Action_Block
                                      (Transition_Node)))
               then

                  N := Message_Comment (" Mapping of actions ");
                  CTU.Append_Node_To_List (N, WStatements);

                  Map_C_Behavior_Action_Block
                    (BATN.Behavior_Action_Block (Transition_Node),
                     S, WDeclarations, WStatements);
               end if;

               --  ii) Update the current state
               Update_Current_State (E, BA, Transition_Node, WStatements);

            else
               Display_Error
                 ("As there is an otherwise transition then"
                  & " the other transition that starts from"
                  & " the same state called '"
                  & Get_Name_String
                    (BATN.Display_Name (BATN.First_Node
                     (BATN.Sources (Transition_Node))))
                  & "' must have a condition",
                  Fatal => True);
            end if;
         end if;

      elsif (BANu.Length (Sub_Transition_List) > 1) then
         Transition_Node := BATN.First_Node (Sub_Transition_List);

         if Present (BATN.Behavior_Condition (Transition_Node))
           and then Present (BATN.Condition
                             (BATN.Behavior_Condition (Transition_Node)))
         then
            Condition := Evaluate_BA_Value_Expression
              (Node             => BATN.Value_Expression
                 (BATN.Condition (BATN.Behavior_Condition (Transition_Node))),
               Subprogram_Root  => S,
               Declarations     => WDeclarations,
               Statements       => WStatements);

            if Present (BATN.Behavior_Action_Block (Transition_Node))
              and then Present (BATN.Behav_Acts
                                (BATN.Behavior_Action_Block
                                   (Transition_Node)))
            then
               N := Message_Comment (" Mapping of actions ");
               CTU.Append_Node_To_List (N, If_Statements);

               Map_C_Behavior_Action_Block
                 (BATN.Behavior_Action_Block (Transition_Node),
                  S, WDeclarations, If_Statements);
            end if;

            Update_Current_State (E, BA, Transition_Node, If_Statements);

            Transition_Node := BATN.Next_Node (Transition_Node);

            Else_Stats := Map_C_Transition_Node
              (Transition_Node, S, WDeclarations,
               WStatements, Else_Stats);

            CTU.Append_Node_To_List
              (CTU.Make_If_Statement
                 (Condition       => Condition,
                  Statements      => if_Statements,
                  Else_Statements => Else_Stats),
               WStatements);
         else
            Display_Error
              ("As there are more than one transition that starts from"
               & " the same state called '"
               & Get_Name_String
                 (BATN.Display_Name (BATN.First_Node
                  (BATN.Sources (Transition_Node))))
               & "', all these transitions must have conditions",
               Fatal => True);
         end if;
      end if;

   end Map_C_A_List_Of_Transitions;

   ---------------------------------------------------------
   -- Examine_Current_State_Until_Reaching_Complete_State --
   ---------------------------------------------------------

   procedure Examine_Current_State_Until_Reaching_Complete_State
     (S                         : Node_Id;
      BA                        : Node_Id;
      WDeclarations             : List_Id;
      WStatements               : List_Id)
   is
      While_Statements     : constant List_Id := New_List
        (CTN.K_Statement_List);
      State, List_Node     : Node_Id;
      Switch_Alternatives  : constant List_Id := New_List
        (CTN.K_Alternatives_List);
      Switch_Statements    : List_Id;
      Switch_Labels        : List_Id;

      N                            : Node_Id;
      Behav_Transition             : Node_Id;
      Transition_Node              : Node_Id;
      Otherwise_Transition_Node    : Node_Id := No_Node;
      E                            : constant Node_Id :=
        AIN.Parent_Subcomponent (S);
      Sub_Transition_List          : List_Id;
      Source                       : Node_Id;
      Condition     : Node_Id;
   begin
      --  Make while (__po_hi_producer_current_state.kind ==
      --  __po_hi_execution)

      Condition := CTU.Make_Expression
        (Left_Expr  => Make_Member_Designator
           (Defining_Identifier => Make_Defining_Identifier
             (MN (M_kind)),
            Aggregate_Name      => Make_Defining_Identifier
              (Map_C_Variable_Name (E, Current_State => True))),
         Operator   => CTU.Op_Equal_Equal,
         Right_Expr => RE (RE_Execution));

      --  Make a « switch case » statement on all the
      --  « execution » states

      State := BATN.First_Node (BATN.States (BA));

      while Present (State) loop
         if Behavior_State_Kind'Val (BATN.State_Kind (State))
           = BSK_No_Kind
         then
            List_Node := BATN.First_Node (BATN.Identifiers (State));

            while Present (List_Node) loop

               Switch_Statements := New_List (CTN.K_Statement_List);
               Switch_Labels := New_List (CTN.K_Label_List);
               Sub_Transition_List := BANu.New_List
                 (BATN.K_List_Id, No_Location);

               Otherwise_Transition_Node := No_Node;

               CTU.Append_Node_To_List
                 (Make_Defining_Identifier
                    (BATN.Display_Name (List_Node)),
                  Switch_Labels);

               --  /** go over all transitions that have « s1 »
               --  as source state. In the case of one transition
               --  without condition, we don't make an « if » statement,
               --  instead we map the actions and then we update the
               --  current state with the destination state of
               --  the transition. **/

               Behav_Transition := BATN.First_Node
                 (BATN.Transitions (BA));
               while Present (Behav_Transition) loop
                  Transition_Node := BATN.Transition (Behav_Transition);

                  if BATN.Kind (Transition_Node) =
                    BATN.K_Execution_Behavior_Transition
                  then
                     if BANu.Length (BATN.Sources (Transition_Node)) = 1
                     then
                        Source := BATN.First_Node
                          (BATN.Sources (Transition_Node));
                        if  (Standard.Utils.To_Upper
                             (BATN.Display_Name (List_Node)) =
                               Standard.Utils.To_Upper
                                 (BATN.Display_Name (Source)))
                        then
                           if Present (BATN.Behavior_Condition
                                       (Transition_Node)) and then
                             Present (BATN.Condition
                                      (BATN.Behavior_Condition
                                         (Transition_Node))) and then

                             BATN.Is_Otherwise
                               (BATN.Condition
                                  (BATN.Behavior_Condition
                                     (Transition_Node)))
                           then
                              Otherwise_Transition_Node :=
                                Transition_Node;
                           else
                              BATN.Set_Next_Node (Transition_Node, No_Node);
                              BANu.Append_Node_To_List
                                (Transition_Node,
                                 Sub_Transition_List);
                           end if;
                        end if;
                     end if;
                  end if;
                  Behav_Transition := BATN.Next_Node (Behav_Transition);
               end loop;

               Map_C_A_List_Of_Transitions
                 (S, BA, Otherwise_Transition_Node, Sub_Transition_List,
                  WDeclarations, Switch_Statements);

               N :=
                 Make_Switch_Alternative (Switch_Labels,
                                          Switch_Statements);

               CTU.Append_Node_To_List (N, Switch_Alternatives);

               List_Node := BATN.Next_Node (List_Node);
            end loop;
         end if;

         State := BATN.Next_Node (State);
      end loop;

      Append_Node_To_List
        (Make_Switch_Alternative (No_List, No_List),
         Switch_Alternatives);

      N :=
        Make_Switch_Statement
          (Expression   => Make_Member_Designator
             (Defining_Identifier => Make_Defining_Identifier
                (MN (M_Name)),
              Aggregate_Name      => Make_Defining_Identifier
                (Map_C_Variable_Name (E, Current_State => True))),
           Alternatives => Switch_Alternatives);

      CTU.Append_Node_To_List (N, While_Statements);

      CTU.Append_Node_To_List
        (CTU.Make_While_Statement
           (Condition  => Condition,
            Statements => While_Statements),
         WStatements);

   end Examine_Current_State_Until_Reaching_Complete_State;

   -------------------------------------
   -- Make_BA_Initialization_Function --
   -------------------------------------

   procedure Make_BA_Initialization_Function
     (S            : Node_Id)
   is
      BA                           : Node_Id;
      N                            : Node_Id;
      Behav_Transition             : Node_Id;
      Transition_Node              : Node_Id;
      Otherwise_Transition_Node    : Node_Id := No_Node;
      Sub_Transition_List          : List_Id;
      Source                       : Node_Id;
      WStatements    : constant List_Id := New_List (CTN.K_Statement_List);
      WDeclarations  : constant List_Id := New_List (CTN.K_Declaration_List);
      At_Least_One_Dest_State_Is_Execution : Boolean := False;

   begin

--  8)
--        void producer_ba_initialization
--      (__po_hi_task_id self)
--  {
--
--  base_types__integer tmp;
--  test__ba__backend__alpha_type tmp2;
--
--  /*
--  /** 1) For all transitions that have the initial state as
--  a source state. In the case of a single transition without condtition,
--  we don't make an « if » statement, instead we map the actions and then
--  we update the __po_hi_producer_current_state with the destination
--  state of the transition.
--  **/
--
--  /*  Read the data from the port Data_In*/
--  __po_hi_gqueue_get_value (self, LOCAL_PORT (producer, data_in),
--                            &(__data_in_request));
--  __data_in_value =
--  __data_in_request.PORT_VARIABLE (producer, data_in);
--
--  if (__data_in_value <= 10)
--  {  /* actions */
--     .....
--  --  -- /* update the current state */
--    __po_hi_producer_current_state = __po_hi_producer_states_array[1];
--  }
--  else if (__data_in_value > 10)
--  {  /* actions */
--
--
--     /* update the current state */
--    __po_hi_producer_current_state = __po_hi_producer_states_array[2];
--  }/** we add « else » statement if there is a transition with
--     « otherwise » condition **/
--
      BA := Get_Behavior_Specification (S);

      Map_C_Behavior_Variables (S, WDeclarations);

--  1) For all transitions that have the initial state as a source state.
--  In the case of a single transition without condition, we don't make an
--  « if » statement, instead we map the actions and then we update the
--  current_state with the destination state of the transition.

      N := Message_Comment
        ("1) For all transitions that have the initial"
         & " state as a source state."
         & " In the case of a single transition without condition,"
         & " we don't make an 'if' statement,"
         & " instead we map the actions and then we update the"
         & " current_state with the destination state of the transition.");

      CTU.Append_Node_To_List (N, WStatements);

      Behav_Transition := BATN.First_Node (BATN.Transitions (BA));
      Sub_Transition_List := BANu.New_List (BATN.K_List_Id, No_Location);

      while Present (Behav_Transition) loop
         Transition_Node := BATN.Transition (Behav_Transition);

         if BATN.Kind (Transition_Node) =
              BATN.K_Execution_Behavior_Transition
         then
            if BANu.Length (BATN.Sources (Transition_Node)) = 1 then
               Source := BATN.First_Node (BATN.Sources (Transition_Node));

               if Behavior_State_Kind'Val (Search_State_Kind (BA, Source))
                 = BSK_Initial
               then
                  if not At_Least_One_Dest_State_Is_Execution then

                     At_Least_One_Dest_State_Is_Execution :=
                       Behavior_State_Kind'Val
                         (Search_State_Kind
                            (BA, BATN.Destination (Transition_Node)))
                         = BSK_No_Kind;

                  end if;

                  if Present (BATN.Behavior_Condition (Transition_Node))
                    and then BATN.Is_Otherwise
                      (BATN.Condition
                         (BATN.Behavior_Condition (Transition_Node)))
                  then
                     Otherwise_Transition_Node := Transition_Node;
                  else
                     BANu.Append_Node_To_List
                       (Transition_Node, Sub_Transition_List);
                  end if;
               end if;
            else
               Display_Error
                 ("Many sources states for a transition are not supported",
               Fatal => True);
            end if;
         else
            Display_Error
              ("Mode Transition is not supported",
               Fatal => True);
         end if;

         Behav_Transition := BATN.Next_Node (Behav_Transition);
      end loop;

      Map_C_A_List_Of_Transitions
        (S, BA, Otherwise_Transition_Node, Sub_Transition_List,
         WDeclarations, WStatements);

--  /** 2) Now we examine __po_hi_producer_current_state if it is not a
--    « complete » state, i.e. it is an « execution » state, then we
--  should proceed transitions until reaching a « complete » state. **/
--
--  while (__po_hi_producer_current_state.kind == __po_hi_execution)
--  {
--  --  --  /** we make a « switch case » statement on all the
--  « execution » states **/
--  switch (__po_hi_producer_current_state.name)
--  {
--  --  --  case s1:
--  /** go over all transitions that have « s1 » as source state.
--  In the case of one transition without condition, we don't make an
--  « if » statement, instead we map the actions and then we update the
--  __po_hi_producer_current_state with the destination state of the
--   transition. **/
--
--  if (tmp < 10)
--  { /* actions */
--    ......
--  --  --    /* update the current state */
--   __po_hi_producer_current_state = __po_hi_producer_states_array[2];
--  }
--  else if (tmp >= 12 && tmp < 16)
--  { /* actions */
--    .....
--  --  --    /* update the current state */
--  __po_hi_producer_current_state = __po_hi_producer_states_array[1];
--  }
--  /** There is a transition with « otherwise » condition, then we add
--     « else » statement **/
--  else
--  { /* actions */
--    ......
--
--  /* update the current state */
--   __po_hi_producer_current_state = __po_hi_producer_states_array[3];
--  }
--  break;
--
--  case s2:
--
--  /** go over all transitions that have « s2 » as source state.
--  In the case of one transition without condition, we don't make an
--  « if » statement, instead we map the actions and the we update the
--  __po_hi_producer_current_state with the destination state of the
--   transition. Idem que « s1 » **/
--
--  /* actions */
--   ....
--
--  /* update the current state */
--  __po_hi_producer_current_state = __po_hi_producer_states_array[3];
--
--  break;
--  --  --  default: // do nothing;
--  break;
--  }
--     }
--
--  }
--
      if At_Least_One_Dest_State_Is_Execution then

         N := Message_Comment
           ("2) Now we examine current state"
            & " if it is not a 'complete' state, i.e. it is an 'execution'"
            & "  state, then we should proceed transitions until reaching"
            & " a 'complete' state.");

         CTU.Append_Node_To_List (N, WStatements);

         Examine_Current_State_Until_Reaching_Complete_State
           (S, BA, WDeclarations, WStatements);

      end if;

      N := Make_Function_Implementation
        (Specification => Make_Specification_Of_BA_Related_Function
           (S, BA_Initialization => True),
         Declarations  => WDeclarations,
         Statements    => WStatements);

      Append_Node_To_List (N, CTN.Declarations (Current_File));
   end Make_BA_Initialization_Function;

   -----------------------------------------------
   -- Make_Specification_Of_BA_Related_Function --
   -----------------------------------------------

   function Make_Specification_Of_BA_Related_Function
     (S                          : Node_Id;
      BA_Body                    : Boolean := False;
      BA_Initialization          : Boolean := False;
      States_Initialization      : Boolean := False;
      Update_Next_Complete_State : Boolean := False) return Node_Id
   is
      N, N1  : Node_Id;
      E      : constant Node_Id := AIN.Parent_Subcomponent (S);
      P      : constant Supported_Thread_Dispatch_Protocol :=
           Get_Thread_Dispatch_Protocol (S);
      Parameter_List  : List_Id;
      Nb_On_Dispatch_Transitions : constant Unsigned_Long_Long :=
        Compute_Nb_On_Dispatch_Transitions (S);
   begin

      if BA_Body then
         Parameter_List := New_List (CTN.K_List_Id);
         N :=
           Make_Parameter_Specification
             (Make_Defining_Identifier (PN (P_Self)),
              Parameter_Type => RE (RE_Task_Id));
         Append_Node_To_List (N, Parameter_List);
      elsif BA_Initialization then
         Parameter_List := New_List (CTN.K_List_Id);
         N :=
           Make_Parameter_Specification
             (Make_Defining_Identifier (PN (P_Self)),
              Parameter_Type => RE (RE_Task_Id));
         Append_Node_To_List (N, Parameter_List);
      end if;

      if P = Thread_Periodic
        or else (P = Thread_Sporadic and then
                 Nb_On_Dispatch_Transitions = 1)
      then
         if States_Initialization then
            Parameter_List := No_List;
         end if;
      elsif P = Thread_Sporadic and then
        Nb_On_Dispatch_Transitions > 1
      then
         if States_Initialization or else Update_Next_Complete_State then
            Parameter_List := Make_List_Id
              (Make_Parameter_Specification
                 (Defining_Identifier =>
                      Make_Defining_Identifier
                    (VN (V_Next_Complete_State)),
                  Parameter_Type      =>
                    Make_Pointer_Type
                      (RE (RE_Ba_Automata_State_T))));
         elsif BA_Body then
            N :=
              Make_Parameter_Specification
                (Make_Defining_Identifier (VN (V_Next_Complete_State)),
                 Parameter_Type => CTU.Make_Pointer_Type
                   (RE (RE_Ba_Automata_State_T)));
            Append_Node_To_List (N, Parameter_List);

            N :=
              Make_Parameter_Specification
                (Make_Defining_Identifier (VN (V_Index_Transition_To_Execute)),
                 Parameter_Type => RE (RE_Int32_T));
            Append_Node_To_List (N, Parameter_List);
         elsif BA_Initialization then
            N :=
              Make_Parameter_Specification
                (Make_Defining_Identifier (VN (V_Next_Complete_State)),
                 Parameter_Type => CTU.Make_Pointer_Type
                   (RE (RE_Ba_Automata_State_T)));
            Append_Node_To_List (N, Parameter_List);
         end if;
      end if;

      --  add data subcomponents of the thread to the call_parameters
      --  of the procedure <<thread_instance_name>>_ba_body
      if BA_Body or else BA_Initialization then
         if not AINU.Is_Empty (AIN.Subcomponents (S)) then
            N1 := AIN.First_Node (AIN.Subcomponents (S));

            while Present (N1) loop
               if AINU.Is_Data (AIN.Corresponding_Instance (N1)) then

                  N :=
                    Make_Parameter_Specification
                      (Map_C_Defining_Identifier (N1),
                       Parameter_Type =>
                         CTU.Make_Pointer_Type
                           (Map_C_Data_Type_Designator
                              (AIN.Corresponding_Instance (N1))));

                  Append_Node_To_List (N, Parameter_List);

               end if;
               N1 := AIN.Next_Node (N1);
            end loop;
         end if;
      end if;

      if BA_Body then
         N := Make_Function_Specification
           (Defining_Identifier => Make_Defining_Identifier
              (Map_C_BA_Related_Function_Name (E, BA_Body => True)),
            Parameters          => Parameter_List,
            Return_Type         => New_Node (CTN.K_Void));
      elsif BA_Initialization then
         N := Make_Function_Specification
           (Defining_Identifier => Make_Defining_Identifier
              (Map_C_BA_Related_Function_Name (E, BA_Initialization => True)),
            Parameters          => Parameter_List,
            Return_Type         => New_Node (CTN.K_Void));
      elsif States_Initialization then
         N := Make_Function_Specification
           (Defining_Identifier => Make_Defining_Identifier
              (Map_C_BA_Related_Function_Name
                   (E, States_Initialization => True)),
            Parameters          => Parameter_List,
            Return_Type         => New_Node (CTN.K_Void));
      elsif Update_Next_Complete_State then
         N := Make_Function_Specification
           (Defining_Identifier => Make_Defining_Identifier
              (Map_C_BA_Related_Function_Name
                   (E, Update_Next_Complete_State => True)),
            Parameters          => Parameter_List,
            Return_Type         => New_Node (CTN.K_Void));
      end if;
      return N;
   end Make_Specification_Of_BA_Related_Function;

   ----------------------------------------------
   -- Map_C_Implementation_of_BA_Body_Function --
   ----------------------------------------------

   procedure Map_C_Implementation_of_BA_Body_Function
     (S            : Node_Id;
      Declarations : List_Id;
      Statements   : List_Id)
   is
      N  : Node_Id;
   begin

      N := Make_Function_Implementation
        (Specification => Make_Specification_Of_BA_Related_Function
           (S, BA_Body => True),
         Declarations  => Declarations,
         Statements    => Statements);

      Append_Node_To_List (N, CTN.Declarations (Current_File));

   end Map_C_Implementation_of_BA_Body_Function;

   -----------------------------------------------
   -- Make_BA_Body_Function_For_Sporadic_Thread --
   -----------------------------------------------

   procedure Make_BA_Body_Function_For_Sporadic_Thread
     (S            : Node_Id;
      Declarations : List_Id;
      Statements   : List_Id)
   is
      BA                         : Node_Id;
      State, List_Node             : Node_Id;
      Switch_Alternatives          : constant List_Id := New_List
        (CTN.K_Alternatives_List);
      Switch_Statements            : List_Id;
      Switch_Labels                : List_Id;

      N                            : Node_Id;
      Behav_Transition             : Node_Id;
      Transition_Node              : Node_Id;
      E                            : constant Node_Id :=
        AIN.Parent_Subcomponent (S);
      Sub_Transition_List          : List_Id;
      Source                       : Node_Id;
   begin

      --  /* 1) we make switch case statement on all complete states from
      --  which on dispatch transitions are started */

      BA := Get_Behavior_Specification (S);

      Map_C_Behavior_Variables (S, Declarations);

      Behav_Transition := BATN.First_Node (BATN.Transitions (BA));

      N := Message_Comment
        ("1) we make switch case statement on all complete states "
         & " from which 'on dispatch' transitions are started.");
      CTU.Append_Node_To_List (N, Statements);

      --  Make a « switch case » statement on all the
      --  « complete » states

      State := BATN.First_Node (BATN.States (BA));

      while Present (State) loop
         if Behavior_State_Kind'Val (BATN.State_Kind (State))
           = BSK_Initial_Complete
           or else Behavior_State_Kind'Val (BATN.State_Kind (State))
             = BSK_Initial_Complete_Final
           or else Behavior_State_Kind'Val (BATN.State_Kind (State))
             = BSK_Complete
           or else Behavior_State_Kind'Val (BATN.State_Kind (State))
             = BSK_Complete_Final
         then

            List_Node := BATN.First_Node (BATN.Identifiers (State));

            while Present (List_Node) loop
               Switch_Statements := New_List (CTN.K_Statement_List);
               Switch_Labels := New_List (CTN.K_Label_List);

               Sub_Transition_List := BANu.New_List
                 (BATN.K_List_Id, No_Location);

               CTU.Append_Node_To_List
                 (Make_Defining_Identifier
                    (BATN.Display_Name (List_Node)),
                  Switch_Labels);

               --  /** go over all « on dispatch » transitions that have
               --  « BATN.Display_Name (List_Node) » as source state.
               --  Then we make an "if" on the index of "on dispatch"
               --  transition to execute (index_transition_to_execute)
               --  received from activity.c
               --  if (index_transition_to_execute == 1)
               --  for each on dispatch transition, Actions are mapped
               --  in the if block statements.
               --  then we update the current state with the destination
               --  state of the transition. **/
               --

               Behav_Transition := BATN.First_Node
                 (BATN.Transitions (BA));
               while Present (Behav_Transition) loop

                  Transition_Node := BATN.Transition (Behav_Transition);

                  if BATN.Kind (Transition_Node) =
                    BATN.K_Execution_Behavior_Transition
                  then
                     if BANu.Length (BATN.Sources (Transition_Node)) = 1
                     then
                        Source := BATN.First_Node
                          (BATN.Sources (Transition_Node));
                        if  (Standard.Utils.To_Upper
                             (BATN.Display_Name (List_Node)) =
                               Standard.Utils.To_Upper
                                 (BATN.Display_Name (Source)))
                        then
                           if Present (BATN.Behavior_Condition
                                       (Transition_Node)) and then
                             Present (BATN.Condition
                                      (BATN.Behavior_Condition
                                         (Transition_Node))) and then

                             BATN.Kind
                               (BATN.Condition
                                  (Behavior_Condition (Transition_Node)))
                               = BATN.K_Dispatch_Condition_Thread
                           then
                              BATN.Set_Next_Node (Transition_Node, No_Node);
                              BANu.Append_Node_To_List
                                (Transition_Node,
                                 Sub_Transition_List);
                           end if;
                        end if;
                     end if;
                  end if;
                  Behav_Transition := BATN.Next_Node (Behav_Transition);
               end loop;

               Map_C_A_List_Of_On_Dispatch_Transitions
                 (S, BA, Sub_Transition_List,
                  Declarations, Switch_Statements);

               N :=
                 Make_Switch_Alternative (Switch_Labels,
                                          Switch_Statements);

               CTU.Append_Node_To_List (N, Switch_Alternatives);

               List_Node := BATN.Next_Node (List_Node);
            end loop;
         end if;

         State := BATN.Next_Node (State);
      end loop;

      Append_Node_To_List
        (Make_Switch_Alternative (No_List, No_List),
         Switch_Alternatives);

      N :=
        Make_Switch_Statement
          (Expression   => Make_Member_Designator
             (Defining_Identifier => Make_Defining_Identifier
                (MN (M_Name)),
              Aggregate_Name      => Make_Defining_Identifier
                (Map_C_Variable_Name (E, Current_State => True))),
           Alternatives => Switch_Alternatives);

      CTU.Append_Node_To_List (N, Statements);

      --  2) Now we examine __po_hi_producer_current_state if
      --  it is not a « complete » state, i.e. it is an « execution »
      --  state, then we should proceed transitions until reaching
      --  a « complete » state.
      --

      N := Message_Comment
        ("2) Now we examine current_state if "
           & "it is not a 'complete' state, i.e. it is an 'execution' "
         & " state, then we should proceed transitions until reaching"
         & " a 'complete' state.");
      CTU.Append_Node_To_List (N, Statements);

      Examine_Current_State_Until_Reaching_Complete_State
        (S, BA, Declarations, Statements);

      Map_C_Implementation_of_BA_Body_Function (S, Declarations, Statements);

   end Make_BA_Body_Function_For_Sporadic_Thread;

   -----------------------------------------------
   -- Make_BA_Body_Function_For_Periodic_Thread --
   -----------------------------------------------

   procedure Make_BA_Body_Function_For_Periodic_Thread
     (S            : Node_Id;
      Declarations : List_Id;
      Statements   : List_Id)
   is
      BA : constant Node_Id := Get_Behavior_Specification (S);
      E  : constant Node_Id := AIN.Parent_Subcomponent (S);
      State, State_Identifier    : Node_Id;
      Dispatch_Transition       : Node_Id;
      Switch_Alternatives       : constant List_Id :=
        New_List (CTN.K_Alternatives_List);
      Switch_Statements         : List_Id;
      Switch_Labels             : List_Id;
      Has_Dispatch_Transitions   : Boolean := False;
      Has_Execution_Destination  : Boolean := False;

      function Find_Dispatch_Transition
        (Identifier : Node_Id) return Node_Id
      is
         State_Name : constant Name_Id :=
           Standard.Utils.To_Upper (BATN.Display_Name (Identifier));
         Behav_Transition : Node_Id :=
           BATN.First_Node (BATN.Transitions (BA));
         Transition_Node, Source : Node_Id;
         Selected : Node_Id := No_Node;
      begin
         while Present (Behav_Transition) loop
            Transition_Node := BATN.Transition (Behav_Transition);
            if BATN.Kind (Transition_Node) =
              BATN.K_Execution_Behavior_Transition
              and then Present (BATN.Behavior_Condition (Transition_Node))
              and then Present (BATN.Condition
                (BATN.Behavior_Condition (Transition_Node)))
              and then BATN.Kind (BATN.Condition
                (BATN.Behavior_Condition (Transition_Node))) =
                BATN.K_Dispatch_Condition_Thread
            then
               --  A transition may list more than one source state.
               Source := BATN.First_Node (BATN.Sources (Transition_Node));
               while Present (Source) loop
                  if State_Name = Standard.Utils.To_Upper
                    (BATN.Display_Name (Source))
                  then
                     if No (Selected) or else
                       Has_Higher_Transition_Priority
                         (Transition_Node, Selected)
                     then
                        Selected := Transition_Node;
                     end if;
                     exit;
                  end if;
                  Source := BATN.Next_Node (Source);
               end loop;
            end if;
            Behav_Transition := BATN.Next_Node (Behav_Transition);
         end loop;
         return Selected;
      end Find_Dispatch_Transition;
   begin
      Map_C_Behavior_Variables (S, Declarations);

      State := BATN.First_Node (BATN.States (BA));
      while Present (State) loop
         if Behavior_State_Kind'Val (BATN.State_Kind (State)) in
           BSK_Initial_Complete | BSK_Initial_Complete_Final |
           BSK_Complete | BSK_Complete_Final
         then
            State_Identifier := BATN.First_Node (BATN.Identifiers (State));
            while Present (State_Identifier) loop
               Dispatch_Transition :=
                 Find_Dispatch_Transition (State_Identifier);
               if Present (Dispatch_Transition) then
                  Has_Dispatch_Transitions := True;
                  Switch_Statements := New_List (CTN.K_Statement_List);
                  Switch_Labels := New_List (CTN.K_Label_List);
                  Append_Node_To_List
                    (Make_Defining_Identifier
                       (BATN.Display_Name (State_Identifier)),
                     Switch_Labels);

                  if Present (BATN.Behavior_Action_Block (Dispatch_Transition))
                    and then Present (BATN.Behav_Acts
                      (BATN.Behavior_Action_Block (Dispatch_Transition)))
                  then
                     Map_C_Behavior_Action_Block
                       (BATN.Behavior_Action_Block (Dispatch_Transition),
                        S, Declarations, Switch_Statements);
                  end if;
                  Update_Current_State
                    (E, BA, Dispatch_Transition, Switch_Statements);

                  Append_Node_To_List
                    (Make_Switch_Alternative
                       (Switch_Labels, Switch_Statements),
                     Switch_Alternatives);

                  if Behavior_State_Kind'Val
                    (Search_State_Kind
                       (BA, BATN.Destination (Dispatch_Transition))) =
                    BSK_No_Kind
                  then
                     Has_Execution_Destination := True;
                  end if;
               end if;
               State_Identifier := BATN.Next_Node (State_Identifier);
            end loop;
         end if;
         State := BATN.Next_Node (State);
      end loop;

      if Has_Dispatch_Transitions then
         Append_Node_To_List
           (Message_Comment
              ("Select the highest-priority on-dispatch transition from "
               & "the current complete state; take one dispatch "
               & "transition per call."),
            Statements);
         Append_Node_To_List
           (Make_Switch_Alternative (No_List, No_List),
            Switch_Alternatives);
         Append_Node_To_List
           (Make_Switch_Statement
              (Expression => Make_Member_Designator
                 (Defining_Identifier => Make_Defining_Identifier
                    (MN (M_Name)),
                  Aggregate_Name => Make_Defining_Identifier
                    (Map_C_Variable_Name (E, Current_State => True))),
               Alternatives => Switch_Alternatives),
            Statements);
      end if;

      if Has_Execution_Destination then
         Append_Node_To_List
           (Message_Comment
              ("Continue through execution states until a complete "
               & "state is reached, then wait for the next dispatch."),
            Statements);
         Examine_Current_State_Until_Reaching_Complete_State
           (S, BA, Declarations, Statements);
      end if;

      Map_C_Implementation_of_BA_Body_Function (S, Declarations, Statements);
   end Make_BA_Body_Function_For_Periodic_Thread;

   ----------------------------------------
   -- Map_C_Many_Transitions_Of_A_Thread --
   ----------------------------------------

   procedure Map_C_Many_Transitions_Of_A_Thread
     (S            : Node_Id;
      Declarations : List_Id;
      Statements   : List_Id)
   is
      P  : constant Supported_Thread_Dispatch_Protocol :=
        Get_Thread_Dispatch_Protocol (S);
      Nb_On_Dispatch_Transitions : constant Unsigned_Long_Long :=
        Compute_Nb_On_Dispatch_Transitions (S);
   begin

      Map_BA_States_To_C_Types (S);

      if P = Thread_Sporadic and then
        Nb_On_Dispatch_Transitions > 1
      then
         Make_Update_Next_Complete_State_Function (S);
      end if;

      Make_States_Initialization_Function (S);

      if Is_To_Make_Init_Sequence (S) then
         Make_BA_Initialization_Function (S);
      end if;

      if P = Thread_Periodic then

         Make_BA_Body_Function_For_Periodic_Thread
           (S, Declarations, Statements);

      elsif P = Thread_Sporadic then

         Make_BA_Body_Function_For_Sporadic_Thread
           (S, Declarations, Statements);
      end if;

   end Map_C_Many_Transitions_Of_A_Thread;

   ---------------------------------
   -- Map_C_Behavior_Action_Block --
   ---------------------------------

   procedure Map_C_Behavior_Action_Block
     (Node         : Node_Id;
      S            : Node_Id;
      Declarations : List_Id;
      Statements   : List_Id)
   is
      pragma Assert (BATN.Kind (Node) = BATN.K_Behavior_Action_Block);
   begin

      if Present (BATN.Behav_Acts (Node)) then
         Map_C_Behav_Acts (Node, S, Declarations, Statements);
      end if;

      --  Behavior_Time (Node) is not yet supported
      --
      --  if Present (Behavior_Time (Node)) then
      --
      --  end if;

   end Map_C_Behavior_Action_Block;

   ----------------------
   -- Map_C_Behav_Acts --
   ----------------------

   procedure Map_C_Behav_Acts
     (Node         : Node_Id;
      S            : Node_Id;
      Declarations : List_Id;
      WStatements  : List_Id)
   is
      pragma Assert (BATN.Kind (Node) = BATN.K_Behavior_Action_Block
                     or else BATN.Kind (Node) = K_Conditional_Statement
                     or else BATN.Kind (Node) = K_While_Cond_Structure
                     or else BATN.Kind (Node) = K_DoUntil_Cond_Structure
                     or else BATN.Kind (Node) = K_For_Cond_Structure
                     or else BATN.Kind (Node) = K_ForAll_Cond_Structure);

      pragma Assert (Present (BATN.Behav_Acts (Node)));

      Behav_actions   : Node_Id;
      Behav_action    : Node_Id;
   begin

      Behav_actions := BATN.Behav_Acts (Node);

      --  In the case of a sequence of behavior actions:
      --  It is mapped into a sequence of C-statement, i.e.
      --  each action is mapped to the corresponding C-statement.
      --  Statement sequence is writen in the same order as the
      --  action sequence.
      --
      if not BANu.Is_Empty (BATN.Behavior_Action_Sequence (Behav_actions))
      then

         Behav_action := BATN.First_Node
           (BATN.Behavior_Action_Sequence (Behav_actions));

         while Present (Behav_action) loop
            Map_C_Behavior_Action (Behav_action, S, Declarations, WStatements);
            Behav_action := BATN.Next_Node (Behav_action);
         end loop;
      end if;

      --  In the case of a set of behavior actions:
      --  we treat it as a sequence of behavior actions.
      --
      if not BANu.Is_Empty (BATN.Behavior_Action_Set (Behav_actions)) then
         Behav_action := BATN.First_Node
           (BATN.Behavior_Action_Set (Behav_actions));

         while Present (Behav_action) loop
            Map_C_Behavior_Action (Behav_action, S, Declarations, WStatements);
            Behav_action := BATN.Next_Node (Behav_action);
         end loop;
      end if;

      --  In the case of a single action: it is mapped
      --  into a C-statement.
      --
      if Present (BATN.Behavior_Action (Behav_actions)) and then
        BANu.Is_Empty (BATN.Behavior_Action_Sequence (Behav_actions))
        and then BANu.Is_Empty (BATN.Behavior_Action_Set (Behav_actions))
      then

         Map_C_Behavior_Action (BATN.Behavior_Action (Behav_actions), S,
                                Declarations, WStatements);
      end if;

   end Map_C_Behav_Acts;

   ---------------------------
   -- Map_C_Behavior_Action --
   ---------------------------

   procedure Map_C_Behavior_Action
     (Node         : Node_Id;
      S            : Node_Id;
      Declarations : List_Id;
      Statements   : List_Id)
   is
      pragma Assert (BATN.Kind (Node) = BATN.K_Behavior_Action);
      pragma Assert (BATN.Kind (BATN.Action (Node)) = BATN.K_If_Cond_Struct
                     or else BATN.Kind (BATN.Action (Node)) =
                       BATN.K_For_Cond_Structure
                     or else BATN.Kind (BATN.Action (Node)) =
                       BATN.K_While_Cond_Structure
                     or else BATN.Kind (BATN.Action (Node)) =
                       BATN.K_ForAll_Cond_Structure
                     or else BATN.Kind (BATN.Action (Node)) =
                       BATN.K_DoUntil_Cond_Structure
                     or else BATN.Kind (BATN.Action (Node)) =
                       BATN.K_Assignment_Action
                     or else BATN.Kind (BATN.Action (Node)) =
                       BATN.K_Communication_Action
                     or else BATN.Kind (BATN.Action (Node)) =
                       BATN.K_Timed_Act);

      Action_Node : constant Node_Id := BATN.Action (Node);
      N           : Node_Id;
   begin
      case BATN.Kind (Action_Node) is

         when K_If_Cond_Struct         =>
            Map_C_If_Cond_Struct (Action_Node, S, Declarations, Statements);

         when K_For_Cond_Structure     =>
            Map_C_For_or_ForAll_Cond_Struct
              (Action_Node, S, Declarations, Statements);

         when K_While_Cond_Structure   =>
            Map_C_While_Cond_Struct (Action_Node, S, Declarations, Statements);

         when K_ForAll_Cond_Structure  =>
            Map_C_For_or_ForAll_Cond_Struct
              (Action_Node, S, Declarations, Statements);

         when K_DoUntil_Cond_Structure =>
            Map_C_DoUntil_Cond_Struct
              (Action_Node, S, Declarations, Statements);

         when BATN.K_Assignment_Action      =>
            Map_C_Assignment_Action (Action_Node, S, Declarations, Statements);

         when K_Communication_Action   =>
            Map_C_Communication_Action (Action_Node, S,
                                        Declarations, Statements);

            --  when K_Timed_Act           =>
            --    Map_C_Timed_Action (Action_Node);

         when others                   =>
            N := Message_Comment ("Behavior Actions not yet mapped");
            CTU.Append_Node_To_List (N, Statements);
      end case;

   end Map_C_Behavior_Action;

   ----------------------
   -- Map_C_Elsif_stat --
   ----------------------

   function Map_C_Elsif_stat
     (Node         : Node_Id;
      S            : Node_Id;
      Declarations : List_Id;
      Statements   : List_Id;
      Else_st      : Node_Id) return List_Id
   is
      N                : Node_Id;
      Else_stats       : constant List_Id := New_List (CTN.K_Statement_List);
      elsif_statements : constant List_Id := New_List (CTN.K_Statement_List);
   begin
      N := BATN.Next_Node (Node);

      if Present (N) then
         Map_C_Behav_Acts
           (Node         => Node,
            S            => S,
            Declarations => Declarations,
            WStatements  => elsif_statements);

         CTU.Append_Node_To_List
           (CTU.Make_If_Statement
              (Condition       => Evaluate_BA_Value_Expression
                   (Node             => Logical_Expr (Node),
                    Subprogram_Root  => S,
                    Declarations     => Declarations,
                    Statements       => Statements),
               Statements      => elsif_statements,
               Else_Statements =>
                 Map_C_Elsif_stat
                   (Node         => N,
                    S            => S,
                    Declarations => Declarations,
                    Statements   => Statements,
                    Else_st      => Else_st)),
            Else_stats);
      else
         Map_C_Behav_Acts
           (Node         => Node,
            S            => S,
            Declarations => Declarations,
            WStatements  => elsif_statements);
         declare
            else_sts : constant List_Id := New_List (CTN.K_Statement_List);
         begin
            if Present (Else_st) then

               Map_C_Behav_Acts
                 (Node         => Else_st,
                  S            => S,
                  Declarations => Declarations,
                  WStatements  => else_sts);

               CTU.Append_Node_To_List
                 (CTU.Make_If_Statement
                    (Condition       => Evaluate_BA_Value_Expression
                         (Node             => Logical_Expr (Node),
                          Subprogram_Root  => S,
                          Declarations     => Declarations,
                          Statements       => Statements),
                     Statements      => elsif_statements,
                     Else_Statements => else_sts),
                  Else_stats);
            else
               CTU.Append_Node_To_List
                 (CTU.Make_If_Statement
                    (Condition       => Evaluate_BA_Value_Expression
                         (Node             => Logical_Expr (Node),
                          Subprogram_Root  => S,
                          Declarations     => Declarations,
                          Statements       => Statements),
                     Statements      => elsif_statements),
                  Else_stats);
            end if;
         end;
      end if;

      return Else_stats;

   end Map_C_Elsif_stat;

   --------------------------
   -- Map_C_If_Cond_Struct --
   --------------------------

   procedure Map_C_If_Cond_Struct
     (Node         : Node_Id;
      S            : Node_Id;
      Declarations : List_Id;
      Statements   : List_Id)
   is

      pragma Assert (BATN.Kind (Node) = K_If_Cond_Struct);

      pragma Assert (Present (Logical_Expr (If_Statement (Node))));
      pragma Assert (Present (Behav_Acts (If_Statement (Node))));

      Condition        : Node_Id;
      else_Statements  : List_Id;
      if_Statements    : constant List_Id := New_List (CTN.K_Statement_List);
      List_Node1       : Node_Id;
   begin

      Condition := Evaluate_BA_Value_Expression
        (Node             => Logical_Expr (If_Statement (Node)),
         Subprogram_Root  => S,
         Declarations     => Declarations,
         Statements       => Statements);

      Map_C_Behav_Acts
        (Node         => If_Statement (Node),
         S            => S,
         Declarations => Declarations,
         WStatements  => if_Statements);

      if not BANu.Is_Empty (Elsif_Statement (Node)) then

         List_Node1 := BATN.First_Node (Elsif_Statement (Node));

         else_statements := Map_C_Elsif_stat (List_Node1, S, Declarations,
                           Statements, Else_Statement (Node));
      else
         if Present (Else_Statement (Node)) then
            else_statements := New_List (CTN.K_Statement_List);
            Map_C_Behav_Acts
              (Node         => Else_Statement (Node),
               S            => S,
               Declarations => Declarations,
               WStatements  => else_statements);
         else
            else_statements := No_List;
         end if;
      end if;

      CTU.Append_Node_To_List
        (CTU.Make_If_Statement
           (Condition       => Condition,
            Statements      => if_Statements,
            Else_Statements => else_statements),
         Statements);

   end Map_C_If_Cond_Struct;

   -------------------------------------
   -- Map_C_For_or_ForAll_Cond_Struct --
   -------------------------------------

   procedure Map_C_For_or_ForAll_Cond_Struct
     (Node         : Node_Id;
      S            : Node_Id;
      Declarations : List_Id;
      Statements   : List_Id)
   is

      pragma Assert (BATN.Kind (Node) = K_For_Cond_Structure
                     or else BATN.Kind (Node) = K_ForAll_Cond_Structure);

      Used_Type       : Node_Id;
      Init_Value      : Node_Id;
      Upper_Value     : Node_Id;
      Element_Name    : Name_Id;
      Loop_Statements : constant List_Id := New_List (CTN.K_Statement_List);
      If_Statements   : constant List_Id := New_List (CTN.K_Statement_List);
      Stop_Statements : constant List_Id := New_List (CTN.K_Statement_List);
      Next_Statements : constant List_Id := New_List (CTN.K_Statement_List);
      Continue_Name   : constant Name_Id :=
        Get_String_Name ("_ba_for_continue");
      Iterator_Frame  : aliased BA_Integer_Iterator;
   begin

      if BATN.Kind (In_Element_Values (Node)) = BATN.K_Integer_Range then

         Used_Type := Map_Used_Type (BATN.Corresponding_Declaration
                                     (BATN.Classifier_Ref (Node)));

         Init_Value := Evaluate_BA_Integer_Value
           (BATN.Lower_Int_Val (In_Element_Values (Node)),
            S,
            Declarations,
            Statements);
         Upper_Value := Evaluate_BA_Integer_Value
           (BATN.Upper_Int_Val (In_Element_Values (Node)),
            S,
            Declarations,
            Statements);
         Element_Name := BATN.Display_Name (Element_Idt (Node));

         --  Keep the declared iterator type available while mapping its body,
         --  including nested loops and names that shadow an outer iterator.
         Iterator_Frame :=
           (Element_Name,
            AAN.Default_Instance
              (BATN.Corresponding_Declaration (BATN.Classifier_Ref (Node))),
            Current_Integer_Iterator);
         Current_Integer_Iterator := Iterator_Frame'Unchecked_Access;
         Map_C_Behav_Acts
           (Node         => Node,
            S            => S,
            Declarations => Declarations,
            WStatements  => Loop_Statements);
         Current_Integer_Iterator := Iterator_Frame.Previous;

         --  Test after the body, before incrementing the iterator. In
         --  particular, never increment a maximum-valued upper bound.
         --  Using >= also stops if the body reduces a variable upper bound.
         Append_Node_To_List
           (Message_Comment
              ("Stop at the inclusive upper bound before incrementing "
               & "to avoid integer overflow."),
            Loop_Statements);
         Append_Node_To_List
           (Make_Assignment_Statement
              (Variable_Identifier => Make_Defining_Identifier (Continue_Name),
               Expression          =>
                 Make_Literal (CV.New_Int_Value (0, 1, 10))),
            Stop_Statements);
         Append_Node_To_List
           (Make_Expression
              (Left_Expr => Make_Defining_Identifier (Element_Name),
               Operator  => CTU.Op_Plus_Plus),
            Next_Statements);
         Append_Node_To_List
           (Make_If_Statement
              (Condition => Make_Expression
                 (Left_Expr  => Make_Defining_Identifier (Element_Name),
                  Operator   => CTU.Op_Greater_Equal,
                  Right_Expr => Upper_Value),
               Statements      => Stop_Statements,
               Else_Statements => Next_Statements),
            Loop_Statements);

         --  The guard skips empty ranges and gives each nested loop its own
         --  scope for the iterator and continuation flag.
         Append_Node_To_List
           (Make_Variable_Declaration
              (Defining_Identifier => Make_Defining_Identifier (Element_Name),
               Used_Type           => Used_Type,
               Value               => Init_Value),
            If_Statements);
         Append_Node_To_List
           (Make_Variable_Declaration
              (Defining_Identifier => Make_Defining_Identifier (Continue_Name),
               Used_Type           => Make_Defining_Identifier
                 (Get_String_Name ("int")),
               Value               =>
                 Make_Literal (CV.New_Int_Value (1, 1, 10))),
            If_Statements);
         Append_Node_To_List
           (Make_While_Statement
              (Condition  => Make_Defining_Identifier (Continue_Name),
               Statements => Loop_Statements),
            If_Statements);
         --  Match the conversion performed by the iterator initialization,
         --  including mixed signed and unsigned types in an empty range.
         Append_Node_To_List
           (Make_If_Statement
              (Condition => Make_Expression
                 (Left_Expr  => Make_Type_Conversion
                    (Subtype_Mark => Used_Type,
                     Expression   => Init_Value),
                  Operator   => CTU.Op_Less_Equal,
                  Right_Expr => Upper_Value),
               Statements => If_Statements),
            Statements);

      else
         Display_Error ("In For/ForAll construct, Kinds"
                        & " other than K_Integer_Range for In_Element_Values"
                        & " are not yet supported", Fatal => True);
      end if;

   end Map_C_For_or_ForAll_Cond_Struct;

   -----------------------------
   -- Map_C_While_Cond_Struct --
   -----------------------------

   procedure Map_C_While_Cond_Struct
     (Node         : Node_Id;
      S            : Node_Id;
      Declarations : List_Id;
      Statements   : List_Id)
   is

      pragma Assert (BATN.Kind (Node) = K_While_Cond_Structure);

      pragma Assert (Present (Logical_Expr (Node)));
      pragma Assert (Present (Behav_Acts (Node)));

      Condition        : Node_Id;
      while_statements : constant List_Id := New_List (CTN.K_Statement_List);
   begin

      Condition := Evaluate_BA_Value_Expression
        (Node             => Logical_Expr (Node),
         Subprogram_Root  => S,
         Declarations     => Declarations,
         Statements       => Statements);

      Map_C_Behav_Acts
        (Node         => Node,
         S            => S,
         Declarations => Declarations,
         WStatements  => while_statements);

      CTU.Append_Node_To_List
        (CTU.Make_While_Statement
           (Condition  => Condition,
            Statements => while_statements),
         Statements);

   end Map_C_While_Cond_Struct;

   -------------------------------
   -- Map_C_DoUntil_Cond_Struct --
   -------------------------------

   procedure Map_C_DoUntil_Cond_Struct
     (Node         : Node_Id;
      S            : Node_Id;
      Declarations : List_Id;
      Statements   : List_Id)
   is
      pragma Assert (BATN.Kind (Node) = K_DoUntil_Cond_Structure);
      pragma Assert (Present (Logical_Expr (Node)));
      pragma Assert (Present (Behav_Acts (Node)));

      Condition       : Node_Id;
      Loop_Statements : constant List_Id := New_List (CTN.K_Statement_List);
      Done_Name       : constant Name_Id :=
        Get_String_Name ("_ba_do_until_done");
   begin
      Map_C_Behav_Acts
        (Node         => Node,
         S            => S,
         Declarations => Declarations,
         WStatements  => Loop_Statements);

      --  Condition evaluation may emit statements, such as port reads.
      --  Keep them after the body and inside the loop on every iteration.
      Condition := Evaluate_BA_Value_Expression
        (Node            => Logical_Expr (Node),
         Subprogram_Root => S,
         Declarations    => Declarations,
         Statements      => Loop_Statements);

      --  A C99 for initializer gives each loop its own flag and resets it
      --  whenever execution enters this construct, including nested loops.
      Append_Node_To_List
        (Message_Comment
           ("Execute the do-until body at least once; "
            & "test the exit condition after each iteration."),
         Statements);
      Append_Node_To_List
        (Make_For_Statement
           (Pre_Cond => Make_Variable_Declaration
              (Defining_Identifier => Make_Defining_Identifier (Done_Name),
               Used_Type           => Make_Defining_Identifier
                 (Get_String_Name ("int")),
               Value               =>
                 Make_Literal (CV.New_Int_Value (0, 1, 10))),
            Condition => Make_Expression
              (Left_Expr  => Make_Defining_Identifier (Done_Name),
               Operator   => CTU.Op_Equal_Equal,
               Right_Expr => Make_Literal (CV.New_Int_Value (0, 1, 10))),
            Post_Cond => Make_Assignment_Statement
              (Variable_Identifier => Make_Defining_Identifier (Done_Name),
               Expression          => Condition),
            Statements => Loop_Statements),
         Statements);
   end Map_C_DoUntil_Cond_Struct;

   --------------------------------
   -- Map_C_Communication_Action --
   --------------------------------

   procedure Map_C_Communication_Action
     (Node         : Node_Id;
      S            : Node_Id;
      Declarations : List_Id;
      Statements   : List_Id)
   is
      pragma Assert (BATN.Kind (Node) = BATN.K_Communication_Action);

      Called_Spg               : Node_Id;
      Called_Spg_Instance      : Node_Id;
      Called_Spg_Spec          : Node_Id;
      Var_identifier           : Node_Id;
      Call_Parameters          : List_Id;
      N, k                     : Node_Id;
      Param_Node               : Node_Id;
      decl                     : Node_Id;
      Called_Spg_Spec_Exist    : Boolean := False;
   begin

      if Kind (BATN.Identifier (Node)) = BATN.K_Name then
         if BANu.Length (BATN.Idt (BATN.Identifier (Node))) = 1 then
            Var_identifier := CTU.Make_Defining_Identifier
              (Name => BATN.Display_Name
                 (BATN.First_Node
                      (BATN.Idt (BATN.Identifier (Node)))),
               Pointer => False);
         else
            Var_identifier := Map_C_BA_Name
              (Node         => BATN.Identifier (Node),
               S            => S,
               Declarations => Declarations,
               Statements   => Statements);
         end if;

      elsif Kind (BATN.Identifier (Node)) = BATN.K_Data_Component_Reference
      then
         Var_identifier := Map_C_Data_Component_Reference
           (Node         => BATN.Identifier (Node),
            S            => S,
            Declarations => Declarations,
            Statements   => Statements);
      end if;

      case Communication_Kind'Val (Comm_Kind (Node)) is

         when CK_Exclamation      =>

            if Is_Subprogram_Call (Node) then

               --  This means we have a call to a subprogram
               --

               --  If not yet added to the declarations of the
               --  current source file we must add the definition
               --  of the called spg

               --  First, we search if the called Spg is already declared
               --  in the current file, i.e. the called spg is not the
               --  first time is called
               --
               decl := CTN.First_Node (CTN.Declarations (Current_File));
               while Present (decl) loop

                  if Kind (decl) = CTN.K_Function_Specification
                    and then
                      Get_Name_String
                        (Standard.Utils.To_Lower
                           (CTN.Name (CTN.Defining_Identifier
                            (decl))))
                    = Get_Name_String
                    (Standard.Utils.To_Lower
                       (CTN.Name (Var_identifier)))

                  then
                     Called_Spg_Spec_Exist := True;
                  end if;
                  exit when Called_Spg_Spec_Exist;
                  decl := CTN.Next_Node (decl);
               end loop;

               if not Called_Spg_Spec_Exist then

                  Called_Spg :=
                    BATN.Corresponding_Entity
                      (BATN.First_Node
                         (BATN.Idt (BATN.Identifier (Node))));

                  Called_Spg_Instance := AAN.Default_Instance (Called_Spg);

                  declare
                     Proxy_Instance : Node_Id;
                  begin
                     if AINU.Is_Thread (S) then
                        Proxy_Instance := S;
                     elsif AINU.Is_Subprogram (S) then
                        Proxy_Instance := Get_Container_Thread (S);
                     end if;

                     if AIN.Subcomponents (Proxy_Instance) = No_List then
                        AIN.Set_Subcomponents
                          (Proxy_Instance,
                           AINU.New_List (AIN.K_List_Id,
                                          AIN.Loc (Proxy_Instance)));
                     end if;

                     declare
                        The_Sub : constant Node_Id :=
                          AINU.New_Node (AIN.K_Subcomponent_Instance,
                                         AIN.Loc (Proxy_Instance));
                     begin
                        AIN.Set_Parent_Component
                          (The_Sub, Called_Spg_Instance);
                        AIN.Set_Corresponding_Instance
                          (The_Sub, Called_Spg_Instance);

                        AIN.Set_Parent_Subcomponent
                          (Called_Spg_Instance,
                           (Proxy_Instance));
                        AINU.Append_Node_To_list
                          (The_Sub,
                           AIN.Subcomponents (Proxy_Instance));
                     end;
                  end;

                  Called_Spg_Spec := Map_C_Subprogram_Spec
                    (Called_Spg_Instance);

                  Set_Defining_Identifier
                    (Called_Spg_Spec,
                     Var_identifier);

                  Append_Node_To_List
                    (Called_Spg_Spec,
                     CTN.Declarations (Current_File));
               end if;

               --  Then, call the function provided by the user in our
               --  subprogram.

               if BANu.Is_Empty (Subprogram_Parameter_List (Node)) then

                  N := Make_Call_Profile (Var_identifier);

               else

                  Call_Parameters := New_List (CTN.K_Parameter_List);
                  N := BATN.First_Node (Subprogram_Parameter_List (Node));

                  while Present (N) loop
                     Param_Node := Parameter (N);

                     case BATN.Kind (Param_Node) is

                     when K_Value_Expression =>

                        K := Evaluate_BA_Value_Expression
                          (Node             => Param_Node,
                           Is_Out_Parameter => BATN.Is_Out (N),
                           Subprogram_Root  => S,
                           Declarations     => Declarations,
                           Statements       => Statements,
                           Is_Put_Value_On_Port => True);

                        Append_Node_To_List
                          (K,
                           Call_Parameters);

                     when K_Name =>
                        Append_Node_To_List
                          (Map_C_BA_Name
                             (Node             => Param_Node,
                              Is_Out_Parameter => BATN.Is_Out (N),
                              S                => S,
                              Declarations     => Declarations,
                              Statements       => Statements),
                           Call_Parameters);

                     when K_Data_Component_Reference =>
                        Append_Node_To_List
                          (Map_C_Data_Component_Reference
                             (Node             => Param_Node,
                              Is_Out_Parameter => BATN.Is_Out (N),
                              S                => S,
                              Declarations     => Declarations,
                              Statements       => Statements),
                           Call_Parameters);

                     when others =>
                        Display_Error ("Other param label Kinds are not"
                                       & " supported", Fatal => True);
                     end case;

                     N := BATN.Next_Node (N);

                  end loop;

                  N := Make_Call_Profile
                    (Defining_Identifier => Var_identifier,
                     Parameters          => Call_Parameters);
               end if;

               CTU.Append_Node_To_List (N, Statements);

            else
               --  It is a port sending action

               if not BANu.Is_Empty (Subprogram_Parameter_List (Node))
                 and then BANu.Length (Subprogram_Parameter_List (Node)) = 1
               then
                  --  Declare request variable if it is not yet declared
                  Make_Request_Variable_Declaration
                    (Declarations,
                     BATN.Display_Name
                       (BATN.First_Node
                            (BATN.Idt (BATN.Identifier (Node)))));

                  Make_Output_Port_Name
                    (Node         => BATN.Identifier (Node),
                     S            => S,
                     Statements   => Statements);

                  Make_Put_Value_On_port (Node, S, Declarations, Statements);
               end if;

               --  Declare request variable if it is not yet declared
               Make_Request_Variable_Declaration
                 (Declarations,
                  BATN.Display_Name
                    (BATN.First_Node
                         (BATN.Idt (BATN.Identifier (Node)))));

               Make_Send_Output_Port (Node, S, Statements);

            end if;

         when CK_Interrogative    =>
            if No (Target (Node)) then
               Make_Next_Value_of_Port
                 (BATN.First_Node (BATN.Idt (BATN.Identifier (Node))),
                  S, Statements);
            else
               CTU.Append_Node_To_List
                 (Make_Assignment_Statement
                    (Variable_Identifier => Map_C_Target
                         (Node         => BATN.Target (Node),
                          S            => S,
                          Declarations => Declarations,
                          Statements   => Statements),
                     Expression          => Make_Get_Value_of_Port
                       (BATN.First_Node (BATN.Idt (BATN.Identifier (Node))),
                        S, Declarations, Statements)),
                  Statements);

               Make_Next_Value_of_Port
                 (BATN.First_Node (BATN.Idt (BATN.Identifier (Node))),
                  S, Statements);
            end if;

            --  when CK_Greater_Greater  =>
            --  when CK_Exclamation_Greater =>
            --  when CK_Exclamation_Lesser  =>

         when others              =>
            Display_Error ("Other Communication Action Kinds"
                           & " are not yet supported",
                           Fatal => True);
      end case;

   end Map_C_Communication_Action;

   ---------------------------
   -- Make_Output_Port_Name --
   ---------------------------

   procedure Make_Output_Port_Name
     (Node         : Node_Id;
      S            : Node_Id;
      Statements   : List_Id)
   is
      N                 : Node_Id;
      Statement_Exist   : Boolean := False;
      Stat              : Node_Id;
      Request_Name      : Name_Id;
      Idt               : Name_Id;
   begin
      if BATN.Kind (Node) = BATN.K_Identifier then
         Idt := BATN.Display_Name (Node);
      else
         Idt := BATN.Display_Name (BATN.First_Node
                                   (BATN.Idt (Node)));
      end if;

      Request_Name := Make_Request_Variable_Name_From_Port_Name (Idt);

      Stat := CTN.First_Node (Statements);
      while Present (Stat) loop

         if CTN.Kind (Stat) = CTN.K_Assignment_Statement
           and then
             CTN.Kind (CTN.Defining_Identifier (Stat))
               = CTN.K_Member_Designator
             and then
               CTN.Kind (CTN.Aggregate_Name
                         (CTN.Defining_Identifier (Stat)))
           = CTN.K_Defining_Identifier
         then
            if Get_Name_String
              (Standard.Utils.To_Lower
                 (CTN.Name
                  (CTN.Aggregate_Name (CTN.Defining_Identifier (Stat)))))
                = Get_Name_String
              (Standard.Utils.To_Lower (Request_Name))
              and then
                Get_Name_String
                  (Standard.Utils.To_Lower
                     (CTN.Name
                        (CTN.Defining_Identifier
                             (CTN.Defining_Identifier (Stat)))))
                  = Get_Name_String
              (Standard.Utils.To_Lower (MN (M_Port)))

            then
               Statement_Exist := True;
            end if;
         end if;

         exit when Statement_Exist;
         Stat := CTN.Next_Node (Stat);
      end loop;

      if not Statement_Exist then
         --  Generate the following code :
         --  request.port =
         --           REQUEST_PORT (thread_instance_name, port_name);

         N := Message_Comment (" The name of an output port is built"
                               & " from the thread_instance name"
                               & " and the port name using"
                               & " the REQUEST_PORT macro. ");
         Append_Node_To_List (N, Statements);

         CTU.Append_Node_To_List
           (Make_Assignment_Statement
              (Variable_Identifier => Make_Member_Designator
                   (Defining_Identifier =>
                        Make_Defining_Identifier (MN (M_Port)),
                    Aggregate_Name      =>
                      Make_Defining_Identifier
                        (Request_Name)),
               Expression          => Make_Call_Profile
                 (RE (RE_REQUEST_PORT),
                  Make_List_Id
                    (Make_Defining_Identifier
                         (Map_Thread_Port_Variable_Name (S)),
                     Make_Defining_Identifier (Idt)))),
            Statements);
      end if;

   end Make_Output_Port_Name;

   ----------------------------
   -- Make_Put_Value_On_port --
   ----------------------------

   procedure Make_Put_Value_On_port
     (Node         : Node_Id;
      S            : Node_Id;
      Declarations : List_Id;
      Statements   : List_Id)
   is
      N : Node_Id;
   begin
      N := Message_Comment (" The name of the corresponding"
                            & " port variable is built from"
                            & " the port name,"
                            & " following similar pattern. ");
      Append_Node_To_List (N, Statements);

      N := Make_Call_Profile
        (RE (RE_PORT_VARIABLE),
         Make_List_Id
           (Make_Defining_Identifier
                (Map_Thread_Port_Variable_Name (S)),
            Make_Defining_Identifier
              (BATN.Display_Name (BATN.First_Node
               (BATN.Idt (BATN.Identifier (Node)))))));

      CTU.Append_Node_To_List
        (Make_Assignment_Statement
           (Variable_Identifier => Make_Member_Designator
                (Defining_Identifier => N,
                 Aggregate_Name      =>
                   Make_Defining_Identifier
                     (Make_Request_Variable_Name_From_Port_Name
                        (BATN.Display_Name (BATN.First_Node
                         (BATN.Idt (BATN.Identifier (Node))))))),
            Expression          => Evaluate_BA_Value_Expression
              (Node             => Parameter
                   (BATN.First_Node
                        (Subprogram_Parameter_List (Node))),
               Subprogram_Root  => S,
               Declarations     => Declarations,
               Statements       => Statements,
               Is_Put_Value_On_Port => True)),
         Statements);

   end Make_Put_Value_On_port;

   ---------------------------
   -- Make_Send_Output_Port --
   ---------------------------

   procedure Make_Send_Output_Port
     (Node       : Node_Id;
      S          : Node_Id;
      Statements : List_Id)
   is
      N               : Node_Id;
      N1              : Name_Id;
      Call_Parameters : List_Id;
   begin

      N := Message_Comment (" Send the request through the thread "
                            & " *local* port,"
                            & " built from the instance name"
                            & " and the port name using"
                            & " the LOCAL_PORT macro. ");
      Append_Node_To_List (N, Statements);

      --  __po_hi_gqueue_store_out
      --    (self,
      --     LOCAL_PORT (<<thread_name>>, <<port_name>>),
      --     &__<<port_name>>_request);

      Call_Parameters := New_List (CTN.K_Parameter_List);

      Set_Str_To_Name_Buffer ("self");
      N1 := Name_Find;
      N := Make_Defining_Identifier (N1);
      Append_Node_To_List (N, Call_Parameters);

      Append_Node_To_List
        (Make_Call_Profile
           (RE (RE_Local_Port),
            Make_List_Id
              (Make_Defining_Identifier
                   (Map_Thread_Port_Variable_Name (S)),
               Make_Defining_Identifier
                 (BATN.Display_Name (BATN.First_Node
                  (BATN.Idt (BATN.Identifier (Node))))))),
         Call_Parameters);

      N :=
        Make_Variable_Address
          (Make_Defining_Identifier
             (Make_Request_Variable_Name_From_Port_Name
                (BATN.Display_Name (BATN.First_Node
                 (BATN.Idt (BATN.Identifier (Node)))))));
      Append_Node_To_List (N, Call_Parameters);

      N :=
        CTU.Make_Call_Profile
          (RE (RE_Gqueue_Store_Out),
           Call_Parameters);
      Append_Node_To_List (N, Statements);

      --  __po_hi_send_output
      --    (self,REQUEST_PORT(<<thread_name>>, <<port_name>>));

      Call_Parameters := New_List (CTN.K_Parameter_List);

      Append_Node_To_List (Make_Defining_Identifier (N1), Call_Parameters);

      Append_Node_To_List
        (Make_Call_Profile
           (RE (RE_REQUEST_PORT),
            Make_List_Id
              (Make_Defining_Identifier
                   (Map_Thread_Port_Variable_Name (S)),
               Make_Defining_Identifier
                 (BATN.Display_Name (BATN.First_Node
                  (BATN.Idt (BATN.Identifier (Node))))))),
         Call_Parameters);

      N := Make_Call_Profile
        (RE (RE_Send_Output),
         Call_Parameters);

      Append_Node_To_List (N, Statements);

   end Make_Send_Output_Port;

   ------------------------------------
   -- Map_C_Data_Component_Reference --
   ------------------------------------

   function Map_C_Data_Component_Reference
     (Node             : Node_Id;
      Is_Out_Parameter : Boolean := False;
      S                : Node_Id;
      Declarations     : List_Id;
      Statements       : List_Id) return Node_Id
   is
      pragma Assert (BATN.Kind (Node) = BATN.K_Data_Component_Reference);

      Var_identifier : Node_Id := No_Node;
      N              : Node_Id;
   begin
      if not BANu.Is_Empty (BATN.Identifiers (Node)) then
         N := BATN.First_Node (BATN.Identifiers (Node));
         Var_identifier := Map_C_BA_Name
           (Node             => N,
            Is_Out_Parameter => Is_Out_Parameter,
            S                => S,
            Declarations     => Declarations,
            Statements       => Statements);
         N := BATN.Next_Node (N);
         while Present (N) loop
            Var_identifier := CTU.Make_Member_Designator
              (Defining_Identifier => Map_C_BA_Name
                 (Node             => N,
                  Is_Out_Parameter => Is_Out_Parameter,
                  S                => S,
                  Declarations     => Declarations,
                  Statements       => Statements),
               Aggregate_Name      => Var_identifier);
            N := BATN.Next_Node (N);
         end loop;
      end if;

      return Var_identifier;
   end Map_C_Data_Component_Reference;

   -------------------
   -- Map_C_BA_Name --
   -------------------

   function Map_C_BA_Name
     (Node             : Node_Id;
      Is_Out_Parameter : Boolean := False;
      S                : Node_Id;
      Declarations     : List_Id;
      Statements       : List_Id) return Node_Id
   is
      use AAN;
      pragma Assert (BATN.Kind (Node) = BATN.K_Name);

      Var_identifier, Next_Ident  : Node_Id := No_Node;
      Corresponding_Entity        : Node_Id;
      N, N1                       : Node_Id;
      Is_Pointer, Next_Is_Pointer : Boolean := False;
   begin
      if not BANu.Is_Empty (BATN.Idt (Node)) then
         N := BATN.First_Node (BATN.Idt (Node));

         Corresponding_Entity := BATN.Corresponding_Entity (N);

         if present (Corresponding_Entity) then

            if AAN.Kind (Corresponding_Entity) = AAN.K_Parameter
              and then AAN.Is_Out (Corresponding_Entity)
            then

               Var_identifier := Evaluate_BA_Identifier
                 (Node             => N,
                  Is_Out_Parameter => True,
                  Subprogram_Root  => S,
                  Declarations     => Declarations,
                  Statements       => Statements);

               Is_Pointer := True;

            elsif AAN.Kind (Corresponding_Entity) = AAN.K_Port_Spec
              and then AAN.Is_Out (Corresponding_Entity)
            then
               --  Declare request variable if it is not yet declared
               Make_Request_Variable_Declaration
                 (Declarations,
                  BATN.Display_Name (N));

               Make_Output_Port_Name
                 (Node         => Node,
                  S            => S,
                  Statements   => Statements);

               --  N1 := Message_Comment (" The name of the corresponding"
               --                        & " port variable is built from"
               --                        & " the port name,"
               --                        & " following similar pattern. ");
               --  Append_Node_To_List (N1, Statements);

               N1 := Make_Call_Profile
                 (RE (RE_PORT_VARIABLE),
                  Make_List_Id
                    (Make_Defining_Identifier
                         (Map_Thread_Port_Variable_Name (S)),
                     Make_Defining_Identifier
                       (BATN.Display_Name (N))));

               Var_identifier := CTU.Make_Member_Designator
                 (Defining_Identifier => N1,
                  Aggregate_Name      =>
                    Make_Defining_Identifier
                      (Make_Request_Variable_Name_From_Port_Name
                           (BATN.Display_Name (N))));

            elsif AAN.Kind (Corresponding_Entity) = AAN.K_Subcomponent
              and then
                AINU.Is_Data (AIN.Corresponding_Instance
                              (Get_Subcomponent_Data_Instance
                                 (Corresponding_Entity, S)))
              and then Get_Data_Representation
                (AIN.Corresponding_Instance
                   (Get_Subcomponent_Data_Instance
                      (Corresponding_Entity, S))) = Data_Struct

            then
               Var_identifier := Evaluate_BA_Identifier
                 (Node             => N,
                  Is_Out_Parameter => Is_Out_Parameter,
                  Subprogram_Root  => S,
                  Declarations     => Declarations,
                  Statements       => Statements);
               Is_Pointer := True;
            end if;

         else
            Var_identifier := Evaluate_BA_Identifier
              (Node             => N,
               Is_Out_Parameter => Is_Out_Parameter,
               Subprogram_Root  => S,
               Declarations     => Declarations,
               Statements       => Statements);
            Is_Pointer := False;
         end if;

         N := BATN.Next_Node (N);

         while Present (N) loop

            Corresponding_Entity := BATN.Corresponding_Entity (N);
            if Present (Corresponding_Entity) then
               if AAN.Kind (Corresponding_Entity) = AAN.K_Parameter
                 and then AAN.Is_Out (Corresponding_Entity)
               then
                  Next_Ident := Evaluate_BA_Identifier
                    (Node             => N,
                     Is_Out_Parameter => True,
                     Subprogram_Root  => S,
                     Declarations     => Declarations,
                     Statements       => Statements);

                  Next_Is_Pointer := True;
               end if;
            else
               Next_Ident := Evaluate_BA_Identifier
                 (Node             => N,
                  Is_Out_Parameter => Is_Out_Parameter,
                  Subprogram_Root  => S,
                  Declarations     => Declarations,
                  Statements       => Statements);
               Next_Is_Pointer := False;
            end if;

            Var_identifier := CTU.Make_Member_Designator
              (Defining_Identifier => Next_Ident,
               Aggregate_Name      => Var_identifier,
               Is_Pointer          => Is_Pointer);

            Is_Pointer := Next_Is_Pointer;

            N := BATN.Next_Node (N);
         end loop;

         if not BANu.Is_Empty (BATN.Array_Index (Node)) then
            N := BATN.First_Node (BATN.Array_Index (Node));

            while Present (N) loop
               Var_identifier := Make_Array_Declaration
                 (Defining_Identifier => Var_identifier,
                  Array_Size => Evaluate_BA_Integer_Value
                    (Node         => N,
                     S            => S,
                     Declarations => Declarations,
                     Statements   => Statements));

               N := BATN.Next_Node (N);
            end loop;
         end if;
      end if;

      return Var_identifier;
   end Map_C_BA_Name;

   ------------------
   -- Map_C_Target --
   ------------------

   function Map_C_Target
     (Node         : Node_Id;
      S            : Node_Id;
      Declarations : List_Id;
      Statements   : List_Id) return Node_Id
   is
      use AAN;
      pragma Assert (BATN.Kind (Node) = BATN.K_Name or else
                     BATN.Kind (Node) = BATN.K_Data_Component_Reference);

      Var_identifier       : Node_Id;
      N                    : Node_Id;
      Corresponding_Entity : Node_Id;
   begin

      if BATN.Kind (Node) = BATN.K_Name then

         if not BANu.Is_Empty (BATN.Idt (Node)) then
            if BANu.Length (BATN.Idt (Node)) = 1 then
               --  We verify if target is
               --  an outgoing_subprogram_parameter_identifier
               --  in this case the corresponding idendifier must
               --  be a pointer
               --
               Corresponding_Entity := BATN.Corresponding_Entity
                 (BATN.First_Node (BATN.Idt (Node)));

               if present (Corresponding_Entity) then

                  if (AAN.Kind (Corresponding_Entity) = AAN.K_Parameter
                    and then AAN.Is_Out (Corresponding_Entity))
                    or else
                      (AAN.Kind (Corresponding_Entity) = AAN.K_Subcomponent
                       and then
                       Component_Category'Val (Category (Corresponding_Entity))
                       = CC_Data)
                  then
                     Var_identifier :=
                       CTU.Make_Defining_Identifier
                         (Name           => BATN.Display_Name
                            (BATN.First_Node (BATN.Idt (Node))),
                          Pointer        => True);
                  elsif AAN.Kind (Corresponding_Entity) = AAN.K_Port_Spec
                    and then AAN.Is_Out (Corresponding_Entity)
                  then
                     --  Declare request variable if it is not yet declared
                     Make_Request_Variable_Declaration
                       (Declarations,
                        BATN.Display_Name (BATN.First_Node
                          (BATN.Idt (Node))));

                     Make_Output_Port_Name
                       (Node         => Node,
                        S            => S,
                        Statements   => Statements);

                     N := Message_Comment (" The name of the corresponding"
                                           & " port variable is built from"
                                           & " the port name,"
                                           & " following similar pattern. ");
                     Append_Node_To_List (N, Statements);

                     N := Make_Call_Profile
                       (RE (RE_PORT_VARIABLE),
                        Make_List_Id
                          (Make_Defining_Identifier
                               (Map_Thread_Port_Variable_Name (S)),
                           Make_Defining_Identifier
                             (BATN.Display_Name (BATN.First_Node
                              (BATN.Idt (Node))))));

                     Var_identifier := CTU.Make_Member_Designator
                       (Defining_Identifier => N,
                        Aggregate_Name      =>
                          Make_Defining_Identifier
                            (Make_Request_Variable_Name_From_Port_Name
                                 (BATN.Display_Name (BATN.First_Node
                                  (BATN.Idt (Node))))));
                  end if;

               else
                  Var_identifier :=
                    CTU.Make_Defining_Identifier
                      (Name           => BATN.Display_Name
                         (BATN.First_Node (BATN.Idt (Node))),
                       Pointer        => False);
               end if;
            else
               Var_identifier := Map_C_BA_Name
                 (Node             => Node,
                  S                => S,
                  Declarations     => Declarations,
                  Statements       => Statements);
            end if;

         end if;

      elsif BATN.Kind (Node) = BATN.K_Data_Component_Reference
      then
         Var_identifier := Map_C_Data_Component_Reference
           (Node             => Node,
            S                => S,
            Declarations     => Declarations,
            Statements       => Statements);
      end if;
      return Var_identifier;
   end Map_C_Target;

   -----------------------------
   -- Map_C_Assignment_Action --
   -----------------------------

   procedure Map_C_Assignment_Action
     (Node         : Node_Id;
      S            : Node_Id;
      Declarations : List_Id;
      Statements   : List_Id)
   is
      pragma Assert (BATN.Kind (Node) = BATN.K_Assignment_Action);
      Expr : Node_Id;
   begin

      Expr := Evaluate_BA_Value_Expression
        (Node             => BATN.Value_Expression (Node),
         Subprogram_Root  => S,
         Declarations     => Declarations,
         Statements       => Statements);

      CTU.Append_Node_To_List
        (CTU.Make_Assignment_Statement
           (Variable_Identifier => Map_C_Target
                (Node         => BATN.Target (Node),
                 S            => S,
                 Declarations => Declarations,
                 Statements   => Statements),
            Expression          => Expr),
         Statements);

      if BATN.Is_Any (Node) then
         Display_Error
           ("The mapping of (any) is not supported", Fatal => True);
      end if;

   end Map_C_Assignment_Action;

   ----------------------------------
   -- Evaluate_BA_Value_Expression --
   ----------------------------------

   function Evaluate_BA_Value_Expression
     (Node                 : Node_Id;
      Is_Out_Parameter     : Boolean := False;
      Subprogram_Root      : Node_Id := No_Node;
      Declarations         : List_Id;
      Statements           : List_Id;
      Is_Put_Value_On_Port : Boolean := False) return Node_Id
   is
      pragma Assert (BATN.Kind (Node) = BATN.K_Value_Expression);
      pragma Assert (not BANu.Is_Empty (BATN.Relations (Node)));

      N          : Node_Id;
      Left_Expr  : Node_Id;
      Right_Expr : Node_Id := No_Node;
      Op         : Operator_Type := Op_None;
      Expr       : Node_Id;
   begin

      N := BATN.First_Node (BATN.Relations (Node));

      Left_Expr := Evaluate_BA_Relation
        (N, Is_Out_Parameter, Subprogram_Root,
         Declarations, Statements, Is_Put_Value_On_Port);

      N := BATN.Next_Node (N);

      if No (N) then
         return Left_Expr;
      else

         while Present (N) loop

            case BATN.Kind (N) is
               when BATN.K_Relation =>
                  Right_Expr := Evaluate_BA_Relation
                    (Node             => N,
                     Subprogram_Root  => Subprogram_Root,
                     Declarations     => Declarations,
                     Statements       => Statements);

               when BATN.K_Operator =>
                  Op := Evaluate_BA_Operator (N);
               when others     => Display_Error
                    ("Not valid Value_Expression", Fatal => True);
            end case;

            if Right_Expr /= No_Node and then
              Op /= Op_None
            then
               Expr := Make_Expression (Left_Expr, Op, Right_Expr);
               Left_Expr := Expr;
               Op := Op_None;
               Right_Expr := No_Node;
            end if;

            N := BATN.Next_Node (N);
         end loop;

         return Expr;
      end if;

   end Evaluate_BA_Value_Expression;

   --------------------------
   -- Evaluate_BA_Relation --
   --------------------------

   function Evaluate_BA_Relation
     (Node                 : Node_Id;
      Is_Out_Parameter     : Boolean := False;
      Subprogram_Root      : Node_Id := No_Node;
      Declarations         : List_Id;
      Statements           : List_Id;
      Is_Put_Value_On_Port : Boolean := False) return Node_Id
   is
      pragma Assert (BATN.Kind (Node) = BATN.K_Relation);

      N          : Node_Id;
      Left_Expr  : Node_Id;
      Right_Expr : Node_Id := No_Node;
      Op         : Operator_Type := Op_None;
      Expr       : Node_Id;
   begin

      if not BANu.Is_Empty (BATN.Simple_Exprs (Node)) then

         N := BATN.First_Node (BATN.Simple_Exprs (Node));

         Left_Expr := Evaluate_BA_Simple_Expression
           (N, Is_Out_Parameter, Subprogram_Root,
            Declarations, Statements, Is_Put_Value_On_Port);

         N := BATN.Next_Node (N);

         if No (N) then
            return Left_Expr;
         else

            while Present (N) loop

               case BATN.Kind (N) is
                  when BATN.K_Simple_Expression =>
                     Right_Expr := Evaluate_BA_Simple_Expression
                       (Node             => N,
                        Subprogram_Root  => Subprogram_Root,
                        Declarations     => Declarations,
                        Statements       => Statements);
                  when BATN.K_Operator =>
                     Op := Evaluate_BA_Operator (N);
                  when others     => Display_Error
                       ("Not valid Relation", Fatal => True);
               end case;

               if Right_Expr /= No_Node and then
                 Op /= Op_None
               then
                  Expr := Make_Expression (Left_Expr, Op, Right_Expr);
                  Left_Expr := Expr;
                  Op := Op_None;
                  Right_Expr := No_Node;
               end if;

               N := BATN.Next_Node (N);
            end loop;

            return Expr;
         end if;

      end if;

      raise Program_Error;
      return No_Node;
   end Evaluate_BA_Relation;

   --------------------------
   -- Evaluate_BA_Operator --
   --------------------------

   function Evaluate_BA_Operator
     (Node             : Node_Id)
      return Operator_Type
   is
      pragma Assert (BATN.Kind (Node) = BATN.K_Operator);

   begin

      case Operator_Kind'Val (BATN.Operator_Category (Node)) is

         --  logical operator
         when OK_And              => return CTU.Op_And;
         when OK_Or               => return CTU.Op_Or;
         when OK_Xor              => Display_Error
              ("Not supported Operator", Fatal => True);
         when OK_Or_Else          => return CTU.Op_Or;
         when OK_And_Then         => return CTU.Op_And;

         --  relational_operator
         when OK_Equal            => return CTU.Op_Equal_Equal;
         when OK_Non_Equal        => return CTU.Op_Not_Equal;
         when OK_Less_Than        => return CTU.Op_Less;
         when OK_Less_Or_Equal    => return CTU.Op_Less_Equal;
         when OK_Greater_Than     => return CTU.Op_Greater;
         when OK_Greater_Or_Equal => return CTU.Op_Greater_Equal;

         --  unary_adding_opetor
         --  binary_adding_operator
         when OK_Plus             => return CTU.Op_Plus;
         when OK_Minus            => return CTU.Op_Minus;

         --  multiplying operator
         when OK_Multiply         => return CTU.Op_Asterisk;
         when OK_Divide           => return CTU.Op_Slash;
         when OK_Mod              => return CTU.Op_Modulo;
         when OK_Rem              => Display_Error
              ("Not supported Operator", Fatal => True);

         --  highest precedence operator
         when OK_Exponent         => Display_Error
              ("Not supported Operator", Fatal => True);
         when OK_Abs              => Display_Error
              ("Not supported Operator", Fatal => True);
         when OK_Not              => return CTU.Op_Not;

         when others              => Display_Error
              ("Not valid Operator", Fatal => True);
      end case;

      raise Program_Error;
      return CTU.Op_None;

   end Evaluate_BA_Operator;

   -----------------------------------
   -- Evaluate_BA_Simple_Expression --
   -----------------------------------

   function Evaluate_BA_Simple_Expression
     (Node                 : Node_Id;
      Is_Out_Parameter     : Boolean := False;
      Subprogram_Root      : Node_Id := No_Node;
      Declarations         : List_Id;
      Statements           : List_Id;
      Is_Put_Value_On_Port : Boolean := False) return Node_Id
   is
      pragma Assert (BATN.Kind (Node) = BATN.K_Simple_Expression);
      pragma Assert (not BANu.Is_Empty (BATN.Term_And_Operator (Node)));

      N          : Node_Id;
      Left_Expr  : Node_Id;
      Right_Expr : Node_Id := No_Node;
      Op         : Operator_Type := Op_None;
      Expr       : Node_Id;
   begin

      N := BATN.First_Node (BATN.Term_And_Operator (Node));

      Left_Expr := Evaluate_BA_Term
        (N, Is_Out_Parameter, Subprogram_Root,
         Declarations, Statements, Is_Put_Value_On_Port);

      N := BATN.Next_Node (N);

      if No (N) then
         return Left_Expr;
      else
         while Present (N) loop

            case BATN.Kind (N) is
               when BATN.K_Term =>
                  Right_Expr := Evaluate_BA_Term
                    (Node             => N,
                     Subprogram_Root  => Subprogram_Root,
                     Declarations     => Declarations,
                     Statements       => Statements);
               when BATN.K_Operator =>
                  Op := Evaluate_BA_Operator (N);
               when others     => Display_Error
                    ("Not valid BA_Term", Fatal => True);
            end case;

            if Right_Expr /= No_Node and then
              Op /= Op_None
            then
               Expr := Make_Expression (Left_Expr, Op, Right_Expr);
               Left_Expr := Expr;
               Op := Op_None;
               Right_Expr := No_Node;
            end if;

            N := BATN.Next_Node (N);
         end loop;

         return Expr;
      end if;

   end Evaluate_BA_Simple_Expression;

   --------------------------
   -- Ensure_Modulo_Helper --
   --------------------------

   function Ensure_Modulo_Helper (Type_Name : Name_Id) return Name_Id is
      Helper_Name : constant Name_Id := Get_String_Name
        ("ocarina_ba_mod_" & Get_Name_String (Type_Name));
      Existing_Node : Node_Id := CTN.First_Node
        (CTN.Declarations (Current_File));
      Params : constant List_Id := New_List (CTN.K_Parameter_List);
      Locals : constant List_Id := New_List (CTN.K_Declaration_List);
      Body_Stmts : constant List_Id := New_List (CTN.K_Statement_List);
      Unsigned_Stmts : constant List_Id := New_List (CTN.K_Statement_List);
      Minus_One_Stmts : constant List_Id := New_List (CTN.K_Statement_List);
      Adjust_Stmts : constant List_Id := New_List (CTN.K_Statement_List);

      function Identifier (Text : String) return Node_Id is
      begin
         return Make_Defining_Identifier (Get_String_Name (Text), False);
      end Identifier;

      function Integer_Literal (Value : Unsigned_Long_Long) return Node_Id is
      begin
         return Make_Literal (CV.New_Int_Value (Value, 1, 10));
      end Integer_Literal;

      function Result_Type return Node_Id is
      begin
         return Make_Defining_Identifier (Type_Name, False);
      end Result_Type;

      function Typed_Minus_One return Node_Id is
      begin
         --  A signed literal emits (type)-1. Casting an unparenthesized
         --  subtraction node here would instead emit (type)0 - 1.
         return Make_Type_Conversion
           (Result_Type, Make_Literal (CV.New_Int_Value (1, -1, 10)));
      end Typed_Minus_One;
   begin
      --  The caller supplies the promoted common C type, including aliases
      --  selected by the target compiler. Keep one helper for each alias.
      while Present (Existing_Node) loop
         if CTN.Kind (Existing_Node) = CTN.K_Function_Implementation
           and then CTN.Name
             (CTN.Defining_Identifier (CTN.Specification (Existing_Node))) =
               Helper_Name
         then
            return Helper_Name;
         end if;
         Existing_Node := CTN.Next_Node (Existing_Node);
      end loop;

      Append_Node_To_List
        (Make_Parameter_Specification
           (Identifier ("ba_mod_left"), Result_Type), Params);
      Append_Node_To_List
        (Make_Parameter_Specification
           (Identifier ("ba_mod_right"), Result_Type), Params);
      Append_Node_To_List
        (Make_Variable_Declaration
           (Identifier ("ba_mod_remainder"), Result_Type), Locals);
      Append_Node_To_List
        (Message_Comment
           ("Modulo evaluates each operand once and retains their common "
            & "C integer type. Unsigned remainder already has the required "
            & "nonnegative result."), Body_Stmts);
      Append_Node_To_List
        (Make_Return_Statement
           (Make_Expression
              (Identifier ("ba_mod_left"), Op_Modulo,
               Identifier ("ba_mod_right"))), Unsigned_Stmts);
      Append_Node_To_List
        (Make_If_Statement
           (Make_Expression
              (Typed_Minus_One, Op_Greater, Integer_Literal (0)),
            Unsigned_Stmts), Body_Stmts);

      Append_Node_To_List
        (Message_Comment
           ("A signed divisor of -1 always gives zero. Handle it before "
            & "remainder so the signed minimum does not trigger C's "
            & "unrepresentable minimum / -1 operation."), Body_Stmts);
      Append_Node_To_List
        (Make_Return_Statement (Integer_Literal (0)), Minus_One_Stmts);
      Append_Node_To_List
        (Make_If_Statement
           (Make_Expression
              (Identifier ("ba_mod_right"), Op_Equal_Equal,
               Typed_Minus_One), Minus_One_Stmts), Body_Stmts);
      Append_Node_To_List
        (Make_Assignment_Statement
           (Identifier ("ba_mod_remainder"), Make_Expression
              (Identifier ("ba_mod_left"), Op_Modulo,
               Identifier ("ba_mod_right"))), Body_Stmts);

      Append_Node_To_List
        (Message_Comment
           ("Only a nonzero remainder with the wrong sign needs correction. "
            & "The opposite signs make the addition safe. Compare with one "
            & "so this shared body also compiles for an unsigned alias."),
         Body_Stmts);
      Append_Node_To_List
        (Make_Assignment_Statement
           (Identifier ("ba_mod_remainder"), Make_Expression
              (Identifier ("ba_mod_remainder"), Op_Plus,
               Identifier ("ba_mod_right"))), Adjust_Stmts);
      Append_Node_To_List
        (Make_If_Statement
           (Make_Expression
              (Make_Expression
                 (Identifier ("ba_mod_remainder"), Op_Not_Equal,
                  Integer_Literal (0)), Op_And,
               Make_Expression
                 (Make_Expression
                    (Make_Expression
                       (Identifier ("ba_mod_remainder"), Op_Less,
                        Integer_Literal (1)), Op_And,
                     Make_Expression
                       (Identifier ("ba_mod_right"), Op_Greater,
                        Integer_Literal (0))), Op_Or,
                  Make_Expression
                    (Make_Expression
                       (Identifier ("ba_mod_remainder"), Op_Greater,
                        Integer_Literal (0)), Op_And,
                     Make_Expression
                       (Identifier ("ba_mod_right"), Op_Less,
                        Integer_Literal (1))))), Adjust_Stmts), Body_Stmts);
      Append_Node_To_List
        (Make_Return_Statement (Identifier ("ba_mod_remainder")), Body_Stmts);
      Append_Node_To_List
        (Make_Function_Implementation
           (Make_Function_Specification
              (Make_Defining_Identifier (Helper_Name, False), Params,
               Result_Type), Locals, Body_Stmts),
         CTN.Declarations (Current_File));
      return Helper_Name;
   end Ensure_Modulo_Helper;

   ----------------------
   -- Evaluate_BA_Term --
   ----------------------

   function Evaluate_BA_Term
     (Node                 : Node_Id;
      Is_Out_Parameter     : Boolean := False;
      Subprogram_Root      : Node_Id := No_Node;
      Declarations         : List_Id;
      Statements           : List_Id;
      Is_Put_Value_On_Port : Boolean := False) return Node_Id
   is
      pragma Assert (BATN.Kind (Node) = BATN.K_Term);
      pragma Assert (not BANu.Is_Empty (BATN.Factors (Node)));

      N          : Node_Id;
      Left_Expr  : Node_Id;
      Right_Expr : Node_Id := No_Node;
      Op         : Operator_Type := Op_None;
      Expr       : Node_Id;
      Last_Mod_Factor : Node_Id := No_Node;
      Need_Types : Boolean;
      Left_Type, Right_Type : BA_Integer_Type;
   begin

      --  Infer only the prefix needed by the last mod. An ordinary term,
      --  or a floating-point suffix after its last mod, follows the existing
      --  generation path without integer type analysis.
      N := BATN.First_Node (Factors (Node));
      while Present (N) loop
         if BATN.Kind (N) = BATN.K_Operator and then
           Operator_Kind'Val (BATN.Operator_Category (N)) = OK_Mod
         then
            Last_Mod_Factor := BATN.Next_Node (N);
         end if;
         N := BATN.Next_Node (N);
      end loop;
      Need_Types := Present (Last_Mod_Factor);
      N := BATN.First_Node (Factors (Node));
      if Need_Types then
         Left_Type := Integer_Type_Of (N, Subprogram_Root);
      end if;

      Left_Expr := Evaluate_BA_Factor (N, Is_Out_Parameter,
                                       Subprogram_Root,
                                       Declarations, Statements,
                                       Is_Put_Value_On_Port);

      N := BATN.Next_Node (N);

      if No (N) then
         return Left_Expr;
      else

         while Present (N) loop

            case BATN.Kind (N) is
               when BATN.K_Factor =>
                  Right_Expr := Evaluate_BA_Factor
                    (Node             => N,
                     Subprogram_Root  => Subprogram_Root,
                     Declarations     => Declarations,
                     Statements       => Statements);
                  if Need_Types then
                     Right_Type := Integer_Type_Of (N, Subprogram_Root);
                  end if;
               when BATN.K_Operator =>
                  Op := Evaluate_BA_Operator (N);
               when others     => Display_Error
                    ("Not valid BA_Term", Fatal => True);
            end case;

            if Right_Expr /= No_Node and then
              Op /= Op_None
            then
               if Need_Types then
                  Left_Type := Common_Integer_Type (Left_Type, Right_Type);
               end if;
               if Op = Op_Modulo then
                  Expr := Make_Call_Profile
                    (Make_Defining_Identifier
                       (Ensure_Modulo_Helper (Left_Type.Type_Name), False),
                     Make_List_Id (Left_Expr, Right_Expr));
               else
                  Expr := Make_Expression (Left_Expr, Op, Right_Expr);
               end if;
               if N = Last_Mod_Factor then
                  Need_Types := False;
               end if;
               Left_Expr := Expr;
               Op := Op_None;
               Right_Expr := No_Node;
            end if;

            N := BATN.Next_Node (N);
         end loop;

         return Expr;
      end if;

   end Evaluate_BA_Term;

   ------------------------
   -- Evaluate_BA_Factor --
   ------------------------

   function Evaluate_BA_Factor
     (Node                 : Node_Id;
      Is_Out_Parameter     : Boolean := False;
      Subprogram_Root      : Node_Id := No_Node;
      Declarations         : List_Id;
      Statements           : List_Id;
      Is_Put_Value_On_Port : Boolean := False) return Node_Id
   is
      pragma Assert (BATN.Kind (Node) = BATN.K_Factor);
      use type Ocarina.AADL_Values.Literal_Type;

      type Power_Operand is record
         Type_Name    : Name_Id := No_Name;
         Is_Signed    : Boolean := True;
         Literal_Node : Node_Id := No_Node;
         Is_Negative  : Boolean := False;
      end record;
      Signed_Max : constant Unsigned_Long_Long := 2 ** 63 - 1;
      Base_Info, Exponent_Info : Power_Operand;
      Base_Expr, Exponent_Expr : Node_Id;

      function Identifier (Spelling : String) return Node_Id is
      begin
         return Make_Defining_Identifier (Get_String_Name (Spelling), False);
      end Identifier;

      function Integer_Literal (Magnitude : Unsigned_Long_Long) return Node_Id
      is
      begin
         return Make_Literal (CV.New_Int_Value (Magnitude, 1, 10));
      end Integer_Literal;

      --  Resolve only declared scalar integers and literal operands.
      --  Parentheses are transparent; compound expressions need a separate
      --  type analysis and must not silently inherit their first variable.
      function Operand_Type
        (Operand_Node : Node_Id;
         Negate       : Boolean := False;
         Unary_Sign   : Boolean := False) return Power_Operand
      is
         Items : List_Id := No_List;
         First_Item, Variable_Node, Data_Instance : Node_Id;
         Result_Info : Power_Operand;
      begin
         case BATN.Kind (Operand_Node) is
            when BATN.K_Literal =>
               declare
                  Literal_Value : constant Ocarina.AADL_Values.Value_Type :=
                    Ocarina.AADL_Values.Value (BATN.Value (Operand_Node));
               begin
                  if Literal_Value.T = Ocarina.AADL_Values.LT_Integer then
                     Result_Info.Literal_Node := Operand_Node;
                     Result_Info.Is_Negative := Literal_Value.ISign /= Negate;
                     if Result_Info.Is_Negative
                       and then Literal_Value.IVal > Signed_Max + 1
                     then
                        Display_Located_Error
                          (BATN.Loc (Operand_Node),
                           "Integer power literal exceeds signed 64-bit range",
                           Fatal => True);
                     end if;
                     Result_Info.Is_Signed := Result_Info.Is_Negative
                       or else Literal_Value.IVal <= Signed_Max;
                     --  Literals have no classifier.  Give them an explicit
                     --  64-bit integer type, independent of native int size.
                     Result_Info.Type_Name := Get_String_Name
                       (if Result_Info.Is_Signed then "int64_t"
                        else "uint64_t");
                     return Result_Info;
                  end if;
               end;
            when BATN.K_Identifier =>
               if not Unary_Sign and then Present (Subprogram_Root) then
                  Variable_Node := Find_BA_Variable
                    (Operand_Node, Get_Behavior_Specification
                       (Subprogram_Root));
                  if Present (Variable_Node) then
                     Data_Instance := AAN.Default_Instance
                       (BATN.Corresponding_Declaration
                          (BATN.Classifier_Ref (Variable_Node)));
                     if Present (Data_Instance) and then
                       Get_Data_Representation (Data_Instance) = Data_Integer
                     then
                        Result_Info.Type_Name := CTN.Name
                          (Map_C_Data_Type_Designator (Data_Instance));
                        --  The existing C mapping uses signed int when no
                        --  size is specified, irrespective of Number_Rep.
                        Result_Info.Is_Signed :=
                          Get_Data_Size (Data_Instance).S = 0 or else
                          Get_Number_Representation (Data_Instance) = Signed;
                        return Result_Info;
                     end if;
                  end if;
               end if;
            when BATN.K_Property_Constant =>
               return Operand_Type
                 (BATN.Identifier (Operand_Node), Negate, Unary_Sign);
            when BATN.K_Value_Variable =>
               if not BATN.Is_Count (Operand_Node)
                 and then not BATN.Is_Fresh (Operand_Node)
                 and then not BATN.Is_Updated (Operand_Node)
               then
                  return Operand_Type
                    (BATN.Identifier (Operand_Node), Negate, Unary_Sign);
               end if;
            when BATN.K_Name =>
               if BANu.Is_Empty (BATN.Array_Index (Operand_Node)) then
                  Items := BATN.Idt (Operand_Node);
               end if;
            when BATN.K_Data_Component_Reference =>
               Items := BATN.Identifiers (Operand_Node);
            when BATN.K_Value_Expression =>
               Items := BATN.Relations (Operand_Node);
            when BATN.K_Relation =>
               Items := BATN.Simple_Exprs (Operand_Node);
            when BATN.K_Simple_Expression =>
               Items := BATN.Term_And_Operator (Operand_Node);
               if BANu.Length (Items) = 2 then
                  First_Item := BATN.First_Node (Items);
                  if BATN.Kind (First_Item) = BATN.K_Operator then
                     if Evaluate_BA_Operator (First_Item) = Op_Minus then
                        return Operand_Type
                          (BATN.Next_Node (First_Item), not Negate, True);
                     elsif Evaluate_BA_Operator (First_Item) = Op_Plus then
                        return Operand_Type
                          (BATN.Next_Node (First_Item), Negate, True);
                     end if;
                  end if;
               end if;
            when BATN.K_Term =>
               Items := BATN.Factors (Operand_Node);
            when BATN.K_Factor =>
               if No (BATN.Upper_Value (Operand_Node))
                 and then not BATN.Is_Not (Operand_Node)
                 and then not BATN.Is_Abs (Operand_Node)
               then
                  return Operand_Type
                    (BATN.Lower_Value (Operand_Node), Negate, Unary_Sign);
               end if;
            when others =>
               null;
         end case;
         if not BANu.Is_Empty (Items) and then BANu.Length (Items) = 1 then
            return Operand_Type
              (BATN.First_Node (Items), Negate, Unary_Sign);
         end if;
         Display_Located_Error
           (BATN.Loc (Operand_Node),
            "Cannot infer integer power operand type: use a declared "
            & "BA integer variable or an integer literal; compound "
            & "operands are not supported", Fatal => True);
         return Result_Info;
      end Operand_Type;

      function Literal_Expression (Info : Power_Operand) return Node_Id is
         Magnitude : constant Unsigned_Long_Long := Ocarina.AADL_Values.Value
           (BATN.Value (Info.Literal_Node)).IVal;
         Literal_Expr : Node_Id;
      begin
         Add_Include
           (Make_Include_Clause (Identifier ("stdint"), Local => False));
         if Info.Is_Negative then
            --  Spell the signed minimum without an out-of-range positive
            --  signed token or a negation of the signed minimum itself.
            Literal_Expr := Make_Expression
              (Integer_Literal (0), Op_Minus, Make_Call_Profile
                 (Identifier ("INT64_C"), Make_List_Id
                    (Integer_Literal
                       (Unsigned_Long_Long'Min (Magnitude, Signed_Max)))));
            if Magnitude > Signed_Max then
               Literal_Expr := Make_Expression
                 (Literal_Expr, Op_Minus, Integer_Literal (1));
            end if;
            return Literal_Expr;
         end if;
         return Make_Call_Profile
           (Identifier (if Info.Is_Signed then "INT64_C" else "UINT64_C"),
            Make_List_Id (Integer_Literal (Magnitude)));
      end Literal_Expression;

      function Ensure_Power_Helper return Name_Id is
         Helper_Name : constant Name_Id := Get_String_Name
           ("ocarina_ba_power_" & Get_Name_String (Base_Info.Type_Name)
            & "_by_" & Get_Name_String (Exponent_Info.Type_Name));
         Existing_Node : Node_Id := CTN.First_Node
           (CTN.Declarations (Current_File));
         Params : constant List_Id := New_List (CTN.K_Parameter_List);
         Locals : constant List_Id := New_List (CTN.K_Declaration_List);
         Body_Stmts : constant List_Id := New_List (CTN.K_Statement_List);
         Loop_Stmts : constant List_Id := New_List (CTN.K_Statement_List);
         Multiply_Stmts : constant List_Id := New_List (CTN.K_Statement_List);
         Square_Stmts : constant List_Id := New_List (CTN.K_Statement_List);
         Failure_Stmts : constant List_Id := New_List (CTN.K_Statement_List);

         function Product (Left_Name : String) return Node_Id is
            Left_Value : Node_Id := Identifier (Left_Name);
         begin
            if not Base_Info.Is_Signed then
               --  Prevent narrow unsigned operands from promoting to signed
               --  int for multiplication.  Assignment restores their width.
               Add_Include
                 (Make_Include_Clause (Identifier ("stdint"), Local => False));
               Left_Value := Make_Type_Conversion
                 (Identifier ("uintmax_t"), Left_Value);
            end if;
            return Make_Expression
              (Left_Value, Op_Asterisk, Identifier ("ba_power_base"));
         end Product;
      begin
         --  Keep one helper per pair of declared C types in this source.
         while Present (Existing_Node) loop
            if CTN.Kind (Existing_Node) = CTN.K_Function_Implementation
              and then CTN.Name
                (CTN.Defining_Identifier (CTN.Specification (Existing_Node))) =
                  Helper_Name
            then
               return Helper_Name;
            end if;
            Existing_Node := CTN.Next_Node (Existing_Node);
         end loop;
         Append_Node_To_List
           (Make_Parameter_Specification
              (Identifier ("ba_power_base"),
               Make_Defining_Identifier (Base_Info.Type_Name, False)), Params);
         Append_Node_To_List
           (Make_Parameter_Specification
              (Identifier ("ba_power_exponent"), Make_Defining_Identifier
                 (Exponent_Info.Type_Name, False)), Params);
         Append_Node_To_List
           (Make_Variable_Declaration
              (Identifier ("ba_power_result"), Make_Defining_Identifier
                 (Base_Info.Type_Name, False),
               Value => Integer_Literal (1)), Locals);
         Append_Node_To_List
           (Message_Comment
              ("Integer power evaluates each operand once at the call site "
               & "and preserves the declared base and exponent types."),
            Body_Stmts);
         if Exponent_Info.Is_Signed then
            Add_Include
              (Make_Include_Clause (Identifier ("stdlib"), Local => False));
            Append_Node_To_List
              (Message_Comment
                 ("Negative exponents are outside this integer helper's "
                  & "supported domain; reject before any conversion."),
               Body_Stmts);
            Append_Node_To_List
              (Make_Call_Profile (Identifier ("abort")), Failure_Stmts);
            Append_Node_To_List
              (Make_If_Statement
                 (Make_Expression
                    (Identifier ("ba_power_exponent"),
                     Op_Less, Integer_Literal (0)),
                  Failure_Stmts), Body_Stmts);
         end if;
         Append_Node_To_List
           (Make_Assignment_Statement
              (Identifier ("ba_power_result"), Product ("ba_power_result")),
            Multiply_Stmts);
         Append_Node_To_List
           (Make_If_Statement
              (Make_Expression
                 (Identifier ("ba_power_exponent"),
                  Op_Modulo, Integer_Literal (2)),
               Multiply_Stmts), Loop_Stmts);
         Append_Node_To_List
           (Make_Assignment_Statement
              (Identifier ("ba_power_exponent"), Make_Expression
                 (Identifier ("ba_power_exponent"),
                  Op_Slash, Integer_Literal (2))),
            Loop_Stmts);
         Append_Node_To_List
           (Make_Assignment_Statement
              (Identifier ("ba_power_base"), Product ("ba_power_base")),
            Square_Stmts);
         Append_Node_To_List
           (Message_Comment
              ("Skip the last unused square: it could overflow even when "
               & "the final power is representable."), Loop_Stmts);
         Append_Node_To_List
           (Make_If_Statement
              (Identifier ("ba_power_exponent"), Square_Stmts), Loop_Stmts);
         Append_Node_To_List
           (Make_While_Statement
              (Identifier ("ba_power_exponent"), Loop_Stmts), Body_Stmts);
         Append_Node_To_List
           (Make_Return_Statement (Identifier ("ba_power_result")),
            Body_Stmts);
         Append_Node_To_List
           (Make_Function_Implementation
              (Make_Function_Specification
                 (Make_Defining_Identifier (Helper_Name, False), Params,
                  Make_Defining_Identifier (Base_Info.Type_Name, False)),
               Locals, Body_Stmts), CTN.Declarations (Current_File));
         return Helper_Name;
      end Ensure_Power_Helper;
   begin
      if BATN.Is_Not (Node) then
         return Make_Expression
           (Left_Expr => Evaluate_BA_Value
              (BATN.Lower_Value (Node), Is_Out_Parameter,
               Subprogram_Root, Declarations, Statements),
            Operator => CTU.Op_Not);
      elsif No (BATN.Upper_Value (Node)) then
         return Evaluate_BA_Value
           (BATN.Lower_Value (Node), Is_Out_Parameter,
            Subprogram_Root, Declarations, Statements, Is_Put_Value_On_Port);
      end if;

      Base_Info := Operand_Type (BATN.Lower_Value (Node));
      Exponent_Info := Operand_Type (BATN.Upper_Value (Node));
      if Present (Base_Info.Literal_Node) then
         Base_Expr := Literal_Expression (Base_Info);
      else
         Base_Expr := Evaluate_BA_Value
           (BATN.Lower_Value (Node), Is_Out_Parameter, Subprogram_Root,
            Declarations, Statements, Is_Put_Value_On_Port);
      end if;
      if Present (Exponent_Info.Literal_Node) then
         Exponent_Expr := Literal_Expression (Exponent_Info);
      else
         Exponent_Expr := Evaluate_BA_Value
           (BATN.Upper_Value (Node), False, Subprogram_Root,
            Declarations, Statements);
      end if;
      --  Return a call expression, not a precomputed statement, so while
      --  conditions reevaluate the current operands on every iteration.
      return Make_Call_Profile
        (Make_Defining_Identifier (Ensure_Power_Helper, False),
         Make_List_Id (Base_Expr, Exponent_Expr));
   end Evaluate_BA_Factor;

   -----------------------
   -- Evaluate_BA_Value --
   -----------------------

   function Evaluate_BA_Value
     (Node                 : Node_Id;
      Is_Out_Parameter     : Boolean := False;
      Subprogram_Root      : Node_Id := No_Node;
      Declarations         : List_Id;
      Statements           : List_Id;
      Is_Put_Value_On_Port : Boolean := False) return Node_Id
   is
      pragma Assert (BATN.Kind (Node) = BATN.K_Value_Variable
                     or else Kind (Node) = BATN.K_Value_Expression
                     or else Kind (Node) = BATN.K_Literal
                     or else Kind (Node) = BATN.K_Boolean_Literal
                     or else Kind (Node) = BATN.K_Property_Constant
                     or else Kind (Node) = BATN.K_Property_Reference
                     or else Kind (Node) = BATN.K_Identifier);
      result : Node_Id;
   begin

      case BATN.Kind (Node) is

         when BATN.K_Value_Variable     =>
            result := Evaluate_BA_Value_Variable
              (Node         => Node,
               S            => Subprogram_Root,
               Declarations => Declarations,
               Statements   => Statements);

         when BATN.K_Literal              =>
            result := Evaluate_BA_Literal (Node);

         when BATN.K_Boolean_Literal    =>
            result := Evaluate_BA_Boolean_Literal (Node);

         when BATN.K_Property_Constant  =>
            result := Evaluate_BA_Property_Constant
              (Node, Is_Out_Parameter, Subprogram_Root,
               Declarations, Statements, Is_Put_Value_On_Port);

            --  when BATN.K_Property_Reference =>
            --    Evaluate_BA_Property_Reference (Node);

         when BATN.K_Value_Expression   =>
            result := Evaluate_BA_Value_Expression
              (Node             => Node,
               Subprogram_Root  => Subprogram_Root,
               Declarations     => Declarations,
               Statements       => Statements);

         when BATN.K_Identifier           =>
            result := Evaluate_BA_Identifier
              (Node             => Node,
               Subprogram_Root  => Subprogram_Root,
               Declarations     => Declarations,
               Statements       => Statements);

         when others                      =>
            Display_Error ("Mapping of other kinds of BA Value"
                           & " are not yet supported.",
                           Fatal => True);
      end case;

      return result;

   end Evaluate_BA_Value;

   -------------------------------
   -- Evaluate_BA_Integer_Value --
   -------------------------------

   function Evaluate_BA_Integer_Value
     (Node         : Node_Id;
      S            : Node_Id;
      Declarations : List_Id;
      Statements   : List_Id) return Node_Id
   is
      pragma Assert (BATN.Kind (Node) = BATN.K_Integer_Value);
      pragma Assert (BATN.Kind (BATN.Entity (Node)) = K_Value_Variable
                       or else BATN.Kind (BATN.Entity (Node)) = K_Literal
                     or else BATN.Kind (BATN.Entity (Node))
                     = K_Property_Constant);

      Entity_Node : constant Node_Id := BATN.Entity (Node);
      result      : Node_Id;
   begin
      case BATN.Kind (Entity_Node) is
         when K_Value_Variable    =>
            result := Evaluate_BA_Value_Variable
              (Node             => Entity_Node,
               S                => S,
               Declarations     => Declarations,
               Statements       => Statements);

         when K_Literal           =>
            result := Evaluate_BA_Literal (Entity_Node);

         when K_Property_Constant =>
            result := Evaluate_BA_Property_Constant
              (Node             => Entity_Node,
               Subprogram_Root  => S,
               Declarations     => Declarations,
               Statements       => Statements);

         when others              =>
            Display_Error (" Incorrect Integer Value ", Fatal => True);
      end case;

      return result;

   end Evaluate_BA_Integer_Value;

   -------------------------
   -- Evaluate_BA_Literal --
   -------------------------

   function Evaluate_BA_Literal (Node : Node_Id) return Node_Id is
      pragma Assert (BATN.Kind (Node) = BATN.K_Literal);
   begin

      return CTU.Make_Literal (CV.To_C_Value (BATN.Value (Node)));

   end Evaluate_BA_Literal;

   ------------------------------------------------------
   -- Make_Call_Parameter_For_Get_Count_and_Next_Value --
   ------------------------------------------------------

   function Make_Call_Parameter_For_Get_Count_and_Next_Value
     (Node : Node_Id;
      S    : Node_Id) return List_Id
   is
      N               : Node_Id;
      N1              : Name_Id;
      Call_Parameters : List_Id;
   begin

      Call_Parameters := New_List (CTN.K_Parameter_List);

      Set_Str_To_Name_Buffer ("self");
      N1 := Name_Find;
      N := Make_Defining_Identifier (N1);
      Append_Node_To_List (N, Call_Parameters);

      Append_Node_To_List
        (Make_Call_Profile
           (RE (RE_Local_Port),
            Make_List_Id
              (Make_Defining_Identifier
                   (Map_Thread_Port_Variable_Name (S)),
               Make_Defining_Identifier
                 (BATN.Display_Name (Node)))),
         Call_Parameters);
      return Call_Parameters;

   end Make_Call_Parameter_For_Get_Count_and_Next_Value;

   ----------------------------
   -- Make_Get_Count_of_Port --
   ----------------------------

   function Make_Get_Count_of_Port
     (Node             : Node_Id;
      S                : Node_Id) return Node_Id
   is
      pragma Assert (BATN.Kind (Node) = BATN.K_Identifier);

      Call_Parameters : List_Id;
      result          : Node_Id := No_Node;
   begin

      --  Make the call to __po_hi_gqueue_get_count
      --  if it is an event port
      if AAN.Is_Event (BATN.Corresponding_Entity (Node)) then
         Call_Parameters := Make_Call_Parameter_For_Get_Count_and_Next_Value
           (Node, S);

         result := CTU.Make_Call_Profile
           (RE (RE_Gqueue_Get_Count),
            Call_Parameters);
      end if;
      return result;

   end Make_Get_Count_of_Port;

   -----------------------------
   -- Make_Next_Value_of_Port --
   -----------------------------

   procedure Make_Next_Value_of_Port
     (Node             : Node_Id;
      S                : Node_Id;
      Statements       : List_Id)
   is
      pragma Assert (BATN.Kind (Node) = BATN.K_Identifier);

      Call_Parameters : List_Id;
   begin

      --  Make the call to __po_hi_gqueue_next_value
      --  if it is an event port
      if AAN.Is_Event (BATN.Corresponding_Entity (Node)) then
         Call_Parameters := Make_Call_Parameter_For_Get_Count_and_Next_Value
           (Node, S);

         Append_Node_To_List
           (CTU.Make_Call_Profile
              (RE (RE_Gqueue_Next_Value),
               Call_Parameters),
            Statements);
      end if;

   end Make_Next_Value_of_Port;

   -----------------------------------------------
   -- Make_Request_Variable_Name_From_Port_Name --
   -----------------------------------------------

   function Make_Request_Variable_Name_From_Port_Name
     (Port_Name : Name_Id) return Name_Id
   is
      Result : Name_Id;
   begin

      Set_Str_To_Name_Buffer ("__");
      Get_Name_String_And_Append (Port_Name);
      Add_Str_To_Name_Buffer ("_request");

      Result := Name_Find;
      return Standard.Utils.To_Lower (Result);

   end Make_Request_Variable_Name_From_Port_Name;

   ---------------------------------------
   -- Make_Request_Variable_Declaration --
   ---------------------------------------

   procedure Make_Request_Variable_Declaration
     (Declarations : List_Id;
      Port_Name    : Name_Id)
   is
      decl                      : Node_Id;
      request_declaration_exist : Boolean := False;
      Request_Name              : Name_Id;
   begin

      Request_Name := Make_Request_Variable_Name_From_Port_Name (Port_Name);

      decl := CTN.First_Node (Declarations);
      while Present (decl) loop

         if Kind (decl) = CTN.K_Variable_Declaration then

            if Get_Name_String
              (Standard.Utils.To_Lower
                 (CTN.Name (CTN.Defining_Identifier (decl))))
                = Get_Name_String (Request_Name)
            then
               request_declaration_exist := True;
            end if;
         end if;

         exit when request_declaration_exist;
         decl := CTN.Next_Node (decl);
      end loop;

      if not request_declaration_exist then
         CTU.Append_Node_To_List
           (Make_Variable_Declaration
              (Defining_Identifier =>
                   Make_Defining_Identifier (Request_Name),
               Used_Type           => RE (RE_Request_T)),
            Declarations);
      end if;

   end Make_Request_Variable_Declaration;

   ----------------------------
   -- Make_Get_Value_of_Port --
   ----------------------------

   function Make_Get_Value_of_Port
     (Node             : Node_Id;
      Subprogram_Root  : Node_Id;
      Declarations     : List_Id;
      Statements       : List_Id) return Node_Id
   is
      N               : Node_Id;
      N1              : Name_Id;
      result          : Node_Id;
      Call_Parameters : List_Id;
   begin
      --  Read from the in data port
      N := Message_Comment ("Read the data from the port "
                            & Get_Name_String
                              (BATN.Display_Name (Node)));

      Append_Node_To_List (N, Statements);

      Make_Request_Variable_Declaration (Declarations,
                                         BATN.Display_Name (Node));

      --  Make the call to __po_hi_gqueue_get_value

      Call_Parameters := New_List (CTN.K_Parameter_List);

      Set_Str_To_Name_Buffer ("self");
      N1 := Name_Find;
      N := Make_Defining_Identifier (N1);
      Append_Node_To_List (N, Call_Parameters);

      Append_Node_To_List
        (Make_Call_Profile
           (RE (RE_Local_Port),
            Make_List_Id
              (Make_Defining_Identifier
                   (Map_Thread_Port_Variable_Name (Subprogram_Root)),
               Make_Defining_Identifier (BATN.Display_Name (Node)))),
         Call_Parameters);

      N :=
        Make_Variable_Address
          (Make_Defining_Identifier
             (Make_Request_Variable_Name_From_Port_Name
                (BATN.Display_Name (Node))));
      Append_Node_To_List (N, Call_Parameters);

      N :=
        CTU.Make_Call_Profile
          (RE (RE_Gqueue_Get_Value),
           Call_Parameters);
      Append_Node_To_List (N, Statements);

      Call_Parameters := New_List (CTN.K_Parameter_List);

      Append_Node_To_List
        (Make_Defining_Identifier
           (Map_Thread_Port_Variable_Name (Subprogram_Root)),
         Call_Parameters);

      Append_Node_To_List
        (Make_Defining_Identifier (BATN.Display_Name (Node)),
         Call_Parameters);

      result := CTU.Make_Call_Profile
        (Make_Member_Designator
           (Defining_Identifier => RE (RE_PORT_VARIABLE),
            Aggregate_Name      =>
              Make_Defining_Identifier
                (Make_Request_Variable_Name_From_Port_Name
                     (BATN.Display_Name (Node)))),
         Call_Parameters);

      return result;

   end Make_Get_Value_of_Port;

   ----------------------------
   -- Evaluate_BA_Identifier --
   ----------------------------

   function Evaluate_BA_Identifier
     (Node                 : Node_Id;
      Is_Out_Parameter     : Boolean := False;
      Subprogram_Root      : Node_Id := No_Node;
      Declarations         : List_Id;
      Statements           : List_Id;
      Is_Put_Value_On_Port : Boolean := False) return Node_Id
   is
      use AAN;
      pragma Assert (BATN.Kind (Node) = BATN.K_Identifier);
      Pointer          : Boolean := False;
      Variable_Address : Boolean := False;
      N, N1            : Node_Id;
      result           : Node_Id;
   begin

      N := BATN.Corresponding_Entity (Node);
      if Is_Out_Parameter then
         if Find_BA_Variable (Node,
                              Get_Behavior_Specification (Subprogram_Root))
           /= No_Node
         then
            --  The given parameter is a BA variable
            --  then it must be mapped to a variable
            --  address to enable its modification
            --  by the called subprogram
            --
            Variable_Address := True;
         else
            --  The given parameter is an OUT/INOUT parameter
            --  of the subprogram implementing the BA, it is
            --  already a pointer
            --
            Pointer := False;
         end if;
      else
         --  In the case of IN parameter_label, if it contain an
         --  OUT parameter (ex. p1) of the subprogram implementing
         --  the BA, then in the generated code C, in order
         --  to have the value of p1, it must be mapped to *p1
         --
         if Subprogram_Root /= No_Node and then
           AINU.Is_Subprogram (Subprogram_Root)
         then
            declare
               use type AIN.Node_Kind;
               Fs : constant Ocarina.ME_AADL.AADL_Instances.Nutils.Node_Array
                 := Features_Of (Subprogram_Root);
            begin
               for F of Fs loop
                  if Standard.Utils.To_Upper
                    (AIN.Display_Name (AIN.Identifier (F)))
                    = Standard.Utils.To_Upper
                    (BATN.Display_Name (Node))
                    --  and then AIN.Is_Out (F)
                    and then
                      ((AIN.Kind (F) = AIN.K_Parameter_Instance
                        and then AIN.Is_Out (F))
                       or else
                         (AIN.Kind (F) = AIN.K_Subcomponent_Access_Instance
                          and then
                          Get_Required_Data_Access
                            (AIN.Corresponding_Instance (F))
                          /= Access_Read_Only))
                  then
                     Pointer := True;
                  end if;
               end loop;
            end;
         end if;

         if Present (N) then
            if AAN.kind (N) = AAN.K_Subcomponent then
               N1 := AIN.Corresponding_Instance
                 (Get_Subcomponent_Data_Instance
                    (N, Subprogram_Root));
               if AINU.Is_Data (N1) then
                  if Get_Data_Representation (N1) = Data_Struct then
                     if not Is_Put_Value_On_Port then
                        return CTU.Make_Defining_Identifier
                          (Name  => BATN.Display_Name (Node));
                     else
                        Pointer := True;
                     end if;
                  else
                     Pointer := True;
                  end if;
               end if;
            end if;
         end if;
      end if;

      result := CTU.Make_Defining_Identifier
        (Name             => BATN.Display_Name (Node),
         Pointer          => Pointer,
         Variable_Address => Variable_Address);

      if Present (N) then
         if AAN.kind (N) = AAN.K_Port_Spec
           and then AAN.Is_Data (N)
         then
            if AAN.Is_In (N) then
               result := Make_Get_Value_of_Port
                 (Node, Subprogram_Root, Declarations, Statements);
            else
               --  i.e the identifier is an out port

               Make_Request_Variable_Declaration
                 (Declarations,
                  BATN.Display_Name (Node));

               Make_Output_Port_Name
                 (Node         => Node,
                  S            => Subprogram_Root,
                  Statements   => Statements);

               N := Make_Call_Profile
                 (RE (RE_PORT_VARIABLE),
                  Make_List_Id
                    (Make_Defining_Identifier
                         (Map_Thread_Port_Variable_Name (Subprogram_Root)),
                     Make_Defining_Identifier
                       (BATN.Display_Name (Node))));

               result := CTU.Make_Member_Designator
                 (Defining_Identifier => N,
                  Aggregate_Name      =>
                    Make_Defining_Identifier
                      (Make_Request_Variable_Name_From_Port_Name
                           (BATN.Display_Name (Node))));

            end if;
         end if;
      end if;

      return result;

   end Evaluate_BA_Identifier;

   --------------------------------------------
   -- Make_Intermediate_Variable_Declaration --
   --------------------------------------------

   procedure Make_Intermediate_Variable_Declaration
     (N            : Name_Id;
      Used_Type    : Node_Id;
      Declarations : List_Id)
   is
      decl              : Node_Id;
      declaration_exist : Boolean := False;
   begin

      decl := CTN.First_Node (Declarations);
      while Present (decl) loop

         if Kind (decl) = CTN.K_Variable_Declaration then

            if Get_Name_String
              (Standard.Utils.To_Lower
                 (CTN.Name (CTN.Defining_Identifier (decl))))
                = Get_Name_String
              (Standard.Utils.To_Lower (N))

            then
               declaration_exist := True;
            end if;
         end if;

         exit when declaration_exist;
         decl := CTN.Next_Node (decl);
      end loop;

      if not declaration_exist then
         CTU.Append_Node_To_List
           (CTU.Make_Variable_Declaration
              (Defining_Identifier => Make_Defining_Identifier (N),
               Used_Type => Used_Type),
            Declarations);
      end if;

   end Make_Intermediate_Variable_Declaration;

   ------------------------------------
   -- Get_Subcomponent_Data_Instance --
   ------------------------------------

   function Get_Subcomponent_Data_Instance
     (Node             : Node_Id;
      Parent_Component : Node_Id) return Node_Id
   is
      Fs : constant Ocarina.ME_AADL.AADL_Instances.Nutils.Node_Array
        := Subcomponents_Of (Parent_Component);
      result : Node_Id;
   begin
      for F of Fs loop
         if Present (AIN.Identifier (F))
           and then Standard.Utils.To_Upper
             (AIN.Display_Name (AIN.Identifier (F)))
           = Standard.Utils.To_Upper
           (AAN.Display_Name (AAN.Identifier (Node)))
         then
            result := F;
         end if;
      end loop;
      return result;
   end Get_Subcomponent_Data_Instance;

   ----------------------------
   -- Get_Port_Spec_Instance --
   ----------------------------

   function Get_Port_Spec_Instance
     (Node             : Node_Id;
      Parent_Component : Node_Id) return Node_Id
   is
      use type AIN.Node_Kind;
      Fs : constant Ocarina.ME_AADL.AADL_Instances.Nutils.Node_Array
        := Features_Of (Parent_Component);
      result : Node_Id;
   begin
      for F of Fs loop
         if Standard.Utils.To_Upper
           (AIN.Display_Name (AIN.Identifier (F)))
           = Standard.Utils.To_Upper
           (BATN.Display_Name (Node))
           and then
             AIN.Kind (F) = AIN.K_Port_Spec_Instance
           --  and then AIN.Is_In (F)
           --  and then AIN.Is_Data (F)
         then
            result := F;
         end if;
      end loop;
      return result;
   end Get_Port_Spec_Instance;

   --------------------------------
   -- Evaluate_BA_Value_Variable --
   --------------------------------

   function Evaluate_BA_Value_Variable
     (Node             : Node_Id;
      S                : Node_Id;
      Declarations     : List_Id;
      Statements       : List_Id) return Node_Id
   is
      pragma Assert (BATN.Kind (Node) = BATN.K_Value_Variable);

      Ident          : constant Node_Id := BATN.Identifier (Node);
      result         : Node_Id;
      Var_identifier : Node_Id;
      N              : Name_Id;
      Used_Type      : Node_Id;
   begin

      if Is_Interrogative (Node) then
         --  the mapping when Is_Interrogative, i.e. :
         --  incoming_port_name?

         Set_Str_To_Name_Buffer ("__");
         Get_Name_String_And_Append
           (BATN.Display_Name
              (BATN.First_Node
                   (BATN.Idt (BATN.Identifier (Node)))));
         Add_Str_To_Name_Buffer ("_gqueue_get_value");
         N := Name_Find;
         Var_identifier := CTU.Make_Defining_Identifier (N);

         Used_Type := Map_C_Data_Type_Designator
           (AIN.Corresponding_Instance
              (Get_Port_Spec_Instance
                   (Node             => BATN.First_Node
                        (BATN.Idt (BATN.Identifier (Node))),
                    Parent_Component => S)));

         Make_Intermediate_Variable_Declaration
           (N            => N,
            Used_Type    => Used_Type,
            Declarations => Declarations);

         CTU.Append_Node_To_List
           (CTU.Make_Assignment_Statement
              (Variable_Identifier => Var_identifier,
               Expression          => Make_Get_Value_of_Port
                 (BATN.First_Node (BATN.Idt (BATN.Identifier (Node))),
                  S, Declarations, Statements)),
            Statements);

         result := Var_identifier;

         Make_Next_Value_of_Port
           (BATN.First_Node (BATN.Idt (BATN.Identifier (Node))),
            S, Statements);

      elsif Is_Count (Node) then
         --  Here we must add a variable and we assign it with
         --  the returned value of the function __po_hi_gqueue_get_count
         --  int __po_hi_gqueue_get_count
         --     ( __po_hi_task_id id, __po_hi_local_port_t port)

         Set_Str_To_Name_Buffer ("__");
         Get_Name_String_And_Append
           (BATN.Display_Name
              (BATN.First_Node
                   (BATN.Idt (BATN.Identifier (Node)))));
         Add_Str_To_Name_Buffer ("_gqueue_get_count");
         N := Name_Find;
         Var_identifier := CTU.Make_Defining_Identifier (N);

         Make_Intermediate_Variable_Declaration
           (N            => N,
            Used_Type    => RE (RE_Int16_T),
            Declarations => Declarations);

         CTU.Append_Node_To_List
           (CTU.Make_Assignment_Statement
              (Variable_Identifier => Var_identifier,
               Expression          => Make_Get_Count_of_Port
                 (BATN.First_Node
                      (BATN.Idt (BATN.Identifier (Node))), S)),
            Statements);

         result := Var_identifier;

      --  TODO: the mapping for the cases Fresh and Updated
      --  elsif Is_Fresh (Node) then
      --
      --  elsif Is_Updated (Node) then
      --
      elsif BATN.Kind (Ident) = BATN.K_Name then
         result := Map_C_BA_Name
           (Node         => Ident,
            S            => S,
            Declarations => Declarations,
            Statements   => Statements);
      elsif BATN.Kind (Ident) = BATN.K_Data_Component_Reference then
         result := Map_C_Data_Component_Reference
           (Node         => Ident,
            S            => S,
            Declarations => Declarations,
            Statements   => Statements);
      end if;

      return result;

   end Evaluate_BA_Value_Variable;

   -----------------------------------
   -- Evaluate_BA_Property_Constant --
   -----------------------------------

   function Evaluate_BA_Property_Constant
     (Node                 : Node_Id;
      Is_Out_parameter     : Boolean := False;
      Subprogram_Root      : Node_Id;
      Declarations         : List_Id;
      Statements           : List_Id;
      Is_Put_Value_On_Port : Boolean := False) return Node_Id
   is
      pragma Assert (BATN.Kind (Node) = BATN.K_Property_Constant);

   begin
      --  We must treat later the case of Property_Set (Node)
      --
      --        if Present (BATN.Property_Set (Node)) then
      --
      --        end if;

      return Evaluate_BA_Identifier
        (BATN.Identifier (Node), Is_Out_Parameter,
         Subprogram_Root, Declarations, Statements, Is_Put_Value_On_Port);

   end Evaluate_BA_Property_Constant;

   ---------------------------
   -- Print_Boolean_Literal --
   ---------------------------

   function Evaluate_BA_Boolean_Literal (Node : Node_Id) return Node_Id  is
      pragma Assert (BATN.Kind (Node) = BATN.K_Boolean_Literal);
      N : Name_Id;
   begin
      if BATN.Is_True (Node) then
         Set_Str_To_Name_Buffer ("True");
         N := Name_Find;
         return Make_Defining_Identifier (N);
         --  Expr := PHR.RE (PHR.RE_True);
         --  Expr := Make_Literal (CV.New_Int_Value (1, 1, 10));
      else
         Set_Str_To_Name_Buffer ("False");
         N := Name_Find;
         return  Make_Defining_Identifier (N);
         --  Expr := PHR.RE (PHR.RE_False);
         --  Expr := Make_Literal (CV.New_Int_Value (0, 1, 10));
      end if;

   end Evaluate_BA_Boolean_Literal;

end Ocarina.Backends.C_Common.BA;
